Actor SkillCount : Inventory
{
	Inventory.MaxAmount 5
	Inventory.InterHubAmount 0
}

Actor NightmareToken : Inventory
{
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 1
}

Actor HordeModeTest : Inventory
{	Inventory.MaxAmount 1	}

ACTOR L4DDirectorSpawner : CustomInventory
{
	+ALWAYSPICKUP
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Use:
		TNT1 A 0 A_SpawnItemEx("TheDirectorL4D",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_NOPOINTERS|SXF_SETTARGET)
		Stop
	}
}

Actor DirectorIsLeaving : Inventory
{	Inventory.MaxAmount 1	}

Actor BuildupToken : CustomInventory
{
	var int user_buildup;
	+ALWAYSPICKUP
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Use:
		TNT1 A 0 A_SetUserVar("user_buildup",user_buildup+1)
		Stop
	}
}


Actor SmallCDUp : CustomInventory
{
	+ALWAYSPICKUP
	var int user_nightmare;
	States
	{
	Pickup:
		TNT1 A 0 A_SetUserVar(user_nightmare,user_nightmare)
		TNT1 A 0 A_JumpIf(user_nightmare > 0,"NM")
		TNT1 A 0 A_SetUserArray(user_cooldown,1,random(100,350))
		Stop
	NM:
		TNT1 A 0 A_SetUserArray(user_cooldown,1,random(75,200))
		Stop
	}
}
Actor SmallSpawnTokenTwo : Inventory
{
	Inventory.MaxAmount 50
	Inventory.InterHubAmount 50
}

Actor TheDirectorL4D
{
	TeleFogSourceType None
	TeleFogDestType None
	var int user_nightmare;
	var int user_space[3];
	//[0] = x
	//[1] = y
	//[2] = z
	var int user_buildup;
	var int user_cooldown[5];
	var int user_delay;
	//[0] = Normal cooldown
	//[1] = Exclusive small child countdown
	//[2] = Bool [0] on cooldown
	//[3] = Bool [1] on cooldown
	//[2] and [3] are basically checking if the spawners had successfully been
	//deployed. If not, they stay at 0 and no cooldown is initialized.
	var int user_largechildcount;
	var int user_mediumchildcount;
	var int user_smallchildcount;
	var int user_horde[3];
	//[0] = Is it available?
	//[1] = Timer
	//[2] = Cooldown
	Monster
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+NORADIUSDMG
	+THRUACTORS
	+NEVERTARGET
	+NOTARGETSWITCH
	+CANTSEEK
	-FRIENDLY
	-COUNTKILL
	+NOCLIP
	+FLOORHUGGER
	+LOOKALLAROUND
	+NEVERFAST
	+NODAMAGE
	+NONSHOOTABLE
	-SHOOTABLE
	-NODROPOFF
	//+DROPOFF
	+NOTRIGGER
	-TELESTOMP
	+NOTELESTOMP
	+DONTMORPH
	+NOICEDEATH
	Radius 10
	Height 20
	Speed 20
	+DONTTHRUST
	Health 1000000
	DamageFactor "Normal", 0
	Species "Director"
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_ChangeFlag("FRIENDLY",0)
		TNT1 A 0 A_RearrangePointers(AAPTR_PLAYER1,AAPTR_NULL,AAPTR_NULL,3)
		TNT1 A 0 A_SpawnItemEx("L4DPopulater",0,0,0,0,0,0,0,32|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		Goto Idle
	Idle:
		TNT1 A 1 A_Look
		Loop
	Cooling:
		TNT1 A 0 A_JumpIf(user_nightmare == 1, "Cooling2")
		TNT1 A random(200,600) A_Jump(240,"Chasing")
		Goto Chasing
	Cooling2:
		TNT1 A random(350,800) A_Jump(250,"Chasing")
		Goto Chasing
	See:
		TNT1 AAA 350 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_PLAYER1) //The original director has 60 seconds. This one is 30.
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,"EnableNightmare",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("PFirstTimeToken2",1,"FirstTime",AAPTR_PLAYER1)
		Goto Chasing
	//Nightmare mode effectively causes regular zombies to spawn roughly twice as fast.
	EnableNightmare:
		TNT1 A 0 A_SetUserVar("user_nightmare",1)
		Goto See+4
	FirstTime:
		TNT1 A 0 A_TakeInventory("PFirstTimeToken2",1,0,AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("SmallSpawnTokenTwo",50,AAPTR_PLAYER1)
	Chasing:
		TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TARGET,AAPTR_PLAYER1),2)
		TNT1 A 0 A_RearrangePointers(AAPTR_PLAYER1,AAPTR_DEFAULT,AAPTR_DEFAULT,4) //Always ensure the player is the target.
		TNT1 A 0 A_JumpIfInventory("HordeModeTest",1,"HordeModeForce",AAPTR_PLAYER1)
		TNT1 AAA 2 A_Chase
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 0 A_JumpIf(user_cooldown[0] <= 0,2)
		TNT1 A 0 A_SetUserArray("user_cooldown",0,user_cooldown[0]-1)
		TNT1 A 0 A_JumpIf(user_cooldown[1] <= 0,4)
		TNT1 A 0 A_JumpIf(user_nightmare == 0,2)
		TNT1 AA 0 A_SetUserArray("user_cooldown",1,user_cooldown[1]-1)
		TNT1 A 0 A_JumpIf(user_nightmare == 0,2)
		TNT1 AA 0 A_SetUserArray("user_cooldown",3,user_cooldown[3]-1)
		Loop
	HordeModeForce:
		TNT1 A 0 A_TakeInventory("HordeModeTest",1000,0,AAPTR_PLAYER1)
		TNT1 A 0 A_SetUserArray("user_horde",2,0) //Wipe it clean for testing purposes.
		TNT1 A 0 A_SetUserArray("user_cooldown",0,0)
		TNT1 A 0 A_SetUserArray("user_cooldown",1,0)
		TNT1 A 0 A_SetUserArray("user_cooldown",3,0)
		Goto HordeMode
	HordeMode:
		TNT1 A 0 A_JumpIf(user_cooldown[3] >= 1,"SmallerMobPrep")
		TNT1 A 0 A_PlaySound("Director/Horde",5,1,0,ATTN_NONE)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_GiveInventory("HordeToken",1)
		TNT1 AAAAAA 0 A_SpawnItemEx("L4DSmallMobRunner",0,0,0,0,0,0,random(0,359),32|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SetUserArray("user_cooldown",3,35*random(15,45))
		TNT1 A 0 A_JumpIf(user_nightmare == 0,2)
		TNT1 A 0 A_SetUserArray("user_cooldown",3,35*random(25,70)) //Drains twice as fast, have to give some benefit of the doubt here.
		TNT1 AAAAA 35
		TNT1 A 0 A_TakeInventory("HordeToken",1)
		Goto Chasing
	Missile:
		TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION)
		TNT1 A 0 A_JumpIfInventory("SmallSpawnTokenTwo",1,"SmallerMobPrep",AAPTR_PLAYER1)
		TNT1 A 3 A_FaceTarget
		TNT1 A 0 A_Jump(2,"LargeMobshoot","HordeMode","HordeMode") //HordeMode listed twice was done on purpose.
		TNT1 A 0 A_Jump(6,"MediumMobshoot")
		Goto SmallerMobPrep
	LargeMobshoot:
		TNT1 A 0 A_JumpIf(user_cooldown[0]>=1,"Chasing")
		TNT1 A 1 A_SpawnItemEx("L4DLargeMobRunner",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		Goto Cooling
	MediumMobshoot:
		TNT1 A 0 A_JumpIf(user_cooldown[0]>=1,"Chasing")
		TNT1 A 1 A_SpawnItemEx("L4DMediumMobRunner",0,0,1,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		Goto Cooling
	SmallMobshoot:
	SmallerMobPrep:
		TNT1 A 0 A_JumpIf(user_cooldown[1]>=1,"Chasing")
		TNT1 A 0 A_TakeInventory("SmallSpawnTokenTwo",1,0,AAPTR_PLAYER1)
		TNT1 A 0 A_FaceTarget
		Goto SmallMobShoot
		TNT1 A 1 A_SpawnItemEx("L4DSmallMobRunner",0,0,1,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		//Note that this cooldown is exclusive and runs individually unlike the other.
		TNT1 A 0 A_SetUserArray("user_cooldown",1,random(75,200)) 
		TNT1 A 0 A_JumpIf(user_nightmare==1,1)
		Goto Cooling
		TNT1 A 0 A_SetUserArray("user_cooldown",1,random(100,350))
		Goto Cooling
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		Goto Cooling
	Death:
		TNT1 A 35 A_GiveInventory("DirectorIsLeaving",1)
		TNT1 A 0 A_TakeInventory("RemoveDirector",32767,0,AAPTR_TARGET)
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING,"None","Director")
		Stop
	}
}

Actor L4DPopulater
{
	var int user_wander;
	var int user_populate;
	var int user_cooldown;
	TeleFogSourceType None
	TeleFogDestType None
	Monster
	-TELESTOMP
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+NORADIUSDMG
	+THRUACTORS
	+NEVERTARGET
	+NOTARGETSWITCH
	+CANTSEEK
	-FRIENDLY
	-COUNTKILL
	+LOOKALLAROUND
	+NEVERFAST
	+NODAMAGE
	+DONTSPLASH
	+NONSHOOTABLE
	-SHOOTABLE
	-NODROPOFF
	+DROPOFF
	+NOTRIGGER
	+NOBLOCKMAP
	+NOTELESTOMP
	+NOTONAUTOMAP
	+DONTTHRUST
	Radius 16
	Height 64
	Speed 20
	RenderStyle None
	Species "Director"
	States
	{
	Spawn:
		TNT1 A 0
	Idle:
		TNT1 A 1 A_Look
		Loop
	See:
	Wandering:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Removal",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("DirectorIsLeaving",1,"Removal",AAPTR_MASTER)
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SetUserVar("user_wander",user_wander+1)
		TNT1 A 0 A_JumpIf(user_wander>=900,"Populate")
		Loop
	Populate:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Removal",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		TNT1 A 0 A_SetUserVar("user_wander",0)
		TNT1 A 0 A_JumpIfCloser(250,"NoPop")
		TNT1 A 0 A_CheckSight(1)
		Goto NoPop
		TNT1 A 1 A_SpawnItemEx("L4DCommonSpawner",random(60,-60),random(-60,60),0,0,0,0,random(0,359),SXF_TRANSFERPOINTERS,210)
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,random(15,150),0,0,random(0,359))
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10)
		TNT1 AAAAAAAAAA 1 A_Wander
		TNT1 A 0 A_SetUserVar("user_populate",user_populate+1)
		TNT1 A 0 A_JumpIf(user_populate>=random(15,45),1)
		Loop
		TNT1 A 0 A_SetUserVar("user_populate",0)
	MoveBack:
		TNT1 A 0 A_Warp(AAPTR_PLAYER1,0,0,0,0,WARPF_NOCHECKPOSITION)
		Goto Wandering
	NoPop:
		TNT1 A 0 A_Jump(256,1,2,3,4,5)
		TNT1 AAAAAAA 1 A_Wander
		Goto Populate
	Frozen:
		TNT1 A 0 A_JumpIfInventory("DirectorIsLeaving",1,"Removal",AAPTR_MASTER)
		TNT1 A 35 A_JumpIfInventory("RemoveDirector",1,"Removal",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		Goto Wandering
	Removal:
	Death:
		TNT1 A 0
		Stop
	}
}


Actor L4DLargeMobRunner
{
	Monster
	var int user_check[2];
	var int user_life;
	var int user_x[2];
	var int user_y[2];
	var int user_z[2];
	-SOLID
	+NEVERTARGET
	-COUNTKILL
	+CANTSEEK
	+FLOORHUGGER
	+NORADIUSDMG
	+THRUACTORS
	+NOBLOOD
	-TELESTOMP
	+LOOKALLAROUND
	+NOTARGETSWITCH
	+NONSHOOTABLE
	-SHOOTABLE
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+NOBLOCKMONST
	+CANTLEAVEFLOORPIC
	+NOTRIGGER
	+NOBLOCKMAP
	+NOTELESTOMP
	+NODAMAGE
	+DONTSPLASH
	RenderStyle None
	MaxDropOffHeight 128
	MaxStepHeight 128
	+DONTTHRUST
	Speed 14
	Radius 60
	Height 60
	Health 1000000
	DamageFactor "Normal", 0.0
	DamageFactor "DirectorKiller", 32767
	Species "Director"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIfInventory("HordeToken",1,"Idle",AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("CooldownEnable1",1,AAPTR_MASTER)
	Idle:
		TNT1 A 0 A_ChangeFlag("FRIENDLY",0)
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_RearrangePointers(AAPTR_PLAYER1,AAPTR_NULL,AAPTR_DEFAULT)
		TNT1 AAAAA 1 A_Look
		Loop
	See:
	Wanderbout:
		TNT1 A 0 A_SetUserArray(user_x,0,x)
		TNT1 A 0 A_SetUserArray(user_y,0,y)
		TNT1 A 0 A_SetUserArray(user_z,0,z)
		TNT1 A 0 Thing_ChangeTID(0,0)
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 1 A_Chase
		TNT1 AAAAAAAAAAAA 1 A_Wander
		TNT1 A 0 A_SetUserVar(user_life,user_life+1)
		TNT1 A 0 A_JumpIf(user_life > 200, "End")
		TNT1 A 0 A_SetUserArray(user_x,1,x)
		TNT1 A 0 A_SetUserArray(user_y,1,y)
		TNT1 A 0 A_SetUserArray(user_z,1,z)
		TNT1 A 0 A_JumpIf((user_x[0] == user_x[1]) && (user_y[0] == user_y[1]) && (user_z[0] == user_z[1]),"Stuck")
	Mobmaking:
		TNT1 A 0 A_JumpIfInventory("HordeToken",1,2,AAPTR_MASTER)
		TNT1 A 0 A_JumpIfCloser(250,"Wanderbout")
		TNT1 A 0 A_CheckSight(1)
		Goto Wanderbout
		TNT1 A 0 A_JumpIfInventory("HordeToken",1,1,AAPTR_MASTER)
		Goto MobChance
	MobChance:
		TNT1 A 0 Thing_ChangeTID(0,32668)
		TNT1 A 0 A_Jump(128,2,3)
		TNT1 A 0 A_SpawnItemEx("L4DTank",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto Vanish
		TNT1 A 0 A_SpawnItemEx("L4DWitchSitting",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto Vanish
		TNT1 A 0 A_SpawnItemEx("L4DWitchWander",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto Vanish
	Vanish:
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 AAAAAAAA 0 A_Wander
		TNT1 A 0 A_JumpIfInventory("HordeToken",1,"VanishHorde",AAPTR_MASTER)
		TNT1 A 0 A_GiveInventory("CooldownDisable1",1,AAPTR_MASTER)
		TNT1 A 0 A_GiveInventory("LargeCooldown",5,AAPTR_MASTER)
		Goto VanishC
	VanishHorde:
		TNT1 A 1
		Goto Death
	VanishC:
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",1,2)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck")
		TNT1 A 0 A_DropItem("L4DPackageSpawnerCheck",1,128)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck",1,64)
		TNT1 A 0 A_SpawnItemEx("Megasphere",0,0,0,0,0,0,0,32,190)
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,32,160)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerReal",0,0,0,0,0,0,0,32,64)
		TNT1 A 0
		Stop
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		Goto See
	Stuck:
		TNT1 A 0 A_GiveInventory("CooldownDisable1",1,AAPTR_MASTER)
		Stop
	Death:
	End:
		TNT1 A 0
		Stop
	}
}

Actor L4DMediumMobRunner
{
	TeleFogSourceType None
	TeleFogDestType None
	var int user_dlimit;
	var int user_limit;
	var int user_life;
	var int user_x[2];
	var int user_y[2];
	var int user_z[2];
	Monster
	+NEVERTARGET
	-COUNTKILL
	+CANTSEEK
	+FLOORHUGGER
	+NORADIUSDMG
	+THRUACTORS
	+NOBLOOD
	-TELESTOMP
	//+MBFBOUNCER
	+FORCEPAIN
	+LOOKALLAROUND
	+NOTARGETSWITCH
	+NONSHOOTABLE
	-SHOOTABLE
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	-FRIENDLY
	+NOBLOCKMONST
	+CANTLEAVEFLOORPIC
	+NOTRIGGER
	+NOBLOCKMAP
	+NOTELESTOMP
	+DONTSPLASH
	RenderStyle None
	MaxDropOffHeight 128
	MaxStepHeight 128
	+DONTTHRUST
	Speed 14
	Health 1000000
	DamageFactor "Normal", 0.0
	DamageFactor "DirectorKiller", 32767
	Species "Director"
	Radius 40
	Height 60
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_limit",random(1,2))
	Idle:
		TNT1 A 0 A_ChangeFlag("FRIENDLY",0)
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_RearrangePointers(AAPTR_PLAYER1,AAPTR_NULL,AAPTR_DEFAULT)
		TNT1 AAAAA 1 A_Look
		Loop
	See:
	Wanderbout:
		TNT1 A 0 A_SetUserArray(user_x,0,x)
		TNT1 A 0 A_SetUserArray(user_y,0,y)
		TNT1 A 0 A_SetUserArray(user_z,0,z)
		TNT1 A 0 A_RearrangePointers(AAPTR_PLAYER1,AAPTR_NULL,AAPTR_DEFAULT)
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 1 A_Chase
		TNT1 AAAAAAAAAAAA 1 A_Wander
		TNT1 A 0 A_SetUserVar(user_life,user_life+1)
		TNT1 A 0 A_JumpIf(user_life > 70, "End")
		TNT1 A 0 A_SetUserArray(user_x,1,x)
		TNT1 A 0 A_SetUserArray(user_y,1,y)
		TNT1 A 0 A_SetUserArray(user_z,1,z)
		TNT1 A 0 A_JumpIf((user_x[0] == user_x[1]) && (user_y[0] == user_y[1]) && (user_z[0] == user_z[1]),"Stuck")
	Mobmaking:
		TNT1 A 0 A_JumpIfTargetInLOS("Wanderbout",360)
		TNT1 A 0 A_JumpIfCloser(250,"Wanderbout")
	MobChance:
		TNT1 A 0 Thing_ChangeTID(0,32669)
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,"Wanderbout")
		TNT1 A 0 A_SpawnItemEx("L4DJockey",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DSmoker",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DSpitter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DHunter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DCharger",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
	CountUp:
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 A 0 A_SetUserVar("user_dlimit",user_dlimit+1)
		TNT1 A 0 A_JumpIf(user_dlimit>=user_limit,"VanishC")
		Goto Wanderbout
	Vanish:
		TNT1 A 0 A_GiveInventory("CooldownDisable1",1,AAPTR_MASTER)
		TNT1 A 0 A_GiveInventory("MediumCooldown",1,AAPTR_MASTER)
		TNT1 A 0 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,0),"VanishC")
	VanishHorde:
		TNT1 A 1
		Stop
	VanishC:
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",1,1)
		TNT1 A 0 A_DropItem("Megasphere",1,32)
		TNT1 A 0 A_DropItem("SoulSphere",1,64)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerReal",0,128)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck")
		TNT1 A 0 A_DropItem("L4DPackageSpawnerCheck",0,64)
		Stop
		TNT1 A 0 
		Stop
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		Goto See
	Stuck:
		TNT1 A 0 A_JumpIf(user_dlimit > 0, "VanishC")
		Stop
	Death:
	End:
		TNT1 A 0
		Stop
	}
}

Actor L4DSmallMobRunner
{
	var int user_dlimit;
	var int user_max;
	var int user_check[2];
	var int user_nightmare;
	var int user_life;
	var int user_x[2];
	var int user_y[2];
	var int user_z[2];
	TeleFogSourceType None
	TeleFogDestType None
	Monster
	+DONTSPLASH
	+NEVERTARGET
	-COUNTKILL
	+CANTSEEK
	+FLOORHUGGER
	+NORADIUSDMG
	+THRUACTORS
	+NOBLOOD
	-TELESTOMP
	//+MBFBOUNCER
	+FORCEPAIN
	+LOOKALLAROUND
	+NOTARGETSWITCH
	+NONSHOOTABLE
	-SHOOTABLE
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+NOBLOCKMONST
	+CANTLEAVEFLOORPIC
	+NOTRIGGER
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	MaxDropOffHeight 128
	MaxStepHeight 128
	+DONTTHRUST
	Speed 14
	Health 1000000
	DamageFactor "Normal", 0.0
	DamageFactor "DirectorKiller", 32767
	Species "Director"
	Radius 20
	Height 64
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_GiveInventory("SmallCDUp",1,AAPTR_MASTER)
		TNT1 A 0 A_SetUserVar("user_dlimit",0)
		TNT1 A 0 A_SetUserVar("user_max",random(7,15))
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,1,AAPTR_PLAYER1)
		Goto Idle
		TNT1 A 0 A_SetUserVar("user_max",random(15,35))
		Goto Idle
	Idle:
		TNT1 AAAAA 1 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("HordeToken",1,"MobHorde",AAPTR_MASTER)
	Wanderbout:
		TNT1 A 0 A_SetUserArray(user_x,0,x)
		TNT1 A 0 A_SetUserArray(user_y,0,y)
		TNT1 A 0 A_SetUserArray(user_z,0,z)
		TNT1 A 1 A_Chase
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,6,AAPTR_PLAYER1)
		TNT1 AAAAAAAAAAAA 1 A_Wander
		TNT1 A 0 A_SetUserVar(user_life,user_life+1)
		TNT1 A 0 A_JumpIf(user_life > 70, "End")
		TNT1 A 0 A_SetUserArray(user_x,1,x)
		TNT1 A 0 A_SetUserArray(user_y,1,y)
		TNT1 A 0 A_SetUserArray(user_z,1,z)
		TNT1 A 0 A_JumpIf((user_x[0] == user_x[1]) && (user_y[0] == user_y[1]) && (user_z[0] == user_z[1]),"Stuck")
	Mobmaking:
		TNT1 A 0 A_JumpIfTargetInLOS("Wanderbout",360)
		TNT1 A 0 A_JumpIfCloser(250,"Wanderbout")
	MobChance:
		TNT1 A 0 Thing_ChangeTID(0,32670)
		TNT1 A 0 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,0),"MobChanceNA")
		TNT1 A 0 A_SpawnItemEx("L4DCommonSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		//This is the only means to check if the spawn was successful or not.
		//If the TID is cleared, then continue onwards. If not, it failed and force
		//abortion so it can try again without penalty.
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 AA 0 A_SpawnItemEx("L4DCommonSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,200)
		Goto CountUp
	MobChanceNA:
		TNT1 A 0 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 AA 0 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,200)
		Goto CountUp
	MobHorde:
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,"MobHordeNightmare",AAPTR_PLAYER1)
		TNT1 A 1 A_Wander
		TNT1 A 0 A_JumpIfTargetInLOS("MobHorde",360)
		TNT1 A 0 A_JumpIfCloser(250,"MobHorde")
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS)
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,128)
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,192)
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",0,1)
		TNT1 A 0 A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DGeneralWeaponSpawner",0,224)
		Goto MHCont
		TNT1 A 0 A_DropItem("L4DJokeWeaponSpawner",0,224)
	MHCont:
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,128)
		TNT1 A 0 A_DropItem("SoulSphere",0,26)
		TNT1 AAAAAAA 0 A_DropItem("Medikit",0,224)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerReal",1,128)
		Stop
	MobHordeNightmare:
		TNT1 A 1 A_Wander
		TNT1 A 0 A_JumpIfTargetInLOS("MobHordeNightmare",360)
		TNT1 A 0 A_JumpIfCloser(250,"MobHordeNightmare")
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS)
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,128)
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,192)
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",1,1)
		TNT1 A 0 A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DGeneralWeaponSpawner",0,224)
		Goto MHNCont
		TNT1 A 0 A_DropItem("L4DJokeWeaponSpawner",0,224)
	MHNCont:
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,192)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,192)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,192)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,192)
		TNT1 A 0 A_DropItem("SoulSphere",0,32)
		TNT1 AAAAAAA 0 A_DropItem("Medikit",0,224)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerReal",0,128)
		Stop
	CountUp:
		TNT1 A 0 A_SetUserVar("user_dlimit",user_dlimit+1)
		TNT1 A 0 A_JumpIf(user_dlimit>=user_max,"Vanish")
		Goto Wanderbout
	Vanish:
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,"VanishNightmare",AAPTR_PLAYER1)
		TNT1 A 1 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,0),1)
		//TNT1 A 0 //A_JumpIfInventory("DirectorMasterToken",1,1,AAPTR_MASTER)
		Goto Death
		TNT1 A 0 A_DropItem("L4DPackageSpawnerCheck")
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,32,230)
		//TNT1 AAAA 0 A_SpawnItemEx("Medikit",0,0,0,0,0,0,0,32,32)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerReal",0,0,0,0,0,0,0,32,190)
		Stop
	VanishNightmare:
		TNT1 A 0 A_Jump(32,2)
		TNT1 A 1 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,0),1)
		Goto Death
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck")
		TNT1 A 0 A_DropItem("SoulSphere",1,66)
		TNT1 A 0 A_DropItem("Megasphere",1,11)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerReal",1,160)
		TNT1 A 0
		Stop
	Stuck:
		TNT1 A 0 A_JumpIf(user_dlimit >= (user_max / 2),"Vanish")
		Stop
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"End",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		Goto See
	Death:
	End:
		TNT1 A 0
		Stop
	}
}

Actor L4DPackageSpawnerMMCheck
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMM",1)
		Stop
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMJ",1)
		Stop
	}
}

Actor L4DPackageSpawnerCheck
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DPackageSpawner",1)
		Stop
		TNT1 A 0 A_DropItem("L4DPackageSpawnerJ",1)
		Stop
	}
}

Actor L4DAmmoPack
{
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 
		TNT1 A 0 A_DropItem("ShellBoxReplacer",0,128)
		TNT1 A 0 A_DropItem("ClipBoxReplacer",0,128)
		TNT1 A 0 A_DropItem("RocketBoxReplacer",0,94)
		TNT1 A 0 A_DropItem("CellPackReplacer",0,94)
	HP:
		TNT1 AAAA 0 A_DropItem("Medikit",0,32)
		Stop
	}
}

Actor L4DAmmoPackMM
{
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,94)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,94)
	HP:
		TNT1 AAAA 0 A_DropItem("Medikit",0,32)
		Stop
	}
}

Actor L4DHealthPack
{
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
	HP1:
		TNT1 AAAA 0 A_DropItem("Medikit",192)
	AmmoPile:
		TNT1 A 0 A_DropItem("ShellBoxReplacer",0,32)
		TNT1 A 0 A_DropItem("ClipBoxReplacer",0,32)
		TNT1 A 0 A_DropItem("RocketBoxReplacer",0,16)
		TNT1 A 0 A_DropItem("CellPackReplacer",0,16)
		Stop
	}
}

Actor L4DHealthPackMM
{
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
	HP1:
		TNT1 AAAAAAAA 0 A_DropItem("Medikit",1,128)
	HP2:
		TNT1 AAAAAAAAAAAAAA 0 A_DropItem("HealthBonus",1,128)
	AmmoPile:
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,24)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,24)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,12)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,12)
		Stop
	}
}

Actor L4DPackageSpawner : RandomSpawner
{
	//Each one of them have an extreme biasity towards their name.
	DropItem "L4DAmmoPack" 255 28
	DropItem "L4DHealthPack" 255 14
	DropItem "L4DGeneralWeaponSpawner" 255 12
}

Actor L4DPackageSpawnerMM : RandomSpawner
{
	//Each one of them have an extreme biasity towards their name.
	DropItem "L4DAmmoPackMM" 255 20
	DropItem "L4DHealthPackMM" 255 12
	DropItem "L4DGeneralWeaponSpawner" 255 12
}

Actor L4DPackageSpawnerJ : RandomSpawner
{
	DropItem "L4DAmmoPack" 255 28
	DropItem "L4DHealthPack" 255 14
	DropItem "L4DJokeWeaponSpawner" 255 12
}

Actor L4DPackageSpawnerMMJ : RandomSpawner
{
	DropItem "L4DAmmoPackMM" 255 20
	DropItem "L4DHealthPackMM" 255 12
	DropItem "L4DJokeWeaponSpawner" 255 12
}

Actor HordeToken : Inventory
{	Inventory.MaxAmount 6	}

Actor L4DGeneralWeaponSpawner : RandomSpawner
{
	DropItem "Slot1Weps" 255 16
	DropItem "Slot2Weps" 255 32
	DropItem "Slot3Weps" 255 32
	DropItem "Slot4Weps" 255 32
	DropItem "Slot5Weps" 255 24
	DropItem "Slot6Weps" 255 16
	DropItem "Slot7Weps" 255 12
	DropItem "Slot8Weps" 255 8
	DropItem "Slot9Weps" 255 4
	DropItem "Slot0Weps" 255 2
}

Actor L4DJokeWeaponSpawner : RandomSpawner
{
	DropItem "Slot1Weps" 255 16
	DropItem "Slot2WepsJ" 255 32
	DropItem "Slot3Weps" 255 32
	DropItem "Slot4WepsJ" 255 32
	DropItem "Slot5Weps" 255 24
	DropItem "Slot6WepsJ" 255 16
	DropItem "Slot7Weps" 255 12
	DropItem "Slot8WepsJ" 255 8
	DropItem "Slot9WepsJ" 255 4
	DropItem "Slot0Weps" 255 2
}

Actor Slot1Weps : RandomSpawner
{
	DropItem "AEoDProxDetonatorItem" 255 10
	DropItem "AEoDTNTItem" 255 10
	DropItem "AEoDHandgrenadeItem" 255 5
	DropItem "AEoDStickyMineItem" 255 5
	DropItem "AEoDRemoteDetonatorItem" 5
	DropItem "AEoDGlyphItem" 255 2
	DropItem "AEoDBoneStaffItem" 255 2 //Added as of 6.03PR3. Why? Demon Orb. That is all.
	DropItem "AEoDJackBombItem" 255 1
}

Actor Slot2Weps : RandomSpawner
{
	DropItem "AEoDKatanaItem" 255 10
	DropItem "AEoDMedievalAxeItem" 255 8
	DropItem "AEoDGauntletsItem" 255 8
	DropItem "AEoDVorpalBladeItem" 255 8
	DropItem "AEoDScytheItem" 255 8
	DropItem "AEoDPainkillerItem" 255 4
	DropItem "AEoDChainsawItem" 255 1
	DropItem "AEoDStroggArmSawItem" 255 1
}

Actor Slot2WepsJ : RandomSpawner
{
	DropItem "AEoDKatanaItem" 255 10
	DropItem "AEoDMedievalAxeItem" 255 8
	DropItem "AEoDGauntletsItem" 255 8
	DropItem "AEoDVorpalBladeItem" 255 8
	DropItem "AEoDSpyKnifeItem" 255 8
	DropItem "AEoDScytheItem" 255 8
	DropItem "AEoDPainkillerItem" 255 4
	DropItem "AEoDChainsawItem" 255 1
	DropItem "AEoDStroggArmSawItem" 255 1
}

Actor Slot3Weps : RandomSpawner
{
	DropItem "AEoDBerettaItem" 255 40
	DropItem "AEoDBlasterItem" 255 40
	DropItem "AEoDGargoyleWandItem" 255 40
	DropItem "AEoDSPASShotgunItem" 255 40
	DropItem "AEoDMac10Item" 255 40
	DropItem "AEoDFlaregunItem" 255 40
	DropItem "AEoDMP5Item" 255 20
	DropItem "AEoDGoldenColtsItem" 255 20
	DropItem "AEoDCrossbowItem" 255 20
	DropItem "AEoDTekbowItem" 255 20
	DropItem "AEODDesertEagleItem" 255 20
	DropItem "AEoDAssaultShotgunItem" 255 10
	DropItem "AEoDShellShockerItem" 255 5
}

Actor Slot4Weps : RandomSpawner
{
	DropItem "GunsAkimbo" 255 70
	DropItem "AEoDDoubleBarreledItem" 255 70
	DropItem "AEoDPKShotgunItem" 255 70
	DropItem "AEoDBioRifleItem" 255 70
	DropItem "AEoDCMRifleItem" 255 70
	DropItem "AEoDCrystalGunItem" 255 40
	DropItem "AEoDVoodooDollItem" 255 40
	DropItem "AEoDBoltGunItem" 255 40
	DropItem "AEoDCarbineItem" 255 40
	DropItem "AEoDSerpentStaffItem" 255 40
	DropItem "AEoDRiotgunItem" 255 30
	DropItem "AEoDDisruptorItem" 255 20
	DropItem "AEoDCombatShotgunItem" 255 20
	DropItem "AEoDDragonClawItem" 255 20
	DropItem "AEoDShockRifleItem" 255 20
	DropItem "AEoDSniperRifleItem" 255 5
	DropItem "AEoDHeadHunterItem" 255 5
	DropItem "AEoDDemonOrbItem" 255 3
	DropItem "AEoDAWPItem" 255 1
}

Actor Slot4WepsJ : RandomSpawner
{
	DropItem "GunsAkimbo" 255 70
	DropItem "AEoDDoubleBarreledItem" 255 70
	DropItem "AEoDPKShotgunItem" 255 70
	DropItem "AEoDBioRifleItem" 255 70
	DropItem "AEoDCMRifleItem" 255 70
	DropItem "AEoDCrystalGunItem" 255 40
	DropItem "AEoDVoodooDollItem" 255 40
	DropItem "AEoDBoltGunItem" 255 40
	DropItem "AEoDCarbineItem" 255 40
	DropItem "AEoDSerpentStaffItem" 255 40
	DropItem "AEoDRiotgunItem" 255 30
	DropItem "AEoDBisonItem" 255 20
	DropItem "AEoDDisruptorItem" 255 20
	DropItem "AEoDCombatShotgunItem" 255 20
	DropItem "AEoDDragonClawItem" 255 20
	DropItem "AEoDShockRifleItem" 255 20
	DropItem "AEoDSniperRifleItem" 255 5
	DropItem "AEoDHeadHunterItem" 255 5
	DropItem "AEoDDemonOrbItem" 255 3
	DropItem "AEoDAWPItem" 255 1
}

Actor Slot5Weps : RandomSpawner
{
	DropItem "AEoDPlasmaRifleItem" 255 100
	DropItem "AEoDFreezethrowerItem" 255 100
	DropItem "AEoDHyperBlasterItem" 255 100
	DropItem "AEoDMinigunItem" 255 100
	DropItem "AEoDIceStormRodItem" 255 80
	DropItem "AEoDDemonStaffItem" 255 80
	DropItem "AEoDGaussGunItem" 255 80
	DropItem "AEoDHornetGunItem" 255 80
	DropItem "AEoDSkullRodItem" 255 80
	DropItem "AEoDLasergunItem" 255 50
	DropItem "AEoDFlamethrowerItem" 255 40
	DropItem "AEoDPeaceMakerItem" 255 40
	DropItem "AEoDNailGunItem" 255 40
	DropItem "AEoDNailgunMK2Item" 255 40
	DropItem "AEoDWyvernHeartItem" 255 5
}

Actor Slot6Weps : RandomSpawner
{
	DropItem "AEoDGrenadeLauncherItem" 255 150
	DropItem "AEoDRocketLauncherItem" 255 150
	DropItem "AEoDMissileLauncherItem" 255 150
	DropItem "AEoDPMFLayerItem" 255 150
	DropItem "AEoDMeteoriteRodItem" 255 150
	DropItem "AEoDFireStormSpellItem" 255 80
	DropItem "AEoDLightningRodItem" 255 80
	DropItem "AEoDScreamerItem" 255 80
	DropItem "AEoDRazorItem" 255 50
	DropItem "AEoDScarabStaffItem" 255 20
	DropItem "AEoDHowitzerItem" 255 5
	DropItem "AEoDWarAxeItem" 255 2
}

Actor Slot6WepsJ : RandomSpawner
{
	DropItem "AEoDGrenadeLauncherItem" 255 150
	DropItem "AEoDRocketLauncherItem" 255 150
	DropItem "AEoDMissileLauncherItem" 255 150
	DropItem "AEoDPMFLayerItem" 255 150
	DropItem "AEoDMeteoriteRodItem" 255 150
	DropItem "AEoDCowManglerItem" 255 80
	DropItem "AEoDFireStormSpellItem" 255 80
	DropItem "AEoDLightningRodItem" 255 80
	DropItem "AEoDScreamerItem" 255 80
	DropItem "AEoDRazorItem" 255 50
	DropItem "AEoDScarabStaffItem" 255 20
	DropItem "AEoDHowitzerItem" 255 5
	DropItem "AEoDWarAxeItem" 255 2
}

Actor Slot7Weps : RandomSpawner
{
	DropItem "AEoDPulsegunItem" 255 100
	DropItem "AEoDEidechsenesserItem" 255 100
	DropItem "AEoDThunderBoltItem" 255 100
	DropItem "AEoDPrismaCannonItem" 255 100
	DropItem "AEoDElectroDriverItem" 255 100
	DropItem "AEoDTeslaCannonItem" 255 100
	DropItem "AEoDIceWandItem" 255 50
	DropItem "AEoDIncineratorItem" 255 50
	DropItem "AEoDDevastatorItem" 255 50
	DropItem "AEoDFragLauncherItem" 255 50
	DropItem "AEoDRipperGunItem" 255 50
	DropItem "AEoDLifeLeechItem" 255 30
	DropItem "AEoDConcussionLauncherItem" 255 50
	DropItem "AEoDPlutoniumCannonItem" 255 50
	DropItem "AEoDGuardianHeadItem" 255 30
	DropItem "AEoDRaptorItem" 255 20
	DropItem "AEoDHailStormItem" 255 20
	DropItem "AEoDVulcanCannonItem" 255 20
	DropItem "AEoDPerforatorItem" 255 5
	DropItem "AEoDArbalestItem" 255 1
}

Actor Slot8Weps : RandomSpawner
{
	DropItem "AEoDMindBlowerItem" 255 150
	DropItem "AEoDStakeGunItem" 255 150
	DropItem "AEoDPulseCannonItem" 255 150
	DropItem "AEoDFlakCannonItem" 255 100
	DropItem "AEoDRailGunItem" 255 100
	DropItem "AEoDBoneShotgunItem" 255 50
	DropItem "AEoDPKSMGItem" 255 50
	DropItem "AEoDIonCannonItem" 255 50
	DropItem "AEoDHEGrenadeLauncherItem" 255 50
	DropItem "AEoDPhalanxItem" 255 50
	DropItem "AEoDBoneDusterItem" 255 50
	DropItem "AEoDSniperRailgunItem" 255 50
	DropItem "AEoDLichStaffItem" 255 50
	DropItem "AEoDMorserLauncherItem" 255 50
	DropItem "AEoDFlailGunItem" 255 50
	DropItem "AEoDDsparilStaffItem" 255 30
	DropItem "AEoDDeathRayItem" 255 30
	DropItem "AEoDNapalmLauncherItem" 255 30
	DropItem "AEoDIncendiaryItem" 255 30
	DropItem "AEoDTomeOfSetItem" 255 20
	DropItem "AEoDRavenStaffItem" 255 10
	DropItem "AEoDShredderStaffItem" 255 10
}

Actor Slot8WepsJ : RandomSpawner
{
	DropItem "AEoDMindBlowerItem" 255 150
	DropItem "AEoDStakeGunItem" 255 150
	DropItem "AEoDPulseCannonItem" 255 150
	DropItem "AEoDFlakCannonItem" 255 100
	DropItem "AEoDRailGunItem" 255 100
	DropItem "AEoDGutFlingerItem" 255 80
	DropItem "AEoDBoneShotgunItem" 255 50
	DropItem "AEoDPKSMGItem" 255 50
	DropItem "AEoDIonCannonItem" 255 50
	DropItem "AEoDHEGrenadeLauncherItem" 255 50
	DropItem "AEoDPhalanxItem" 255 50
	DropItem "AEoDBoneDusterItem" 255 50
	DropItem "AEoDSniperRailgunItem" 255 50
	DropItem "AEoDLichStaffItem" 255 50
	DropItem "AEoDMorserLauncherItem" 255 50
	DropItem "AEoDFlailGunItem" 255 50
	DropItem "AEoDDsparilStaffItem" 255 30
	DropItem "AEoDDeathRayItem" 255 30
	DropItem "AEoDNapalmLauncherItem" 255 30
	DropItem "AEoDIncendiaryItem" 255 30
	DropItem "AEoDTomeOfSetItem" 255 20
	DropItem "AEoDRavenStaffItem" 255 10
	DropItem "AEoDShredderStaffItem" 255 10
}

Actor Slot9Weps : RandomSpawner
{
	DropItem "AEoDSoulReaverItem" 255 100
	DropItem "AEoDRailGunPrototypeItem" 255 100
	DropItem "AEoDHERocketLauncherItem" 255 100
	DropItem "AEoDPrototypeItem" 255 50
	DropItem "AEoDNecrossbowItem" 255 50
	DropItem "AEoDSoulleechItem" 255 50
	DropItem "AEoDProteusDynamoItem" 255 25
	DropItem "AEoDPhotonCannonItem" 255 25
	DropItem "AEoDHellCoreItem" 255 12
	DropItem "AEoDHellRootItem" 255 12
	DropItem "AEoDBouncerItem" 255 12
	DropItem "AEoDWraithVergeItem" 255 6
	DropItem "AEoDDrakanItem" 255 6
	DropItem "AEoDBloodScourgeItem" 255 6
	DropItem "AEoDCrawlerItem" 255 6
	DropItem "AEoDGluonGunItem" 255 6
	DropItem "AEoDTempestStaffItem" 255 3
}

Actor Slot9WepsJ : RandomSpawner
{
	DropItem "AEoDSoulReaverItem" 255 100
	DropItem "AEoDRailGunPrototypeItem" 255 100
	DropItem "AEoDHERocketLauncherItem" 255 100
	DropItem "AEoDPrototypeItem" 255 50
	DropItem "AEoDNecrossbowItem" 255 50
	DropItem "AEoDSoulleechItem" 255 50
	DropItem "AEoDProteusDynamoItem" 255 25
	DropItem "AEoDPhotonCannonItem" 255 25
	DropItem "AEoDPopcornGunItem" 255 20
	DropItem "AEoDHellCoreItem" 255 12
	DropItem "AEoDHellRootItem" 255 12
	DropItem "AEoDBouncerItem" 255 12
	DropItem "AEoDWraithVergeItem" 255 6
	DropItem "AEoDDrakanItem" 255 6
	DropItem "AEoDBloodScourgeItem" 255 6
	DropItem "AEoDCrawlerItem" 255 6
	DropItem "AEoDGluonGunItem" 255 6
	DropItem "AEoDTempestStaffItem" 255 3
}

Actor Slot0Weps : RandomSpawner
{
	DropItem "AEoDPenetratorItem" 255 32
	DropItem "AEoDBioForceGunItem" 255 32
	DropItem "AEoDBFG9000Item" 255 16
	DropItem "AEoDRedeemerItem" 255 16
	DropItem "AEoDDarkMatterGunItem" 255 8
	DropItem "AEoDFusionCannonItem" 255 8
	DropItem "AEoDQSGItem" 255 8
	DropItem "AEoDApocalypseItem" 255 2
	DropItem "AEoDNovaItem" 255 2
	DropItem "AEoDVolcanoCannonItem" 255 2
	DropItem "AEoDDarkFlameStaffItem" 255 2
	DropItem "AEoDInfernoItem" 255 2
	DropItem "AEoDDeathsHandItem" 255 1
	DropItem "AEoDHandOfGodItem" 255 1
}

Actor L4DSmallMobRunnerX
{
	var int user_dlimit;
	var int user_max;
	var int user_check[2];
	var int user_nightmare;
	Monster
	+FRIGHTENED
	+NOTARGET
	-COUNTKILL
	+CANTSEEK
	+FLOORHUGGER
	+NORADIUSDMG
	+THRUACTORS
	+NOBLOOD
	-TELESTOMP
	//+MBFBOUNCER
	-NOTELEPORT
	+FORCEPAIN
	+LOOKALLAROUND
	+NOTARGETSWITCH
	+NONSHOOTABLE
	-SHOOTABLE
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+NOBLOCKMONST
	-NODROPOFF
	+NOTRIGGER
	+NOBLOCKMAP
	+DROPOFF
	+NOTELESTOMP
	+NOTONAUTOMAP
	+CANTLEAVEFLOORPIC
	RenderStyle None
	MaxDropOffHeight 128
	MaxStepHeight 128
	+DONTTHRUST
	Speed 14
	Health 1000000
	DamageFactor "Normal", 0.0
	DamageFactor "DirectorKiller", 32767
	Radius 20
	Height 64
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 //ACS_NamedExecuteAlways("DirectorLimiter",0,1,0)
		TNT1 A 0 A_SetUserVar("user_dlimit",0)
		TNT1 A 0 A_SetUserVar("user_max",random(7,15))
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,1,AAPTR_PLAYER1)
		Goto Idle
		TNT1 A 0 A_SetUserVar("user_max",random(10,30))
		Goto Idle
	Idle:
		TNT1 A 0 A_ClearTarget
		TNT1 AAAAA 1 A_Look
		Loop
	See:
		TNT1 A 0 A_ChangeFlag("FRIENDLY",0)
		TNT1 A 0 A_RearrangePointers(AAPTR_PLAYER1,AAPTR_NULL,AAPTR_DEFAULT)
		TNT1 A 0 A_JumpIfInventory("HordeTokenX",1,"MobHorde",AAPTR_TARGET)
	Wanderbout:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 1 A_Chase
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,6,AAPTR_PLAYER1)
		TNT1 AAAAAAAAAAAA 1 A_Wander
	Mobmaking:
		TNT1 A 0 A_JumpIfTargetInLOS("Wanderbout",360)
		TNT1 A 0 A_JumpIfCloser(250,"Wanderbout")
	MobChance: //Does not spawn ambush flagged zombies.
		TNT1 A 0 Thing_ChangeTID(0,32670)
		TNT1 A 0 A_Jump(256,1,2,3) //SPAWN EM IN PACKS!!! >:3
		TNT1 AAA 0 A_SpawnItemEx("L4DCommonSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID,128)
		Goto CountUp
	MobHorde:
		TNT1 A 0 A_TakeInventory("HordeTokenX",1,0,AAPTR_PLAYER1)
	MobHordeB:
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,"MobHordeNightmare",AAPTR_PLAYER1)
		TNT1 A 1 A_Wander
		TNT1 A 0 A_JumpIfTargetInLOS("MobHordeB",360)
		TNT1 A 0 A_JumpIfCloser(250,"MobHordeB")
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS)
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,128)
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,192)
		TNT1 A 0 A_SpawnItemEx("L4DGeneralWeaponSpawner",0,0,0,0,0,0,0,32,64)
		TNT1 AA 0 A_SpawnItemEx("ShellBoxReplacer",0,0,0,0,0,0,0,32,128)
		TNT1 AA 0 A_SpawnItemEx("ClipBoxReplacer",0,0,0,0,0,0,0,32,128)
		TNT1 AA 0 A_SpawnItemEx("RocketBoxReplacer",0,0,0,0,0,0,0,32,128)
		TNT1 AA 0 A_SpawnItemEx("CellPackReplacer",0,0,0,0,0,0,0,32,128)
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,32,230)
		TNT1 AAAAAAA 0 A_SpawnItemEx("Medikit",0,0,0,0,0,0,0,32,32)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerReal",0,0,0,0,0,0,0,32,128)
		TNT1 A 0 //ACS_NamedExecuteAlways("DirectorLimiter",0,2,0)
		Stop
	MobHordeNightmare:
		TNT1 A 1 A_Wander
		TNT1 A 0 A_JumpIfTargetInLOS("MobHordeNightmare",360)
		TNT1 A 0 A_JumpIfCloser(250,"MobHordeNightmare")
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS)
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,128)
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS,192)
		TNT1 A 0 A_SpawnItemEx("L4DGeneralWeaponSpawner",0,0,0,0,0,0,0,32,32)
		TNT1 AA 0 A_SpawnItemEx("ShellBoxReplacer",0,0,0,0,0,0,0,32,64)
		TNT1 AA 0 A_SpawnItemEx("ClipBoxReplacer",0,0,0,0,0,0,0,32,64)
		TNT1 AA 0 A_SpawnItemEx("RocketBoxReplacer",0,0,0,0,0,0,0,32,64)
		TNT1 AA 0 A_SpawnItemEx("CellPackReplacer",0,0,0,0,0,0,0,32,64)
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,32,192)
		TNT1 AAAAAAA 0 A_SpawnItemEx("Medikit",0,0,0,0,0,0,0,32,16)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerReal",0,0,0,0,0,0,0,32,96)
		TNT1 A 0 //ACS_NamedExecuteAlways("DirectorLimiter",0,2,0)
		Stop
	CountUp:
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 A 0 A_SetUserVar("user_dlimit",user_dlimit+1)
		TNT1 A 0 A_JumpIf(user_dlimit>=user_max,"Vanish")
		Goto Wanderbout
	Vanish:
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,"VanishNightmare",AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DPackageSpawner")
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,32,230)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerReal",0,0,0,0,0,0,0,32,190)
		TNT1 A 0 //ACS_NamedExecuteAlways("DirectorLimiter",0,2,0)
		Stop
	VanishNightmare:
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMM")
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,32,190)
		TNT1 A 0 A_SpawnItemEx("Megasphere",0,0,0,0,0,0,0,32,245)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerReal",0,0,0,0,0,0,0,32,96)
		TNT1 A 0 //ACS_NamedExecuteAlways("DirectorLimiter",0,2,0)
		Stop
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		Goto WanderBout
	Death:
	End:
		TNT1 A 0 //ACS_NamedExecuteAlways("DirectorLimiter",0,2,0)
		Stop
	}
}

Actor SpawnSmallMonsterL4D : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("L4DSmallMobRunnerX",0,0,0,0,0,0,0,32)
		Stop
	}
}

Actor SpawnMediumMonsterL4D : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("L4DMediumMobRunner",0,0,0,0,0,0,0,32)
		Stop
	}
}

Actor SpawnLargeMonsterL4D : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("L4DLargeMobRunner",0,0,0,0,0,0,0,32)
		Stop
	}
}

Actor SpawnHorde : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_PlaySound("Director/Horde",5,1,0,ATTN_NONE)
		TNT1 A 0 A_GiveInventory("HordeTokenX",6)
		TNT1 AAAAAA 0 A_SpawnItemEx("L4DSmallMobRunnerX",0,0,0,0,0,0,0,32)
		Stop
	}
}

Actor HordeTokenX : Inventory
{ Inventory.MaxAmount 6 }