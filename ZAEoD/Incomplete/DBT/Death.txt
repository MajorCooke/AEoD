//"THIS IS IT!" - Dr. Kleiner, Half-Life 2: Episode 2

/*
Soooo, here's what I'ma thinking about this. I know it may seem kinda cheesy
at first, but the ideas still really prototype-ish. And weird.*/

Actor DBTWhiteFlash
{
	+NOINTERACTION
	RenderStyle Stencil
	StencilColor "White"
	Scale 0.6
	Alpha 0
	States
	{
	Spawn:
		TNT1 A 0
		DBT2 KKK 4 A_FadeTo(1,0.4,0)
		DBT2 KKKKKK 2 A_FadeTo(0,0.17,1)
		Stop
	}
}

//Try to get this synchronized with DBT's fade-out effect so he looks like he's going dark
//without relying on GL lights for the effect.
Actor DBTBlackFade
{
	var int user_alpha;
	+NOINTERACTION
	RenderStyle Stencil
	StencilColor "Black"
	Scale 0.6
	Alpha 0
	States
	{
	Spawn:
		TNT1 A 0
	Fader:
		DBT2 A 2 A_FadeTo(1,0.025,0)
		DBT2 A 0 A_JumpIf(alpha>=1,1)
		Loop
		DBT2 IJ 8
		Stop
	}
}

Actor DBTSGPortalMissile
{
	var int user_count;
	Projectile
	Radius 15
	Height 15
	+THRUACTORS
	+SEEKERMISSILE
	+NOTELESTOMP
	Speed 20
	WeaveIndexZ 8
	Scale 0.65
	Translation "0:255=%[0,0,0]:[0.5,0.5,0.5]"
	States
	{
	Spawn: 
		PARS A 2 NoDelay A_PlaySound("greatwraith/active",CHAN_AUTO,1,0)
	Looping:
		//PARS A 0 A_Weave(0,2,0,4.0)
		PARS A 0 A_ChangeVelocity(-2,0,0,CVF_RELATIVE)
		PARS A 0 A_SpawnItemEx("DBTSGTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE)
		PARS A 2 A_SetUserVar("user_count",user_count+1)
		PARS A 0 A_JumpIf(user_count>=19,"Explosion")
		Loop
	Death:
	Explosion:
		TNT1 A 0 A_Stop
		PARS A 0 A_PlaySound("greatwraith/death",CHAN_AUTO,1,0)
		TNT1 A 0 A_SpawnItemEx("DBTDSPortal",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TRACER)
		TNT1 A 0 A_FaceTracer
		TNT1 A 0 A_SpawnItemEx("DBTDeathStorm",0,0,0,7,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
		Stop
	}
}
		

Actor DBTDSPortal
{
	+NOINTERACTION
	+NOTELESTOMP
	RenderStyle Translucent
	Translation "0:255=%[0,0,0]:[0.5,0.5,0.5]"
	Scale 1.0
	States
	{
	Spawn:
		PARS BBBCCCDDDEEEFFF 1 bright A_SetScale(scalex+0.1)
		PARS FFFF 1 bright A_SetScale(scalex+0.075)
	Looping:
		PARS F 1 bright A_SetScale(scalex+0.05)
		PARS F 0 bright A_FadeOut(0.1,1)
		Loop
	}
}

Actor DBTDeathStorm
{
	var int user_count;
	Projectile
	+THRUACTORS
	+FOILINVUL
	+EXTREMEDEATH
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	+SEEKERMISSILE
	+NOTELESTOMP
	Radius 15
	Height 8
	Speed 7
	DamageType "PlayerResistant"
	States
	{
	Spawn:
		SPIR P 0 NoDelay A_SeekerMissile(1,1,1,256)
		SPIR P 0 A_FaceTracer
		//SPIR P 0 A_ScaleVelocity(7)
	Spiritbomb:
		SPIR P 0 A_SpawnItemEx("DBTDeathSpirit",user_count*-7,random(-16,16),random(-16,16),velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SPIR PPP 0 A_SpawnItemEx("DBTDeathSpirit",user_count*-7,random(-16,16),random(-16,16),velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM,192)
		SPIR P 0 A_SpawnItemEx("DBTDeathCloud",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		SPIR PPP 0 A_SpawnItemEx("DBTDeathCloud",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH,192)
		SPIR P 1 A_Explode(random(500,1000),256,0)
		SPIR P 0 A_SetUserVar("user_count",user_count+1)
		SPIR P 0 A_JumpIf(user_count==29,"DBTBloodShedderSpawn")
		SPIR P 0 A_JumpIf(user_count>=59,1)
		Loop
	Looping:
		SPIR P 0 A_SpawnItemEx("DBTDeathCloud",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		SPIR PPP 0 A_SpawnItemEx("DBTDeathCloud",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH,192)
		SPIR P 1 A_Explode(random(500,1000),256,0)
		Loop
	DBTBloodShedderSpawn:
		SPIR P 0 A_SpawnItemEx("DBTBloodShedder",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		Goto Spiritbomb
	Death:
		TNT1 A 0 A_SetDamageType("TempestLightning")
		TNT1 A 0 A_Explode(50000,256,0,0,256)
		Stop
	}
}

Actor DBTDeathCloud
{
	var int user_cap;
	var int user_y;
	var int user_z;
	+NOINTERACTION
	+NOTELESTOMP
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[0.65,0.65,0.65]"
	Alpha 0.25
	Scale 0.35
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.60,0.75))
		TNT1 A 0 A_SetUserVar("user_cap",75)
		TNT1 A 0 A_SetUserVar("user_y",random(0,user_cap))
		TNT1 A 0 A_SetUserVar("user_cap",user_cap-user_y)
		TNT1 A 0 A_SetUserVar("user_z",random(0,user_cap))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetUserVar("user_y",user_y*(-1.0))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetUserVar("user_z",user_z*(-1.0))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(scalex*-1,scaley)
		TNT1 A 0 A_ChangeVelocity(frandom(1.0,4.0),user_y*(0.1),user_z*(0.1),CVF_RELATIVE)
		TNT1 A 0 A_Jump(256,"S1","S2")
	S1:
		X002 AAABBBCCCCDDDDD random(1,2) bright A_ScaleVelocity(frandom(0.87,0.90))
		X002 EEEEEFFFFFGGGGGHHHHHIIIIIJJJJJ random(1,2) bright A_ScaleVelocity(0.85)
		X002 KKKKLLLLMMMNNNOOOPPPQQQRRSSTTUU random(1,2) bright A_ScaleVelocity(0.8)
		Stop
	S2:
		X003 A 0 A_SetScale(scalex*0.5,scaley*0.5)
		X003 AABBCCDDDEEEFFFGGGHHHIIIJJJKKKLLL random(1,2) bright A_ScaleVelocity(0.85)
		X003 MMNNOOPPQQRRSTUVWXYZ random(1,2) bright A_ScaleVelocity(0.80)
		Stop
	}
}

Actor DBTDeathSpirit
{
	var int user_dist[2];
	Projectile
	+THRUACTORS
	+RANDOMIZE
	Radius 10
	Height 3
	//RenderStyle Stencil
	//StencilColor "Black"
	Alpha 0.0
	States
	{
	Spawn:
		SPIR A 0 NoDelay A_SetUserArray("user_dist",0,frandom(-1.25,1.25))
		SPIR A 0 A_SetUserArray("user_dist",1,frandom(-2.5,2.5))
	LoopFade:
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR B 1 A_Weave(1,1,user_dist[0],user_dist[1])
		SPIR A 0 A_FadeTo(0.4,0.01,0)
		SPIR A 0 A_JumpIf(alpha>=0.4,"Looping")
		Loop
	Looping:
		SPIR AABB 2 A_Weave(1,1,user_dist[0],user_dist[1])
		Loop
	Death:
		SPIR D 4
		SPIR E 4 A_PlaySound("SpiritDie",CHAN_AUTO,1,0)
		SPIR FGHI 4
		Stop
	}
}

Actor DBTBloodShedder
{
	Projectile
	+THRUACTORS
	+NOTELESTOMP
	Radius 8
	Height 8
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_ScaleVelocity(0.01)
		TNT1 A 30
		TNT1 A 0 A_ScaleVelocity(100)
	Looping:
		TNT1 A 2 A_RadiusGive("DBTBleeder",384,RGF_MONSTERS,1)
		Loop
	Death:
		TNT1 A 0
		Stop
	}
}

Actor DBTBleeder : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	+NOTELESTOMP
	States
	{
	Use:
		TNT1 A 0 A_JumpIfInventory("Invulnerability",1,"End")
		TNT1 A 0 A_JumpIfInventory("DeathBringerToken",1,"End")
		TNT1 A 0 A_CheckFlag("NOBLOOD","End")
		TNT1 A 0 A_CheckFlag("INVULNERABLE","End")
		TNT1 A 0 A_CheckFlag("NONSHOOTABLE","End")
		TNT1 A 0 A_SpawnItemEx("MCBloodParticle",random(0,(radius+10)),0,random((height*0.2),(height*0.8)),0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 AAA 0 A_SpawnItemEx("MCBloodParticle",random(0,(radius/2)),0,random((height*0.2),(height*0.8)),0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR,192)
		Stop
	End:
		TNT1 A 0
		Stop
	}
}