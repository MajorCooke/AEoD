/*==============================================================================
Team Fortress 2 Classes
Ripped and created by Major Cooke
Sounds and Sprites from Team Fortress 2
==============================================================================*/

//==============================================================================
//==============================================================================
//								The Demoman
//==============================================================================
//==============================================================================

Actor TF2Demoman	//cyberdemon
{
	DropItem "HealthBottle" 64
	DropItem "ExploBigAmmo" 128
	DropItem "ExploBigAmmo" 128
	DropItem "ExploBigAmmo" 128
	DropItem "ExploBigAmmo" 128
	DropItem "MegaHeart"
	DropItem "AEoDHEGrenadeLauncherItem" 128
	DropItem "PowerslaveSpawner" 28
	DropItem "PowerupSpawner" 8
	var int user_targetacquired;
	var int user_count;
	var int user_chance;
	var int user_distance;
	var int user_chargin;
	Monster	RipLevelMin 1
	+BOSS
	+BOSSDEATH
	+NORADIUSDMG
	+DONTMORPH
	+NOICEDEATH
	+MISSILEMORE
	//+MISSILEEVENMORE
	+NOFEAR
	//Translation "16:47=%[0,0,0]:[0,0,1]", "169:191=%[0,0,0]:[0,0,1]"
	Mass 250
	Health 4500
	Radius 24
	Height 42
	Scale 0.35
	Speed 6
	PainChance 64
	SeeSound "Demoman/See"
	ActiveSound "Demoman/Active"
	PainSound "Demoman/Pain"
	DeathSound "Demoman/Death"
	Obituary "%o was dominated by the Demoman."
	MeleeRange 26
	BloodColor "Brown" //He drank so much scotch, his blood turned to crap. =P
	PainChance "Charm", 0
	PainChance "Terror", 0
	PainChance "Defile", 0
	PainChance "Drain", 40
	PainChance "Chaos", 255
	DamageFactor "ChaosExpl1", 0
	DamageFactor "ChaosExpl2", 0
//Armor
	DamageFactor "Electric", 1.25
	DamageFactor "Plasma", 0.75
	DamageFactor "PlasmaDisint", 0.75
	DamageFactor "AEBullet", 0.5
	DamageFactor "DemoDamager", 1000
	States
	{
	Pain.Chaos:
		TNT1 A 0 A_CustomMissile("ChaosExpl1",0,0,0)
		TNT1 A 0 A_CustomMissile("ChaosExpl2",0,0,0)
		Goto See
	Pain.ChaosExpl1:
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",1)
		Goto Spawn
	Pain.ChaosExpl2:		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",0)		Goto See
	Pain.Drain:			TNT1 A 0 A_SpawnItemEx("DrainGiver",0,0,0,0,0,0,0,32,0)	Goto Pain	
	
	Spawn:
		N001 A 0
		N001 A 0 A_SpawnItemEx("DemomanReloadChecker",0,0,0,0,0,0,0,48)
	Idle:
		N001 A 0 A_JumpIf(user_targetacquired==1,"Taunting")
		N001 A 1 A_Look
		Loop
	Taunting:
		N001 A 0 A_SetUserVar("user_targetacquired",0)
		N001 A 0 A_PlaySound("Demoman/Score",CHAN_WEAPON,1,0,ATTN_NONE)
		Goto Idle
		
	ReloadChecker:
		TNT1 A 0 A_JumpIf(user_count>=1,"Setter")
	See:
		TNT1 A 0 A_JumpIfInventory("DemoReloadRequired",1,"SeeReloadPend")
		TNT1 A 0 A_SetUserVar("user_count",0)
		N001 BBBCCCDDDEEE 2 A_Chase("","Missile",CHF_FASTCHASE)
		Loop
	Setter:
		TNT1 A 0 A_SetUserVar("user_count",user_count-1)
		TNT1 A 0 A_GiveInventory("DemoReloadRequired",1)
		Goto ReloadChecker
	ReloadNeedActivator:
		TNT1 A 0 A_TakeInventory("DemoReloadRequired",9000)
		TNT1 A 0 A_SetUserVar("user_count",0)
		TNT1 A 0 A_GiveInventory("DemoReloadRequired",4)
		Goto SeeReloadPend
	SeeReloadPend:
		N001 BBBCCCDDDEEE 2 A_Chase("","",CHF_FASTCHASE)
		N001 B 0 A_JumpIfInventory("DemoReloadRequired",1,"SeeReloadPend")
		Goto See
	Pain:
		N001 P 6 A_Pain
		Goto ReloadChecker
	Missile:
		//N001 A 0 A_JumpIf(health>=7000,"VeryLongRange")
		//N001 A 0 A_JumpIfCloser(700,1)
		//Goto VeryLongRange
		//N001 A 0 A_Jump(192,"UllapoolCharge") //Just for testing. Comment out afterwards.
		N001 A 0 A_JumpIf(health>=4000,"HPCheck0")
		N001 A 0 A_JumpIf(health<=500,"HPCheck1")
		N001 A 0 A_JumpIf(health<=1000,"HPCheck2")
		N001 A 0 A_JumpIf(health<=2000,"HPCheck3")
		N001 A 0 A_JumpIf(health<=2500,"HPCheck4")
		N001 A 0 A_JumpIf(health<=3000,"HPCheck5")
		Goto VeryLongRange
	HPCheck0:
		N001 A 0 A_Jump(8,"UllapoolCharge")
		Goto VeryLongRange
	HPCheck1:
		N001 A 0 A_Jump(192,"UllapoolCharge")
		Goto VeryLongRange
	HPCheck2:
		N001 A 0 A_Jump(96,"UllapoolCharge")
		Goto VeryLongRange
	HPCheck3:
		N001 A 0 A_Jump(64,"UllapoolCharge")
		Goto VeryLongRange
	HPCheck4:
		N001 A 0 A_Jump(32,"UllapoolCharge")
		Goto VeryLongRange
	HPCheck5:
		N001 A 0 A_Jump(16,"UllapoolCharge")
		Goto VeryLongRange
		//N001 A 0 A_SetUserVar("user_chance",random(1,10))
	VeryLongRange:
		N001 A 0 A_JumpIfCloser(2500,"LongRange")
		N001 A 0 A_JumpIfTargetInLOS(1,360,JLOSF_DEADNOJUMP)
		Goto ReloadChecker
		N001 A 6 A_FaceTarget
		N001 F 0 Bright A_PlaySound("Demoman/GrenadeLauncher/Fire",CHAN_AUTO,1.0,0)
		N001 F 3 Bright A_CustomMissile("GrenadeL4",32,0,0,0)
		N001 GGAAA 3
		N001 A 0 A_SetUserVar("user_count",user_count+1)
		N001 A 0 A_JumpIf(user_count>=4,"ReloadNeedActivator")
		N001 A 0 A_Jump(32,"VeryLongRange")
		Goto ReloadChecker
	LongRange:
		N001 A 0 A_JumpIfCloser(1600,"MediumRange")
		N001 A 0 A_JumpIfTargetInLOS(1,360,JLOSF_DEADNOJUMP)
		Goto ReloadChecker
		N001 A 6 A_FaceTarget
		N001 F 0 Bright A_PlaySound("Demoman/GrenadeLauncher/Fire",CHAN_AUTO,1.0,0)
		N001 F 3 Bright A_CustomMissile("GrenadeL3",32,0,0,0)
		N001 GGAAA 3
		N001 A 0 A_SetUserVar("user_count",user_count+1)
		N001 A 0 A_JumpIf(user_count>=4,"ReloadNeedActivator")
		N001 A 0 A_Jump(64,"LongRange")
		Goto ReloadChecker
	MediumRange:
		N001 A 0 A_JumpIfCloser(800,"ShortRange")
		N001 A 0 A_JumpIfTargetInLOS(1,360,JLOSF_DEADNOJUMP)
		Goto ReloadChecker
		N001 A 6 A_FaceTarget
		N001 F 0 Bright A_PlaySound("Demoman/GrenadeLauncher/Fire",CHAN_AUTO,1.0,0)
		N001 F 3 Bright A_CustomMissile("GrenadeL3",32,0,0,0)
		N001 GAAA 3
		N001 A 0 A_SetUserVar("user_count",user_count+1)
		N001 A 0 A_JumpIf(user_count>=4,"ReloadNeedActivator")
		N001 A 0 A_Jump(128,"MediumRange")
		Goto ReloadChecker
	ShortRange:
		N001 A 6 A_FaceTarget
		N001 A 0 A_JumpIfTargetInLOS(1,360,JLOSF_DEADNOJUMP)
		Goto ReloadChecker
		N001 F 0 Bright A_PlaySound("Demoman/GrenadeLauncher/Fire",CHAN_AUTO,1.0,0)
		N001 F 3 Bright A_CustomMissile("QuakeGrenadeBig",32,0,0,0)
		N001 GAAA 3
		N001 A 0 A_SetUserVar("user_count",user_count+1)
		N001 A 0 A_JumpIf(user_count>=4,"ReloadNeedActivator")
		N001 A 0 A_Jump(192,"ShortRange")
		Goto ReloadChecker
	UllapoolCharge:
		N001 H 0 A_ChangeFlag("NOPAIN",1)
		N001 H 0 A_TakeInventory("IDied",1000,0)
		N001 H 0 A_SetUserVar("user_chargin",1)
		N001 H 0 A_SetUserVar("user_distance",0)
		N001 H 0 A_StopSound(6)
		N001 H 0 A_PlaySound("Demoman/Charge",6,1,0)
		N001 H 0 A_Stop
		N001 H 0 A_FaceTarget
		N001 H 0 A_SpawnItemEx("DemomanUllapool",12,0,21,1,0,0,0,32|SXF_SETMASTER)
	UllapoolLooping:
		N001 H 1 A_JumpIf(user_chargin==0,"Restore") 
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 H 1 A_JumpIf(user_chargin==0,"Restore")
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 I 1 A_JumpIf(user_chargin==0,"Restore")
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 I 1 A_JumpIf(user_chargin==0,"Restore")
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 J 1 A_JumpIf(user_chargin==0,"Restore")
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 J 1 A_JumpIf(user_chargin==0,"Restore")
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 K 1 A_JumpIf(user_chargin==0,"Restore")
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 K 1 A_JumpIf(user_chargin==0,"Restore")
		N001 H 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		N001 H 0 A_SetUserVar("user_distance",user_distance+1)
		N001 H 0 A_JumpIf(user_distance>=5,"Restore")
		Loop
	Restore:
		"####" "#" 0 A_RemoveChildren(1,RMVF_EVERYTHING,"DemomanUllapool")
		"####" "#" 0 A_SetUserVar("user_chargin",0)
		"####" "#" 0 A_SetUserVar("user_distance",0)
		"####" "#" 0 A_ChangeFlag("NOPAIN",0)
		Goto See
	Death:
		TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(10,20),0,0,0,0,33)
		TNT1 AAA 0 A_SpawnItemEx("GibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 AAAAAA 0 A_SpawnItemEx("SmallGibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Fall
		TNT1 A 0 A_KillChildren("Telefrag",KILS_KILLMISSILES|KILS_FOILINVUL)
		TNT1 A 1 A_PlaySound("other/gibdeath",CHAN_AUTO,1)
		TNT1 A 35 A_GiveInventory("IDied",1)
		TNT1 A 0 A_KillMaster
		Stop
	Death.UnholyDisint:
	Death.PlasmaDisint:
	Disintegrate:
		"####" "#" 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		"####" "#" 0 Thing_SetTranslation(0,50)
		"####" "#" 0 A_KillMaster
		"####" "#" 1 A_FadeOut(0.015,0)
		"####" "#" 0 A_ChangeFlag("NOINTERACTION",1)
		"####" "#" 0 A_Stop    
		"####" "#" 0 A_Fall
		"####" "#" 0 A_SpawnItemEx("DisintegrateShadowMediumBig",0,0,40,0,0,0,0,32)
	DisLoop:
		"####" "#" 0 A_SpawnItemEx("DisintegrateSparksMediumBig",0,0,40,0,0,0,0,32)
		"####" "##" 2 A_FadeOut(0.03)
		Loop
	Ice:
		"####" "#" 5 A_GenericFreezeDeath
		"####" "#" 0 A_KillMaster
		"####" "#" 1 A_FreezeDeathChunks
		Wait
	}
}

Actor DemomanUllapool
{
	var int user_count;
	Projectile
	+DONTREFLECT
	+SKYEXPLODE
	+DONTSPLASH
	+EXPLODEONWATER
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+HITTRACER
	+FORCERADIUSDMG
	+EXTREMEDEATH
	Speed 5
	Radius 30
	Height 16
	RenderStyle None
	DamageType "DemoDamager"
	Obituary "%o was dusted by the Demoman's suicidal ullapool caber charge."
	States
	{
	Spawn:
		TNT1 A 0
	Looping:
		TNT1 A 0 A_JumpIfHealthLower(1,"End",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("IDied",1,"End",AAPTR_TARGET)
		TNT1 A 0 A_SetUserVar("user_count",user_count+1)
		TNT1 A 1 A_JumpIf(user_count>=40,"Restore")
		TNT1 A 0 A_Warp(AAPTR_TARGET,24,0,12,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,"Looping")
		TNT1 A 0 A_JumpIfHealthLower(1,"End",AAPTR_TARGET)
	Checker:
		TNT1 A 1
	Death:
	Restore:
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_GiveInventory("DemomanRestorer",1,AAPTR_TARGET)
		Stop
	Crash:
	XDeath:
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_GiveInventory("DemomanRestorer",1,AAPTR_TARGET)
		TNT1 A 0 A_CheckFlag("ISMONSTER","Explode",AAPTR_TRACER)
		Stop
	Explode:
		TNT1 A 0 A_Explode(random(2000,6000),768,1)
		TNT1 A 0 A_SpawnItemEx("RedeemerExplosionSpawner",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS)
		//TNT1 A 0 A_DamageMaster(random(2000,6000),"Normal")
		Stop
	End:
		TNT1 A 0
		Stop
	}
}

Actor DemomanRestorer : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Use:
		TNT1 A 0 A_SetUserVar("user_chargin",0)
		Stop
	}
}
		
Actor DemomanReloadChecker
{
	Projectile
	+NOINTERACTION
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
	Idle:
		TNT1 A 0 A_JumpIfHealthLower(1,"Death",AAPTR_MASTER)
		TNT1 A 5 A_JumpIfInventory("DemoReloadRequired",1,"ReloadSequencer",AAPTR_MASTER)
		Loop
	ReloadSequencer:
		TNT1 A 20 A_GiveInventory("ReloaderSound1",1,AAPTR_MASTER)
	ReloadingStage:
		TNT1 A 20 A_GiveInventory("ReloaderSound2",1,AAPTR_MASTER)
		TNT1 A 0 A_TakeInventory("DemoReloadRequired",1,0,AAPTR_MASTER)
		TNT1 A 0 A_JumpIfInventory("DemoReloadRequired",1,"ReloadingStage",AAPTR_MASTER)
	Ending:
		TNT1 A 0 A_GiveInventory("ReloaderSound3",1,AAPTR_MASTER)
		Goto Idle
	Death:
		TNT1 A 0
		Stop
	}
}

Actor DemoReloadRequired : Inventory
{	Inventory.MaxAmount 4	}

Actor ReloaderSound1 : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_PlaySound("Demoman/GrenadeLauncher/Open",CHAN_WEAPON,1,0)
		Stop
	}
}

Actor ReloaderSound2 : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_PlaySound("Demoman/GrenadeLauncher/Reload",CHAN_WEAPON,1,0)
		Stop
	}
}

Actor ReloaderSound3 : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_PlaySound("Demoman/GrenadeLauncher/Close",CHAN_WEAPON,1,0)
		Stop
	}
}

Actor DemomanExploder : CustomInventory
{ 
	+AUTOACTIVATE
	States
	{
	Use:
		TNT1 A 0 A_SpawnItemEx("RedeemerExplosionSpawner",0,0,0,0,0,0,0,32|SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor DemomanMover : SpecialSpot
{
	+NOCLIP
	+NOSECTOR
	BounceType "Hexen"
	WallBounceFactor 0.8
	BounceFactor 0.8
	Mass 0x7FFFFFFF
	Speed 3
	Radius 28
	Height 30
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAA 1
		Stop
	}
}

Actor NoFog
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	}
}
//==============================================================================
//==============================================================================
//								The Soldier
//==============================================================================
//==============================================================================

Actor TF2Soldier	//cyberdemon
{
	DropItem "AEoDConcussionLauncherItem" 128
	DropItem "ExploBigAmmo" 128
	DropItem "ExploBigAmmo" 128
	DropItem "ExploBigAmmo" 128
	DropItem "ExploBigAmmo" 128
	DropItem "ExploBigAmmo" 128
	DropItem "MegaHeart" 64
	DropItem "PowerslaveSpawner" 28
	DropItem "PowerupSpawner" 8
	var int user_count;
	var int user_targetacquired;
	Monster	RipLevelMin 1
	+BOSS
	+BOSSDEATH
	+NORADIUSDMG
	+MISSILEMORE
	+DONTMORPH
	+NOICEDEATH
	+NOFEAR
	Health 4500
	Speed 5
	Radius 25
	Height 47
	Scale 0.35
	PainChance 64
	SeeSound "Soldier/See"
	ActiveSound "Soldier/Active"
	PainSound "Soldier/Pain"
	DeathSound "Soldier/Death"
	Obituary "%o was socket-slagged by the Soldier."
	//Armor
	DamageFactor "Electric", 1.25
	DamageFactor "Plasma", 0.75
	DamageFactor "PlasmaDisint", 0.75
	DamageFactor "AEBullet", 0.5
	PainChance "Charm", 0
	PainChance "Terror", 0
	PainChance "Defile", 0
	PainChance "Drain", 40
	PainChance "Chaos", 255
	DamageFactor "ChaosExpl1", 0
	DamageFactor "ChaosExpl2", 0
//Armor
	DamageFactor "Electric", 1.25
	DamageFactor "Plasma", 0.75
	DamageFactor "PlasmaDisint", 0.75
	DamageFactor "AEBullet", 0.5
	States
	{
	Pain.Chaos:
		TNT1 A 0 A_CustomMissile("ChaosExpl1",0,0,0)
		TNT1 A 0 A_CustomMissile("ChaosExpl2",0,0,0)
		Goto See
	Pain.ChaosExpl1:
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",1)
		Goto Spawn
	Pain.ChaosExpl2:		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",0)		Goto See
	Pain.Drain:			TNT1 A 0 A_SpawnItemEx("DrainGiver",0,0,0,0,0,0,0,32,0)	Goto Pain	
	
	Spawn:
		N002 A 0
		N002 A 0 A_JumpIf(user_targetacquired==1,"Taunting")
		N002 A 1 A_Look
		Loop
	Taunting:
		N002 A 0 A_SetUserVar("user_targetacquired",0)
		N002 A 0 A_PlaySound("Soldier/Score",CHAN_WEAPON,1,0,ATTN_NONE)
		Goto Spawn
	See:
		N002 BBBCCCDDDEEE 2 A_FastChase
		Loop
	Missile:
		N002 A 0 A_SetUserVar("user_count",0)
		N002 F 4 A_FaceTarget
		N002 A 0 A_JumpIfCloser(1580,1)
		Goto Concussion
		N002 A 0 A_Jump(256,"Elemental","Mortar","Drunk")
	Elemental:
	Mortar:
	Concussion:
		N002 F 0 A_FaceTarget
		N002 F 0 A_PlaySound("weapons/rocketlaunch",CHAN_WEAPON,1,0)
		N002 F 6 bright A_CustomMissile("ConcussionMissileMon",37,10)
		N002 GGAAA 3 A_FaceTarget
		N002 A 0 A_SetUserVar("user_count",user_count+1)
		N002 A 0 A_JumpIf(user_count>=4,"See")
		N002 A 0 A_Jump(128,"See")
		Loop
	Drunk:
		N002 F 0 A_FaceTarget
		N002 F 0 A_PlaySound("weapons/rocketlaunch",CHAN_AUTO,1,0)
		N002 F 0 bright A_CustomMissile("DrunkMissile",37,10,random(-10,10))
		N002 F 6 bright A_CustomMissile("DrunkMissile",37,10,random(-10,10))
		N002 GGAAA 3 A_FaceTarget
		Goto See
	Pain:
		N002 G 3
		N002 G 3 A_Pain
		Goto See
	Death:
		TNT1 AAAA 0 A_SpawnItemEx("GibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 AAAAAA 0 A_SpawnItemEx("SmallGibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(10,20),0,0,0,0,33)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Fall
		TNT1 A 1 A_PlaySound("other/gibdeath",CHAN_AUTO,1)
		TNT1 A 35 
		TNT1 A 0 A_KillMaster
		Stop
	Death.UnholyDisint:
	Death.PlasmaDisint:
	Disintegrate:
		"####" "#" 0 Thing_SetTranslation(0,50)
		"####" "#" 0 A_KillMaster
		"####" "#" 1 A_FadeOut(0.015)
		"####" "#" 0 A_ChangeFlag("NOINTERACTION",1)
		"####" "#" 0 A_Stop    
		"####" "#" 0 A_Fall
		"####" "#" 0 A_SpawnItemEx("DisintegrateShadowMediumBig",0,0,40,0,0,0,0,32)
		"####" "#" 0 A_SpawnItemEx("DisintegrateSparksMediumBig",0,0,40,0,0,0,0,32)
		"####" "##" 2 A_FadeOut(0.03)
		Goto Disintegrate+7

	Ice:
		"####" "#" 5 A_GenericFreezeDeath
		"####" "#" 0 A_KillMaster
		"####" "#" 1 A_FreezeDeathChunks
		Wait
	}
}		

//==============================================================================
//==============================================================================
//								The Heavy Weapons Guy
//==============================================================================
//==============================================================================

Actor TF2Heavy		//spidermastermind
{
	DropItem "HealthBottle" 190
	DropItem "HealthBottle" 190
	DropItem "HealthBottle" 190
	DropItem "HealthBottle" 190
	DropItem "HWMGSpawner" 64
	DropItem "HalfWeaponPower"
	DropItem "MegaHeart" 128
	DropItem "PowerslaveSpawner" 28
	DropItem "PowerupSpawner" 
	DropItem "PowerupSpawner" 
	DropItem "PowerupSpawner" 
	var int user_targetacquired;
	var int user_moving;
	var int user_firing;
	var int user_sandvich;
	var int user_sandvicheaten;
	Monster	RipLevelMin 1
	+BOSS
	+BOSSDEATH
	+DONTMORPH
	+NOICEDEATH
	+NORADIUSDMG
	+QUICKTORETALIATE
	+NOFEAR
	+MISSILEMORE
	PainChance 32
	Speed 5
	Health 5000 //He-he's like a bear! He's like a big shaved bear that hates people!
	Obituary "%o was hammered by the Heavy's minigun."
	Radius 32
	Height 48
	Mass 400
	Scale 0.4
	SeeSound "Heavy/See"
	PainSound "Heavy/Pain"
	ActiveSound "Heavy/Active"
	DeathSound "Heavy/Death"
	PainChance "Charm", 0
	PainChance "Terror", 0
	PainChance "Defile", 0
	PainChance "Drain", 40
	PainChance "Chaos", 255
	DamageFactor "ChaosExpl1", 0
	DamageFactor "ChaosExpl2", 0
//Armor
	DamageFactor "Electric", 1.25
	DamageFactor "Plasma", 0.75
	DamageFactor "PlasmaDisint", 0.75
	DamageFactor "AEBullet", 0.5
	States
	{
	Pain.Chaos:
		TNT1 A 0 A_CustomMissile("ChaosExpl1",0,0,0)
		TNT1 A 0 A_CustomMissile("ChaosExpl2",0,0,0)
		Goto See
	Pain.ChaosExpl1:
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",1)
		Goto Spawn
	Pain.ChaosExpl2:		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",0)		Goto See
	Pain.Drain:			TNT1 A 0 A_SpawnItemEx("DrainGiver",0,0,0,0,0,0,0,32,0)	Goto Pain	
	
	Spawn:
		N003 A 0
		N003 A 0 A_SetUserVar("user_moving",0)
		N003 A 0 A_SetUserVar("user_firing",0)
		N003 A 0 A_SetUserVar("user_sandvich",1)
	Idle:
		N003 A 0 A_JumpIf(user_targetacquired==1,"Taunt")
		N003 A 1 A_Look
		Loop
	Taunt:
		N003 A 0 A_SetUserVar("user_targetacquired",0)
		N003 A 0 A_PlaySound("Heavy/Score",CHAN_AUTO,1,0)
		Goto Spawn
	See:
		N003 I 0 A_StopSound(CHAN_BODY)
		N003 I 0 A_StopSound(CHAN_WEAPON)
		N003 A 0 A_SetUserVar("user_targetacquired",1)
		//You'd be surprised as to how well he can dodge in TF2 just by unspinning.
		N003 BBCCDDEEBBCCDDEE 2 A_FastChase
		Loop
	Missile:
		N003 A 0 A_PlaySound("Heavy/Minigun/Start",CHAN_BODY,1,1)
		N003 FGH 10 A_FaceTarget
		N003 I 0 A_SetUserVar("user_moving",1)
		N003 I 5 A_FaceTarget
	TargetSightCheck:
		N003 I 0 A_StopSound(CHAN_WEAPON)
		N003 I 0 A_SetUserVar("user_firing",0)
		//N003 I 0 A_JumpIfTargetInLOS("MinigunFire",90,0,1024)
		N003 L 0 A_FaceTarget
		N003 LLMMNNOO 2 A_Chase("","MinigunFire")
		N003 O 0 A_JumpIfCloser(512,"TargetSightCheck")
		N003 O 0 A_Jump(64,"SpinDownEnd")
		Loop
	MinigunFire:
		N003 K 0 A_SetUserVar("user_firing",1)
		N003 K 0 A_PlaySound("Heavy/Minigun/Fire",CHAN_WEAPON,1,1)
	MinigunFiring:
		N003 K 0 A_FaceTarget
		N003 J 0 A_JumpIfTargetInLOS(1,360,JLOSF_DEADNOJUMP)
		Goto SpinDownEnd
		N003 J 0 A_JumpIfCloser(1024,1)
		Goto WalkingFire
		N003 KKKK 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 K 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)//A_CustomMissile("HeavyShotSprayer",20,14,0,2)
		//N003 K 0 //A_TransferPointer(AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_MASTER,AAPTR_TRACER)
		N003 J 1 
		N003 J 0 A_MonsterRefire(32,"SpinDown")
		Loop
	WalkingFire:
		N003 L 0 A_FaceTarget
		N003 L 0 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 L 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 L 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 L 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 L 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 M 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 M 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 M 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 M 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 N 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 N 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 N 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 N 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 O 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 O 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 LLLL 0 bright A_CustomMissile("DisruptorShotHeavy",25,11,frandom(-4,4),0,(pitch+random(-10,10)))
		N003 O 1 bright A_SpawnItemEx("HeavyShotSprayer",20,11,25,0,0,0,0,32)
		N003 L 0 A_Chase("","")
		N003 O 1 A_JumpIfCloser(1024,"MinigunFiring")
		N003 O 0 A_MonsterRefire(0,"SpinDown")
		Loop
	SpinDown:
		N003 I 0 A_LookEx(0,0,0,0,0,"MinigunFire")
	SpinDownEnd:
		N003 I 0 A_SetUserVar("user_firing",0)
		N003 I 0 A_StopSound(CHAN_BODY)
		N003 I 0 A_StopSound(CHAN_WEAPON)
		N003 I 0 A_PlaySound("Heavy/Minigun/Stop",CHAN_BODY,1,0)
		N003 HGF 6 
		Goto See
	Pain:
		N003 A 0 A_JumpIf(user_sandvich==0,3)
		N003 A 0 A_JumpIf(health>=1200,2)
		N003 A 0 A_Jump(128,"Sandvich")
		N003 A 0 A_JumpIf(user_moving==1,"PainMoving")
		N003 A 3 A_Pain
		Goto See
	PainMoving:
		N003 A 0 A_StopSound(CHAN_BODY)
		N003 A 0 A_PlaySound("Heavy/Minigun/Stop",CHAN_BODY,1,0)
		N003 A 0 //A_JumpIf(user_firing==1,"PainFiring")
		N003 I 3 A_Pain
		Goto TargetSightCheck
	PainFiring:
		N003 A 0 A_StopSound(CHAN_WEAPON)
		N003 I 3 A_Pain
		Goto MinigunFire
	Sandvich:
		N003 A 0 A_StopSound(CHAN_BODY)
		N003 PQ 6
		N003 R 5 A_ChangeFlag("NOPAIN",1)
		N003 R 0 A_SetUserVar("user_sandvich",0)
		N003 S 2 A_PlaySound("Heavy/Sandvich",CHAN_WEAPON,1,0)
		N003 TTSSSSTTSSSSTTTSSSSSSSTSTTTSSSSSTTSSSS 2 HealThing(random(4,20))
		N003 U 35 A_FaceTarget
		N003 V 4 A_ChangeFlag("NOPAIN",0)//SECRET FRAME LOL
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 A 0 A_StopSound(CHAN_AUTO)
		TNT1 AAAA 0 A_SpawnItemEx("GibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 AAAAAA 0 A_SpawnItemEx("SmallGibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(10,40),0,0,0,0,1)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Fall
		TNT1 A 1 A_PlaySound("other/gibdeath",CHAN_AUTO,1)
		TNT1 A 35
		TNT1 A 0 A_KillMaster
		Stop
	Death.UnholyDisint:
	Death.PlasmaDisint:
	Disintegrate:
		"####" "#" 0 Thing_SetTranslation(0,50)
		"####" "#" 0 A_KillMaster
		"####" "#" 1 A_FadeOut(0.015)
		"####" "#" 0 A_ChangeFlag("NOINTERACTION",1)
		"####" "#" 0 A_Stop    
		"####" "#" 0 A_Fall
		"####" "#" 0 A_SpawnItemEx("DisintegrateShadowMediumBig",0,0,40,0,0,0,0,32)
		"####" "#" 0 A_SpawnItemEx("DisintegrateSparksMediumBig",0,0,40,0,0,0,0,32)
		"####" "##" 2 A_FadeOut(0.03)
		Goto Disintegrate+7

	Ice:
		"####" "#" 5 A_GenericFreezeDeath
		"####" "#" 0 A_KillMaster
		"####" "#" 1 A_FreezeDeathChunks
		Wait
	}
}

Actor HWMGSpawner : RandomSpawner
{
	DropItem "AEoDVulcanCannonItem" 255 2
	DropItem "AEoDHailstormItem" 255 2
}

Actor HeavyShotSprayer
{
	Projectile
	+NOINTERACTION
	+SEEKERMISSILE
	States
	{
	Spawn:
		TNT1 A 0
		//TNT1 A 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TRACER)
		//TNT1 A 0 A_SeekerMissile(90,90,2,256)
		//TNT1 A 0 A_FaceTracer
		//TNT1 AAAA 0 A_CustomMissile("DisruptorShotHeavy",0,0,random(-15,15),4)
		TNT1 A 0 A_SpawnItemEx("MuzzleFlashOrange",0,0,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("MuzzleFlashOrange",6,0,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("MuzzleFlashOrangeSmall",10,0,0,0,0,0,0,32)
		Stop
	}
}

Actor DisruptorShotHeavy : FastProjectile
{
	Speed 250
	Radius 4
	Height 4
	Damage (random(1,6))
	Scale 0.1
	+STRIFEDAMAGE
	+BLOODSPLATTER
	+DONTREFLECT
	DamageType "Plasma"
	MissileType DisruptorShotTrail
	RenderStyle Add
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,random(-10,30),0,1)//A_ChangeVelocity(0,0,random(-25,25),1)
	Movement:
		NB02 A 1 bright
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("DisruptorShotPuff",0,0,0,0,0,0,0,32)
		TNT1 A 3
		Stop
   }
}

//==============================================================================
//==============================================================================
//									The Scout
//==============================================================================
//==============================================================================

Actor TF2Scout //hellknight
{
	DropItem "HealthBottle" 64
	DropItem "ShellBigAmmo" 128
	DropItem "ShellBigAmmo" 128
	DropItem "HealthHeart" 210
	DropItem "PowerslaveSpawner" 28
	DropItem "PowerupSpawner" 8
	var int user_bonk[2];
	var int user_shell;
	var int user_reloading;
	Monster	RipLevelMin 1
	+NOEXTREMEDEATH
	+NORADIUSDMG
	+DONTMORPH
	+NOICEDEATH
	+MISSILEMORE
	+MISSILEEVENMORE
	+BOSS
	Health 500
	Radius 32
	Height 64
	Speed 15
	Scale 0.35
	PainChance 64
	//Flesh
	Obituary "Scout just proved that he is #1, not %o."
	SeeSound "Scout/Sight"
	PainSound "Scout/Pain"
	ActiveSound "Scout/Active"
	DeathSound "Scout/Death"
	DamageFactor "Electric", 1.25
	DamageFactor "AEPoison", 1.25
	DamageFactor "AEBullet", 1.5
	PainChance "Charm", 192
	PainChance "Terror", 192
	PainChance "Defile", 0
	PainChance "Drain", 110
	PainChance "Chaos", 255
	PainChance "ChaosExpl1", 255
	PainChance "ChaosExpl2", 255
//Armor
	DamageFactor "Electric", 1.25
	DamageFactor "Plasma", 0.75
	DamageFactor "PlasmaDisint", 0.75
	DamageFactor "AEBullet", 0.5
	States
	{
	Pain.Chaos:
		TNT1 A 0 A_CustomMissile("ChaosExpl1",0,0,0)
		TNT1 A 0 A_CustomMissile("ChaosExpl2",0,0,0)
		Goto See
	Pain.ChaosExpl1:
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",1)
		Goto Spawn
	Pain.ChaosExpl2:		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",0)		Goto See
	Pain.Charm:			TNT1 A 0 A_ChangeFlag("FRIENDLY",1)				Goto See
	Pain.Terror:		TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)			Goto See
	Pain.Drain:			TNT1 A 0 A_SpawnItemEx("DrainGiver",0,0,0,0,0,0,0,32,0)	Goto Pain
	
	Spawn:
		N004 A 0
		N004 A 0 A_SpawnItemEx("ScoutReloadChecker",0,0,0,0,0,0,0,48)
	Idle:
		N004 A 2 A_Look
		Loop
	CheckReload:
		N004 A 0 A_JumpIf(user_reloading==1,"ChasingReloadPend")
		N004 A 0 A_JumpIf(user_shell>=1,"ReloadRequired")
	See:
		N004 B 0 A_JumpIfCloser(128,"ChasingMelee")
		Goto ChasingGun
	ReloadRequired:
		N004 A 0 A_GiveInventory("ScoutReloadToken",user_shell)
		N004 A 0 A_SetUserVar("user_shell",0)
		N004 A 0 A_SetUserVar("user_reloading",1)
	ChasingReloadPend:
		N004 IIJJKKLL 2 A_Chase("","",CHF_FASTCHASE)
		N004 I 0 A_JumpIfInventory("ScoutReloadToken",1,"ChasingReloadPend")
		N004 I 0 A_SetUserVar("user_reloading",0)
		Goto See
	ChasingMelee:
		N004 B 0 A_UnSetInvulnerable 
		N004 BBCCDDEE 2 A_Chase("Melee","",CHF_FASTCHASE)
		N004 E 0 A_JumpIfCloser(256,"ChasingMelee")
		Goto ChasingGun
	ChasingGun:
		N004 B 0 A_UnSetInvulnerable 
		N004 I 0 A_JumpIfCloser(256,"ChasingMelee")
		N004 IIJJKKLL 2 A_Chase("Melee","Missile",CHF_FASTCHASE)
		Loop
	ChasingBonk:
		N004 B 0 A_SpawnItemEx("ScoutTrail1",0,0,0,0,0,0,0,32) N004 BB 1 A_Chase("","",CHF_FASTCHASE)
		N004 B 0 A_SpawnItemEx("ScoutTrail2",0,0,0,0,0,0,0,32) N004 CC 1 A_Chase("","",CHF_FASTCHASE)
		N004 B 0 A_SpawnItemEx("ScoutTrail3",0,0,0,0,0,0,0,32) N004 DD 1 A_Chase("","",CHF_FASTCHASE)
		N004 B 0 A_SpawnItemEx("ScoutTrail4",0,0,0,0,0,0,0,32) N004 EE 1 A_Chase("","",CHF_FASTCHASE)
		N004 B 0 A_SetUserArray("user_bonk",1,user_bonk[1]-1)
		N004 B 0 A_JumpIf(user_bonk[1]<=0,1)
		Loop
		N004 B 0 A_UnsetInvulnerable
		Goto See
	Missile:
		N004 A 6 A_FaceTarget
		N004 M 0 A_JumpIf(user_bonk[0]==1,2)
		N004 M 0 A_Jump(16,"Bonk","BoneDusterShot")
		//N004 M 0 A_Jump(16,"BoneDusterShot")
	Scattergun:
		N004 M 0 A_CustomBulletAttack(8,8,6,2,"NewPuff1")
		N004 M 0 A_PlaySound("Scout/ScatterGun/Fire",CHAN_WEAPON,1,0)
		N004 M 2 bright A_SpawnItemEx("MonMuzzleFlash",7,5,35,0,0,0,0,32)
		N004 MNN 2
		N004 O 12
		N004 O 0 A_SetUserVar("user_shell",user_shell+1)
		N004 O 0 A_JumpIf(user_shell>=6,"CheckReload")
		Loop
	BoneDusterShot:
		N004 A 0 A_PlaySound("BoneDuster/Reload",6,1,0)
		N004 OOO 35 A_FaceTarget
		N004 A 0 A_CustomBulletAttack(8,8,10,4,"NewPuff1")
		N004 A 0 A_PlaySound("BoneDuster/AltFire",CHAN_BODY,1,0)
		N004 A 0 A_Recoil(35)
		N004 A 0 A_SetUserVar("user_shell",6)
		N004 M 2 bright
		N004 MNN 2
		N004 O 12
		Goto CheckReload
	Melee:
		N004 F 0 A_FaceTarget
		N004 F 4 A_PlaySound("Scout/Bat/Swing",5,1,0)
		N004 G 0 A_CustomMeleeAttack(12,"Scout/Bat/Hit","")
		N004 GGGHH 2 A_FaceTarget
		Goto CheckReload
	Bonk:
		N004 B 0 A_SetUserArray("user_bonk",0,1)
		N004 B 0 A_SetUserArray("user_bonk",1,60)
		N004 B 0 A_SetInvulnerable
		N004 B 0 A_PlaySound("Scout/Invulnerable",CHAN_VOICE,1,0)
		Goto ChasingBonk
	Pain:
		N004 A 2 A_UnSetInvulnerable
		N004 A 2 A_Pain
		Goto CheckReload
	Death:
		TNT1 AAAA 0 A_SpawnItemEx("GibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 AAAAAA 0 A_SpawnItemEx("SmallGibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(10,40),0,0,0,0,1)
		TNT1 A 0 A_PlaySound("Scout/Death",CHAN_AUTO,1)
		TNT1 A 0 A_Fall
		TNT1 A 1 A_PlaySound("other/gibdeath",CHAN_AUTO,1)
		TNT1 A 35 A_GiveInventory("IDied",1)
		Stop		
	Death.UnholyDisint:
	Death.PlasmaDisint:
	Disintegrate:
		"####" "#" 0 Thing_SetTranslation(0,50)
		"####" "#" 1 A_FadeOut(0.015)
		"####" "#" 0 A_ChangeFlag("NOINTERACTION",1)
		"####" "#" 0 A_Stop    
		"####" "#" 0 A_Fall
		"####" "#" 0 A_SpawnItemEx("DisintegrateShadowMedium",0,0,30,0,0,0,0,32)
		"####" "#" 0 A_SpawnItemEx("DisintegrateSparksMedium",0,0,30,0,0,0,0,32)
		"####" "##" 2 A_FadeOut(0.03)
		Goto Disintegrate+6
	}
}

Actor ScoutReloadChecker
{
	Monster
	+NOINTERACTION
	+NONSHOOTABLE
	-SHOOTABLE
	-COUNTKILL
	+NODAMAGE
	States
	{
	Spawn:
		TNT1 A 0
	Idle:
		TNT1 A 5 A_JumpIfInventory("ScoutReloadToken",1,"Reloading",AAPTR_MASTER)
		Loop
	Reloading:
		TNT1 A 18 A_GiveInventory("ScoutScatterReload",1,AAPTR_MASTER)
		TNT1 A 0 A_TakeInventory("ScoutReloadToken",1,0,AAPTR_MASTER)
		TNT1 A 0 A_JumpIfInventory("ScoutReloadToken",1,"Reloading",AAPTR_MASTER)
		Goto Idle
	}
}

Actor ScoutReloadToken : Inventory
{	Inventory.MaxAmount 6	}

Actor ScoutScatterReload : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_PlaySound("Scout/ScatterGun/Reload",CHAN_WEAPON,1,0)
		Stop
	}
}

Actor ScoutTrail1
{
	+NOINTERACTION
	RenderStyle Add
	Scale 0.35
	Alpha 0.75
	States
	{
	Spawn:
		N004 B 2 A_FadeOut(0.03)
		Wait
	}
}

Actor ScoutTrail2 : ScoutTrail1
{
	States
	{
	Spawn:
		N004 C 2 A_FadeOut(0.03)
		Wait
	}
}

Actor ScoutTrail3 : ScoutTrail1
{
	States
	{
	Spawn:
		N004 D 2 A_FadeOut(0.03)
		Wait
	}
}

Actor ScoutTrail4 : ScoutTrail1
{
	States
	{
	Spawn:
		N004 E 2 A_FadeOut(0.03)
		Wait
	}
}

//==============================================================================
//==============================================================================
//									The Sniper
//==============================================================================
//==============================================================================

Actor TF2Sniper	//archvile
{
	DropItem "AEoDSniperRifleItem" 128
	DropItem "BulletBigAmmo" 128
	DropItem "BulletBigAmmo" 128
	DropItem "HealthHeart" 210
	DropItem "PowerslaveSpawner" 28
	DropItem "PowerupSpawner" 8
	var int user_charge;
	Monster	RipLevelMin 1
	+BOSS
	+BOSSDEATH
	+NORADIUSDMG
	+QUICKTORETALIATE
	+FULLVOLDEATH
	+MISSILEMORE
	Health 1000
	Radius 32
	Height 64
	Speed 8
	Scale 0.4
	PainChance 64
	SeeSound "Sniper/Sight"
	ActiveSound "Sniper/Active"
	PainSound "Sniper/Pain"
	DeathSound "Sniper/Death"
	PainChance "Charm", 96
	PainChance "Terror", 96
	PainChance "Drain", 64
	PainChance "Defile", 0
	PainChance "Chaos", 255
	PainChance "ChaosExpl1", 128
	PainChance "ChaosExpl2", 128
//Armor
	DamageFactor "Electric", 1.25
	DamageFactor "Plasma", 0.75
	DamageFactor "PlasmaDisint", 0.75
	DamageFactor "AEBullet", 0.5
	Obituary "%o forgot that 'sniper no sniping' doesn't work on the Sniper mercenary."
	States
	{
	Pain.Chaos:
		TNT1 A 0 A_CustomMissile("ChaosExpl1",0,0,0)
		TNT1 A 0 A_CustomMissile("ChaosExpl2",0,0,0)
		Goto See
	Pain.ChaosExpl1:
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",1)
		Goto Spawn
	Pain.ChaosExpl2:		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",0)		Goto See
	Pain.Charm:			TNT1 A 0 A_ChangeFlag("FRIENDLY",1)				Goto See
	Pain.Terror:		TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)			Goto See
	Pain.Drain:			TNT1 A 0 A_SpawnItemEx("DrainGiver",0,0,0,0,0,0,0,32,0)	Goto Pain
	Pain.Defile:		TNT1 A 0 A_GiveInventory("DefileCheck",1)	
	
	Spawn:
		N006 A 2 A_Look
		Loop
	See:
		N006 BBCCDDEE 3 A_FastChase
		Loop
	Missile:
		N006 F 0 A_Jump(256,"NormalShot","ExplosiveShot","SydneyShot")
	NormalShot:
		N006 F 0 A_PlaySound("Weapons/AWP/Zoom",CHAN_BODY,1,0)
		N006 FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 1 A_FaceTarget
		N006 F 0 A_PlaySound("Weapon/SniperFire",CHAN_WEAPON,1,0)
		N006 G 2 bright A_CustomMissile("SniperBulletNormal",38,0)
		N006 GHHII 2
		N006 J 2 A_PlaySound("Weapon/SniperCharge",CHAN_BODY,1,0)
		N006 JJII 2
		Goto See
	ExplosiveShot:
		N006 F 0 A_PlaySound("Weapons/AWP/Zoom",CHAN_BODY,1,0)
		N006 FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 1 A_FaceTarget
		N006 F 0 A_PlaySound("Weapon/SniperFire",CHAN_WEAPON,1,0)
		N006 G 2 bright A_CustomMissile("SniperBulletExplosive",38,0)
		N006 GHHII 2
		N006 J 2 A_PlaySound("Weapon/SniperCharge",CHAN_BODY,1,0)
		N006 JJII 2
		Goto See
	SydneyShot:
		N006 F 0 A_PlaySound("Weapons/AWP/Zoom",CHAN_BODY,1,0)
		N006 FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 1 A_FaceTarget
		N006 F 0 A_PlaySound("Weapon/SniperFire",CHAN_WEAPON,1,0)
		N006 G 2 bright A_CustomMissile("SniperBulletSydney",38,0)
		N006 GHHII 2
		N006 J 2 A_PlaySound("Weapon/SniperCharge",CHAN_BODY,1,0)
		N006 JJII 2
		Goto See
	Pain:
		N006 A 2
		N006 A 4 A_Pain
		Goto See
	Death:
		TNT1 AAAA 0 A_SpawnItemEx("GibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 AAAAAA 0 A_SpawnItemEx("SmallGibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(10,40),0,0,0,0,1)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Fall
		TNT1 A 1 A_PlaySound("other/gibdeath",CHAN_AUTO,1)
		TNT1 A 35 A_GiveInventory("IDied",1)
		Stop
	Death.UnholyDisint:
	Death.PlasmaDisint:
	Disintegrate:
		"####" "#" 0 Thing_SetTranslation(0,50)
		"####" "#" 1 A_FadeOut(0.015)
		"####" "#" 0 A_ChangeFlag("NOINTERACTION",1)
		"####" "#" 0 A_Stop    
		"####" "#" 0 A_Fall
		"####" "#" 0 A_SpawnItemEx("DisintegrateShadowMedium",0,0,30,0,0,0,0,32)
		"####" "#" 0 A_SpawnItemEx("DisintegrateSparksMedium",0,0,30,0,0,0,0,32)
		"####" "##" 2 A_FadeOut(0.03)
		Goto Disintegrate+6
	}
}

Actor SniperBulletNormal : FastProjectile
{
	+BLOODSPLATTER
	Projectile
	Damage (random(80,190))
	Speed 80
	Radius 4
	Height 6
	MissileType "AWPTrail"
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Crash:
	Death:
		TNT1 A 0 A_SpawnItemEx("NewPuff1",0,0,0,0,0,0,0,32)
		Stop
	XDeath:
		TNT1 A 0
		Stop
	}
}

Actor SniperBulletExplosive : FastProjectile
{
	Projectile
	+BLOODSPLATTER
	+FORCERADIUSDMG
	Damage (random(10,60))
	Speed 80
	Radius 4
	Height 6
	MissileType "AWPTrailBlack"
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("PBExplosionDebrisless",0,0,0,0,0,0,0,32)
		TNT1 A 0 A_PlaySound("weapons/rockexplo")
		TNT1 A 0 A_Explode(100,128,1)
		Stop
	}
}

Actor AWPTrailBlack
{
	+NOINTERACTION
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("AWPTrailSmokeBlack",0,frandom(-2,2),frandom(-2,2),0,frandom(-1,1),frandom(-1,1),0,SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor AWPTrailSmokeBlack
{
	Projectile
	+NOINTERACTION
	Scale 0.15
	Alpha 0.4
	RenderStyle Translucent
	Translation "0:255=%[0,0,0]:[0,0,0]"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"Two","Three")
	One:
		X006 ABCDEFGHIJKLMNOP 1 //bright 
		X006 P 1 bright A_FadeOut(0.35)
		Wait
	Two:
		X007 ABCDEFGHIJ 1 //bright
		X007 J 1 A_FadeOut(0.02) 
		Wait
	Three:
		X033 ABCDEFGHIJKLMNOPQR 1
		X033 R 1 A_FadeOut(0.04)
		Wait
	}
}

Actor SniperBulletSydney : FastProjectile
{
	var int user_jarate;
	Projectile
	+SEEKERMISSILE
	Damage 5
	Speed 120
	Radius 4
	Height 6
	MissileType "AWPTrailYellow"
	Obituary "%o was humiliated by the Jarate Sniper."
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_SeekerMissile(0,0,3,256)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_JumpIfTracerCloser(64,"Stickening")
		TNT1 A 0 A_SpawnItemEx("NewPuff1",0,0,0,0,0,0,0,32)
		Stop
	Stickening:
		TNT1 A 0 A_JumpIfInventory("PlayerToken",1,"StickyPlayer",AAPTR_TRACER)
		TNT1 A 0 A_GiveInventory("PowerJarate",1,AAPTR_TRACER)
		TNT1 A 0 A_SetUserVar("user_jarate",35*5)
		Goto StickyTime
	StickyPlayer:
		TNT1 A 0 A_GiveInventory("JarateEffect",1,AAPTR_TRACER)
		TNT1 A 0 A_SetUserVar("user_jarate",35*5)
		Goto StickyTime
	StickyTime:
		TNT1 A 0 A_JumpIfInventory("PowerInvulBase",1,"End",AAPTR_TRACER)
		TNT1 A 0 A_JumpIfInventory("PowerNoSeek",1,"End",AAPTR_TRACER)
		TNT1 A 0 A_Warp(AAPTR_TRACER,0,0,32,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SpawnItemEx("LemonJuiceBig",64,0,0,0,0,0,random(0,359),32,225)
		TNT1 A 0 A_SetUserVar("user_jarate",user_jarate-1)
		TNT1 A 1 A_JumpIf(user_jarate<=0,"End")
		Loop
	End:
		TNT1 A 0 A_TakeInventory("PowerJarate",1,0,AAPTR_TRACER)
		TNT1 A 0 A_TakeInventory("JarateEffect",1,0,AAPTR_TRACER)
		Stop
	}
}

Actor AWPTrailYellow
{
	+NOINTERACTION
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("AWPTrailSmokeYellow",0,frandom(-2,2),frandom(-2,2),0,frandom(-1,1),frandom(-1,1),0,SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor AWPTrailSmokeYellow
{
	Projectile
	+NOINTERACTION
	Scale 0.15
	Alpha 0.4
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[1,1,0]"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"Two","Three")
	One:
		X006 ABCDEFGHIJKLMNOP 1 //bright 
		X006 P 1 bright A_FadeOut(0.35)
		Wait
	Two:
		X007 ABCDEFGHIJ 1 //bright
		X007 J 1 A_FadeOut(0.02) 
		Wait
	Three:
		X033 ABCDEFGHIJKLMNOPQR 1
		X033 R 1 A_FadeOut(0.04)
		Wait
	}
}

Actor JarateEffect : PowerupGiver
{
	Powerup.Colormap 0.0,0.0,0.0,1.0,1.0,0.0
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 0
	Powerup.Type "Jarate"
	Powerup.Duration 2100
	//Powerup.Color YellowMap
}

Actor PowerJarate : PowerProtection
{
	DamageFactor 1.15
}

//==============================================================================
//==============================================================================
//									The Spy
//==============================================================================
//==============================================================================

Actor TF2Spy		//archvile
{
	DropItem "HealthBottle" 192
	DropItem "HealthBottle" 192
	DropItem "HealthBottle" 192
	DropItem "HealthBottle" 192
	DropItem "AEoDSpyKnifeItem" 128
	DropItem "HealthHeart" 210
	DropItem "PowerslaveSpawner" 28
	DropItem "PowerupSpawner" 8
	var int user_count;
	var int user_bullet;
	var int user_cloak[3];
	//[0] = Counter for going to and from cloaking
	//[1] = Boolean: Cloaked? Yes or no.
	//[2] = Chase counter
	var int user_threat;
	var int user_deadringer;
	Monster	RipLevelMin 1
	+BOSS
	+BOSSDEATH
	+NOICEDEATH
	+DONTMORPH
	+MISSILEMORE
	+DONTBLAST
	+LOOKALLAROUND
	+NOFEAR
	Radius 16
	Height 64
	Scale 0.35
	Health 750
	Speed 8
	PainChance 64
	SeeSound "Spy/Sight"
	ActiveSound "Spy/Active"
	PainSound "Spy/Pain"
	DeathSound "Spy/Death"
	MeleeRange 64
	PainChance "Charm", 96
	PainChance "Terror", 96
	PainChance "Drain", 64
	PainChance "Defile", 0
	PainChance "Chaos", 255
	PainChance "ChaosExpl1", 128
	PainChance "ChaosExpl2", 128
//Armor
	DamageFactor "Electric", 1.25
	DamageFactor "Plasma", 0.75
	DamageFactor "PlasmaDisint", 0.75
	DamageFactor "AEBullet", 0.5
	Obituary "%o was fancily outsassed by the Spy."
	HitObituary "%o couldn't keep the snaking stabs of the Spy off of %g"
	States
	{
	Pain.Chaos:
		TNT1 A 0 A_CustomMissile("ChaosExpl1",0,0,0)
		TNT1 A 0 A_CustomMissile("ChaosExpl2",0,0,0)
		Goto See
	Pain.ChaosExpl1:
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",1)
		Goto Spawn
	Pain.ChaosExpl2:		TNT1 A 0 A_ChangeFlag("QUICKTORETALIATE",0)		Goto See
	Pain.Charm:			TNT1 A 0 A_ChangeFlag("FRIENDLY",1)				Goto See
	Pain.Terror:		TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)			Goto See
	Pain.Drain:			TNT1 A 0 A_SpawnItemEx("DrainGiver",0,0,0,0,0,0,0,32,0)	Goto Pain
	Pain.Defile:		TNT1 A 0 A_GiveInventory("DefileCheck",1)
	
	Spawn:
		N055 A 0
		N055 A 0 A_GiveInventory("ImaSpyToken",1)
	Idle:
		N055 A 10 A_Look
		Loop
	See:
		TNT1 A 0 A_SetUserVar("user_bullet",0)
		N055 A 0 A_SetShootable
		N055 A 0 A_JumpIf(alpha<=0.0,"ChaseCloaked")
		N055 A 0 A_ChangeFlag("NOPAIN",0)
		N055 BBCCDDEE 2 A_FastChase
		N055 A 0 A_SetUserArray("user_cloak",1,user_cloak[1]-16)
		Loop
	ChaseNoFire:
		TNT1 A 0 A_SetUserVar("user_bullet",0)
		N055 A 0 A_SetShootable
		N055 A 0 A_JumpIf(alpha>=1.0,"See")
		N055 A 0 A_ChangeFlag("NOPAIN",0)
		N055 BBCCDDEE 2 A_Chase("","",CHF_FASTCHASE)
		Loop
	ChaseCloaked:
		TNT1 A 0 A_SetUserVar("user_bullet",0)
		N055 A 0 A_UnsetShootable
		N055 A 0 A_ChangeFlag("NOPAIN",0)
		N055 A 0 A_JumpIfTargetInLOS(1,0,JLOSF_NOSIGHT|JLOSF_DEADNOJUMP) //If it's dead, just decloak.
		Goto DecloakNormal
		N055 A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		N055 A 0 A_JumpIfTargetInLOS(1,0) //If he can't see them anymore, decloak.
		Goto DecloakNormal
		N055 A 0 A_JumpIfCloser(64,1)
		Goto ChaseCloaked
		N055 A 0 A_JumpIfTargetInLOS("ChaseCloaked",220,JLOSF_TARGETLOS)
		Goto DecloakBackstabReady
	Missile:
		N055 A 0 A_FaceTarget
		N055 A 0 A_JumpIf(user_bullet>=6,"MisAbort")
		N055 A 0 A_JumpIfCloser(512,"Cloak")
		N055 F 0 A_CustomMissile("SpyScreamerShot",22,12)
		N055 F 0 A_PlaySound("Screamer/Fire",CHAN_WEAPON,1,0)
		N055 F 5 bright
		N055 GAA 4 A_FaceTarget
		N055 A 0 A_SetUserArray("user_cloak",1,user_cloak[1]-17)
		N055 A 0 A_SetUserVar("user_bullet",user_bullet+1)
		N055 A 0 A_MonsterRefire(0,"See")
		Loop
	MisAbort:
		TNT1 A 0 A_SetUserVar("user_bullet",0)
		TNT1 A 0 A_Jump(256,"See","Cloak")
	Melee:
		N055 S 0 A_JumpIfTargetInLOS(1,220,JLOSF_TARGETLOS,64)
		Goto Backstab
		N055 P 6 A_PlaySound("Scout/Bat/Swing",5,1,0)
		N055 Q 6 A_CustomMeleeAttack(20,"bfk/hit","")
		N055 R 4
		Goto See
	Backstab:
		N055 S 0 A_PlaySound("Scout/Bat/Swing",5,1,0)
		N055 SSTU 1 A_FaceTarget
		N055 U 0 A_JumpIfInventory("PlayerToken",1,1,AAPTR_TARGET)
		Goto BackstabSuccess
		N055 U 0 A_JumpIfTargetInLOS("BackstabFail",220,JLOSF_TARGETLOS,64)
		Goto BackstabSuccess
	BackstabSuccess:
		N055 V 0 A_JumpIfInventory("PlayerToken",1,"BSSuccessPlayer",AAPTR_TARGET)
		N055 V 8 A_CustomMeleeAttack(random(1000,3000),"crithit","") //Monsters are insta-killed if not invulnerable.
		N055 UT 2
		Goto See
	BSSuccessPlayer:
		N055 V 8 A_CustomMeleeAttack(random(50,200),"crithit","")
		N055 UT 2
		Goto See
	BackstabFail:
		N055 V 8 A_CustomMeleeAttack(20,"bfk/hit","")
		N055 UT 2
	Cloak:
		TNT1 A 0 A_SetUserVar("user_bullet",0)
		N055 A 0 A_ChangeFlag("NOPAIN",1)
		N055 A 0 A_PlaySound("spycrab/cloak",5,1,0)
		N055 A 0 A_SpawnItemEx("SpyCloakSetter",0,0,0,0,0,0,0,32)
	Cloaking:
		N055 A 0 A_FaceTarget
		N055 A 0 A_JumpIf(alpha<=0.0,"ChaseCloaked")
		N055 A 1 A_JumpIfCloser(256,"CloakRetreat")
		Loop
	CloakRetreat:
		N055 H 0 A_JumpIf(alpha<=0.0,"ChaseCloaked")
		N055 H 0 A_FaceTarget
		N055 HH 2 A_ChangeVelocity(-4,0,0,1)
		N055 H 0 A_JumpIfCloser(256,1)
		Goto Cloaking
		N055 H 0 A_JumpIf(alpha<=0.0,"ChaseCloaked")
		N055 H 0 A_FaceTarget
		N055 II 2 A_ChangeVelocity(-4,0,0,1)
		N055 H 0 A_JumpIfCloser(256,1)
		Goto Cloaking
		N055 H 0 A_JumpIf(alpha<=0.0,"ChaseCloaked")
		N055 H 0 A_FaceTarget
		N055 JJ 2 A_ChangeVelocity(-4,0,0,1)
		N055 H 0 A_JumpIfCloser(256,1)
		Goto Cloaking
		N055 H 0 A_JumpIf(alpha<=0.0,"ChaseCloaked")
		N055 H 0 A_FaceTarget
		N055 KK 2 A_ChangeVelocity(-4,0,0,1)
		N055 H 0 A_JumpIfCloser(256,"CloakRetreat")
		Goto Cloaking
	DecloakNormal:
		N055 A 0 A_SetShootable
		N055 A 0 A_SpawnItemEx("SpyCloakDesetter",0,0,0,0,0,0,0,32)
		N055 A 0 A_JumpIf(user_deadringer==1,"DecloakEnd")
		N055 A 0 A_PlaySound("spycrab/uncloak",7,1,0)
		Goto DecloakEnd+2
	DecloakEnd:
		N055 A 0 A_SetUserVar("user_deadringer",2)
		N055 A 0 A_PlaySound("spycrab/deadringer",7,1,0)
		N055 A 0 A_SetUserArray("user_cloak",1,350)
		Goto ChaseNoFire
	DecloakDR:
		N055 A 0 A_UnhideThing
		N055 A 0 A_SpawnItemEx("SpyCloakDesetter",0,0,0,0,0,0,0,32)
	Waiting:
		N055 A 1 A_JumpIf(alpha>=1.0,"See")
		Loop
	DecloakBackstabReady:
		N055 A 0 A_SetShootable
		N055 A 0 A_SpawnItemEx("SpyCloakDesetter",0,0,0,0,0,0,0,32)
		N055 A 0 A_JumpIf(user_deadringer==1,"DSZing")
		N055 A 0 A_PlaySound("spycrab/uncloak",7,1,0)
		Goto DSZing+2
	DSZing:
		N055 A 0 A_SetUserVar("user_deadringer",2)
		N055 A 0 A_PlaySound("spycrab/deadringer",7,1,0)
		N055 A 0 A_SetUserArray("user_cloak",1,350)
	DecloakBSWait:
		N055 S 1 A_JumpIf(alpha>=1.0,"Melee")
		Loop
	Pain:
		TNT1 A 0 A_SetUserVar("user_bullet",0)
		N055 A 0 A_JumpIf(user_deadringer>=1,"NormalPain")
		N055 A 0 A_JumpIfHealthLower(300,1)
		Goto NormalPain
		N055 A 0 A_Jump(32,"DeadRinger")
	NormalPain:
		N055 A 2
		N055 A 2 A_Pain
		N055 A 0 A_JumpIf(alpha<=0.0,2)
		N055 A 0 A_Jump(32,"Cloak")
		Goto See
	DeadRinger:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_SetUserVar("user_deadringer",1)
		TNT1 A 0 A_FadeTo(0,1,0)
		TNT1 A 0 A_Stop
		TNT1 AAAA 0 A_SpawnItemEx("GibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 AAAAAA 0 A_SpawnItemEx("SmallGibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(10,40),0,0,0,0,1)
		TNT1 A 0 A_Pain
		TNT1 A 1 A_PlaySound("other/gibdeath",CHAN_AUTO,1)
		Goto ChaseCloaked
	Death:
		TNT1 A 0 A_FadeTo(1,1,0)
		TNT1 A 0 A_Stop
		TNT1 AAAA 0 A_SpawnItemEx("GibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 AAAAAA 0 A_SpawnItemEx("SmallGibDropper",0,0,random(20,40),0,0,0,0,33)
		TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(10,40),0,0,0,0,1)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Fall
		TNT1 A 1 A_PlaySound("other/gibdeath",CHAN_AUTO,1)
		TNT1 A 35 A_GiveInventory("IDied",1)
		Stop
	Death.UnholyDisint:
	Death.PlasmaDisint:
	Disintegrate:
		"####" "#" 0 Thing_SetTranslation(0,50)
		"####" "#" 1 A_FadeOut(0.015)
		"####" "#" 0 A_ChangeFlag("NOINTERACTION",1)
		"####" "#" 0 A_Stop    
		"####" "#" 0 A_Fall
		"####" "#" 0 A_SpawnItemEx("DisintegrateShadowMedium",0,0,30,0,0,0,0,32)
		"####" "#" 0 A_SpawnItemEx("DisintegrateSparksMedium",0,0,30,0,0,0,0,32)
		"####" "##" 2 A_FadeOut(0.03)
		Goto Disintegrate+6
	}
}

Actor ImaSpyToken : Inventory
{	Inventory.MaxAmount 1 }

Actor SpyScreamerShot : FastProjectile
{
	Projectile
	+FORCERADIUSDMG
	DamageType "AEBullet"
	Radius 4
	Height 4
	Speed 80
	Scale 0.25
	Damage 5
	States
	{
	Spawn:
		MISL A 1
		Loop
	Death:
		TNT1 A 0 A_Explode(random(30,50),32,1)
		TNT1 A 1 A_SpawnItemEx("NewExplosionSmall",0,0,0,0,0,0,0,32)
		Stop
	}
}

Actor SpyCloakSetter
{
	Projectile
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_GiveInventory("SpyCloakerItem",1,AAPTR_TARGET)
		Stop
	}
}

Actor SpyCloakDesetter
{
	Projectile
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_GiveInventory("SpyDecloakerItem",1,AAPTR_TARGET)
		Stop
	}
}

Actor SpyCloakDesetterFast
{
	Projectile
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAA 1 A_GiveInventory("SpyDecloakerFastItem",3,AAPTR_TARGET)
		Stop
	}
}

Actor SpyCloakerItem : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_FadeTo(0,0.03,0)
		Stop
	}
}

Actor SpyDecloakerItem : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_FadeTo(1,0.03,0)
		Stop
	}
}

Actor SpyDecloakerFastItem : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_FadeTo(1,0.06,0)
		Stop
	}
}