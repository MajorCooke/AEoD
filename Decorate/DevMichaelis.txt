
// Basically, _the_ Face Melter.
//
// Primary fires heavy stream of plasma that vaporizes smaller things and melts bigger.
// Accelerates and decelerates too slow for immediate action.
// Secondary fires a few bolts instantly, but reloads even slower.

Actor AEoDFusionChaingun : Weapon // 6072
{
	+FLOORCLIP
	+INVENTORY.IGNORESKILL
	Weapon.AmmoType1 "PlasmaRod"
	Weapon.AmmoType2 "PlasmaRod"
	Weapon.AmmoGive1 60
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.Kickback 10
	Weapon.SelectionOrder 10950
	Inventory.PickupMessage "You got the Fusion Chaingun!"
	Inventory.PickupSound "reilsss/weappickup/plasmagun"
	Weapon.UpSound "weapons/plasma/draw"
	Scale 0.125
	Tag "Fusion Chaingun"
	States
	{
	Spawn:
		O002 Z -1
		Loop
	Ready:
		O002 C 1 Offset(192, 191) 
		O002 C 1 Offset(189, 176)
		O002 A 1 Offset(179, 159)
		O002 A 1 Offset(167, 134)
		O002 B 1 Offset(148, 116)
		O002 B 1 Offset(123, 110)
	ReallyReady:
		TNT1 A 0 A_PlaySound("reilsss/weapons/plasmaidle1", CHAN_BODY, 1.0, 1)
		TNT1 B 0 A_JumpIfInventory("PowerQuickFire",1,"ReallyReadyQuick")
		O002 B 4 Offset(121, 109) A_WeaponReady(WRF_NOBOB)	// Bobbing cancels custom offsets
		O002 A 4 Offset(121, 109) A_WeaponReady(WRF_NOBOB)
		O002 C 4 Offset(121, 109) A_WeaponReady(WRF_NOBOB)
		O002 A 4 Offset(121, 109) A_WeaponReady(WRF_NOBOB)
		Goto ReallyReady+1
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		O002 B 1 Offset(123, 110)
		O002 B 1 Offset(148, 116)
		O002 A 1 Offset(167, 134)
		O002 A 1 Offset(179, 159)
		O002 C 1 Offset(189, 176)
		O002 C 1 Offset(192, 191) 
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Wait
	Fire:
	WindUp:
		TNT1 A 0 A_PlaySound("reilsss/weapons/FusionChaingunfire", CHAN_WEAPON, 1.0, 0)
		TNT1 A 0 A_GunFlash
		O002 A 4 Offset(124, 110) Bright
		O002 B 3 Offset(125, 109) Bright
		O002 D 2 Offset(125, 110) Bright
		O002 E 2 Offset(126, 110) Bright
		O002 F 2 Offset(127, 110) Bright
		O002 G 2 Offset(128, 110) Bright
		O002 H 2 Offset(129, 110) Bright
		O002 D 1 Offset(129, 110) Bright
		O002 E 1 Offset(130, 112) Bright
		O002 F 1 Offset(131, 113) Bright
		O002 G 1 Offset(132, 114) Bright
		O002 H 1 Offset(133, 114) Bright
		Goto ActuallyFire
	ActuallyFire:
		TNT1 A 0 A_JumpIfInventory("PowerQuickFire",1,"ActuallyFireQuick")
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_GunFlash
		O002 D 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 E 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 F 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 G 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 H 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		TNT1 A 0 A_Refire("ActuallyFire")
		Goto WindDown
	WindDown:
		TNT1 A 0 A_JumpIfInventory("PowerQuickFire",1,"WindDownQuick")
		TNT1 A 0 A_PlaySound("reilsss/weapons/FusionChaingunfire", CHAN_WEAPON, 1.0, 0)
		TNT1 A 0 A_GunFlash
		O002 D 1 Offset(133, 114) Bright
		O002 E 1 Offset(132, 113) Bright
		O002 F 1 Offset(131, 112) Bright
		O002 G 1 Offset(130, 113) Bright
		O002 H 1 Offset(129, 114) Bright
		O002 D 2 Offset(128, 113) Bright
		O002 E 2 Offset(127, 112) Bright
		O002 F 2 Offset(126, 111) Bright
		O002 G 2 Offset(125, 110) Bright
		O002 H 2 Offset(124, 110) Bright
		O002 B 3 Offset(123, 109) Bright
		O002 A 4 Offset(122, 110) Bright
		Goto ReallyReady
		
	ReallyReadyQuick:
		TNT1 A 0 A_PlaySound("reilsss/weapons/plasmaidle1", CHAN_BODY, 1.0, 1)
		O002 B 2 Offset(121, 109) A_WeaponReady(WRF_NOBOB)
		O002 A 2 Offset(121, 109) A_WeaponReady(WRF_NOBOB)
		O002 C 2 Offset(121, 109) A_WeaponReady(WRF_NOBOB)
		O002 A 2 Offset(121, 109) A_WeaponReady(WRF_NOBOB)
		Goto ReallyReady+1
	FireQuick:
	WindUpQuick:
		O002 A 2 Offset(122, 110) Bright 
		O002 B 1 Offset(123, 110) Bright 
		O002 D 1 Offset(125, 110) Bright 
		O002 E 1 Offset(125, 110) Bright 
		O002 F 1 Offset(125, 110) Bright
		O002 G 1 Offset(125, 110) Bright
		O002 H 1 Offset(125, 110) Bright
		O002 D 1 Offset(125, 110) Bright
		O002 F 1 Offset(129, 113) Bright
		O002 H 1 Offset(130, 114) Bright
		Goto ActuallyFire
	ActuallyFireQuick:
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_GunFlash
		O002 D 0 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 D 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 E 0 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 E 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 F 0 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 F 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 G 0 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 G 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 H 0 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 H 1 Offset(135, 114) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		TNT1 A 0 A_Refire("ActuallyFireQuick")	// Lets player enjoy double-density volley even after powerup is over, because it won't last for long anyway.
		Goto WindDown
	WindDownQuick:
		O002 D 1 Offset(130, 114) Bright
		O002 F 1 Offset(129, 114) Bright
		O002 H 1 Offset(128, 113) Bright
		O002 D 1 Offset(127, 113) Bright
		O002 E 2 Offset(126, 112) Bright
		O002 F 1 Offset(125, 112) Bright
		O002 G 1 Offset(125, 111) Bright
		O002 H 1 Offset(124, 110) Bright
		O002 B 2 Offset(123, 109) Bright
		O002 A 2 Offset(122, 110) Bright
		Goto ReallyReady
		
	AltFire:
		TNT1 A 0 A_Recoil(5)
		TNT1 A 0 A_JumpIfInventory("PowerQuickFire",1,"AltFireQuick")
		TNT1 A 0 A_GunFlash
		O002 D 1 Offset(125, 113) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 E 1 Offset(124, 112) Bright
		TNT1 A 0 A_GunFlash
		O002 F 1 Offset(123, 112) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 G 1 Offset(123, 112) Bright
		O002 H 1 Offset(122, 111) Bright 
		TNT1 A 0 A_GunFlash
		O002 D 2 Offset(122, 111) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 E 2 Offset(122, 110) Bright
		O002 F 2 Offset(121, 110) Bright 
		O002 G 2 Offset(121, 109) Bright
		O002 H 2 Offset(121, 109) Bright 
		Goto ReallyReady
	AltFireQuick:
		TNT1 A 0 A_GunFlash("AltFlash")
		O002 D 1 Offset(125, 113) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 E 1 Offset(124, 112) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 F 1 Offset(123, 111) Bright 
		TNT1 A 0 A_GunFlash("AltFlash")
		O002 G 2 Offset(122, 110) Bright A_FireCustomMissile("FusionChaingunBall", 0, 1, 6, 3)
		O002 H 2 Offset(121, 109) Bright 
		Goto ReallyReady

	Flash:
		TNT1 AAAAA 1 A_Light(random(1,random(1,random(1,8))))
		TNT1 A 0 A_Light0
		Stop
	AltFlash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		TNT1 A 0 A_Light0
		Stop
	}
}

Actor FusionChaingunBall
{
	var int user_deviation; // one third amount to move outward before setting on course as a power of 2.
	var int user_xs; // \
	var int user_ys; //  } original vector to be restored after deviation
	var int user_zs; // /
	PROJECTILE
	+DROPOFF
	+NOBLOCKMAP
	+NOGRAVITY
	+SKYEXPLODE
	+DONTSPLASH
	+HITTRACER
	Speed 35
	Height 8
	Radius 5
	Damage (random(10,50))
	RenderStyle Add
	DamageType "Plasma"
	Alpha 1
	Scale 0.15
	//Translation "160:167=%[0.0,0.0,0.0]:[1.5,0.0,0.0]", "232:239=%[0.0,0.0,0.0]:[2.0,0.0,0.0]", "48:79=%[0.8,0.0,0.0]:[2.0,1.0,1.0]"
	States
	{
	Spawn:
		TNT1 AA 0 A_ScaleVelocity(frandom(0.9,1/0.9))
		TNT1 A 0 A_SetUserVar(user_xs, velx)
		TNT1 A 0 A_SetUserVar(user_ys, vely)
		TNT1 A 0 A_SetUserVar(user_zs, velz)
		TNT1 A 0 A_SetUserVar(user_deviation, random(-14,14))
		TNT1 A 0 A_SpawnItemEx("FusionChaingunFlare", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_ORIGINATOR|SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("FusionChaingunTrail", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_ORIGINATOR|SXF_CLIENTSIDE)
	aLooper:
		R053 ABC 1 Bright A_ChangeVelocity(0,random(-random(0,2),random(0,2)),random(-random(0,1),random(0,1)), CVF_RELATIVE)
		TNT1 A 0 A_SetUserVar(user_deviation, user_deviation / 2)
		TNT1 A 0 A_JumpIf(user_deviation == 0, "Blooper")
		Loop
	Blooper:
		R053 A 1 Bright A_ChangeVelocity(user_xs,user_ys,user_zs)
		R053 A 0 A_ScaleVelocity(0.4)
		R053 B 1 Bright A_ChangeVelocity(user_xs,user_ys,user_zs)
		R053 B 0 A_ScaleVelocity(0.4)
		R053 C 1 Bright A_ChangeVelocity(user_xs,user_ys,user_zs)
		R053 C 0 A_ScaleVelocity(0.5)
		R053 A 1 Bright A_ChangeVelocity(user_xs,user_ys,user_zs)
		R053 A 0 A_ScaleVelocity(0.5)
		R053 B 1 Bright A_ChangeVelocity(user_xs,user_ys,user_zs)
		R053 B 0 A_ScaleVelocity(0.6)
		R053 C 1 Bright A_ChangeVelocity(user_xs,user_ys,user_zs)
		R053 C 0 A_ScaleVelocity(0.6)
	Clooper:
		R053 ABC 1 Bright
		Loop
	XDeath:
	Crash:
		// Monsters less than 1000 hp tough enjoy additional AoE damage
		"####" "#" 0 A_JumpIfHealthLower(1000,"AlsoBoom",AAPTR_TRACER)
	Death:
		"####" "#" 0 A_RemoveChildren(true,RMVF_EVERYTHING)
	DeathLooper:
		"####" "#" 1 Bright A_SetScale(scalex*1.25)
		"####" "#" 0 A_FadeOut(0.15)
		Loop
	AlsoBoom:
		"####" "#" 0 A_SpawnItemEx("FusionChaingunBoom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto Death
	}
}

Actor FusionChaingunFlare
{
	+NOINTERACTION
	Speed 0
	RenderStyle Add
	Alpha 0.0
	Scale 0.18
	//Translation "0:255=%[0,0,0]:[1.0,0.5,0.2]"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4,5,6)
		//R053 DEFGH 1 A_JumpIf(true,"Looper")
		//MUZ2 ABCDEF 1 A_JumpIf(true,"Looper")
	Looper:
		R053 DEFGH 1 Bright A_Warp(AAPTR_MASTER, -5,0,0,0, WARPF_INTERPOLATE)
		//"####" "#" 1 Bright A_Warp(AAPTR_MASTER, -5,0,0,0, WARPF_INTERPOLATE)
		"####" "#" 0 A_FadeTo(1)
		Goto Looper
	}
}

Actor FusionChaingunTrail : FusionChaingunFlare
{
	Translation "0:255=%[0,0,0]:[1.0,0.3,0.2]"
	Alpha 0.75
	Scale 0.1
	States
	{
	Spawn:
		X101 ABCDEDFGHIJKLMNOP 1 A_Warp(AAPTR_MASTER, -10,0,0,0, WARPF_INTERPOLATE)
		Loop
	}
}

Actor FusionChaingunBoom : FusionChaingunFlare
{
	Alpha 1
	Scale 0.22
	DamageType "Fire"	// Yep it's plasma but plasma is hot so...
	+EXTREMEDEATH
	States
	{
	Spawn:
		R053 KK 0 A_Explode(50,50,0,0,32)
		R053 KKLLMMNNOOOPPPPQQQQ 1 Bright A_FadeOut(0.04)
		Stop
	}
}

Actor FusionChaingunDrop : CustomInventory
{
	States
	{
	Use:
		TNT1 A 0
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("DroppedFusionChaingun",0,0,32,8,0,0,0,SXF_NOCHECKPOSITION,0)
		Stop
	}
}

Actor DroppedFusionChaingun
{
	Radius 16
	Height 20
	Scale 0.125
	States
	{
	Spawn:
		O002 Z 15
		TNT1 A 0 A_SpawnItemEx("AEoDFusionChaingunNoAmmo",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER,0)
		Stop
	}
}

Actor AEoDFusionChaingunItem : CustomInventory // 6072
{
	+FLOORCLIP
	+INVENTORY.IGNORESKILL
	Scale 0.125
	Inventory.Amount 1
	Inventory.PickupMessage "You got the Fusion Chaingun!"
	Inventory.PickupSound "reilsss/weappickup/plasmagun"
	States
	{
	Spawn:
		O002 Z -1
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("AEoDFusionChaingun",1,"Ammo")
		TNT1 A 0 A_JumpIf(CallACS("WeaponSlotLimitCheck",5) <= 0,"Nothing")
		TNT1 A 0 A_GiveInventory("Weapon5Count",1)
		TNT1 A 0 A_GiveInventory("FusionChaingunDrop",1)
		TNT1 A 0 A_GiveInventory("AEoDFusionChaingun",1)
		TNT1 A 0 	
		Stop
	Nothing:
		TNT1 A 0 A_JumpIfInventory("AEoDFusionChaingun",1,"Ammo")
		TNT1 A 0 A_JumpIfInventory("LimitScriptActive",1,1)
		Goto Pickup+2
		TNT1 A 0 ACS_NamedExecute("s798",0,5,61,0)
		Fail
	Ammo:
		TNT1 A 0 A_GiveInventory("AEoDFusionChaingun",1)
		Stop
	}
}

Actor AEoDFusionChaingunNoAmmo : AEoDFusionChaingunItem
{
	+FLOORCLIP
	+INVENTORY.IGNORESKILL
	Scale 0.125
	Inventory.PickupMessage "You got the Plasma Rifle!"
	Inventory.PickupSound "reilsss/weappickup/plasmagun"
	States
	{
	Spawn:
		O002 Z -1
		Loop
	Pickup:
		TNT1 A 0 A_JumpIf(CallACS("WeaponSlotLimitCheck",5) <= 0,"Nothing")
		TNT1 A 0 A_JumpIfInventory("PlasmaRod",60,"Ammo")
		TNT1 A 0 A_GiveInventory("Weapon5Count",1)
		TNT1 A 0 A_GiveInventory("FusionChaingunDrop",1)
		TNT1 A 0 A_GiveInventory("AEoDFusionChaingun",1)
		TNT1 A 0 A_TakeInventory("PlasmaRod",60)
		TNT1 A 0 	
		Stop
	Nothing:
		TNT1 A 0 A_JumpIfInventory("AEoDFusionChaingun",1,"Ammo")
		TNT1 A 0 A_JumpIfInventory("LimitScriptActive",1,1)
		Goto Pickup+2
		TNT1 A 0 ACS_NamedExecute("s798",0,5,61,0)
		Fail
	Ammo:
		TNT1 A 0 A_TakeInventory("PlasmaRod",60)
		TNT1 A 0 A_JumpIfInventory("AEoDFusionChaingun",1,2)
		TNT1 A 0 A_GiveInventory("Weapon5Count",1)
		TNT1 A 0 A_GiveInventory("FusionChaingunDrop",1)
		TNT1 A 0 A_GiveInventory("AEoDFusionChaingun",1)
		Stop
	}
}