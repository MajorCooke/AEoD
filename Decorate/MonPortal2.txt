//==================================================================================
//==================================================================================

//Wheatley BOSS - Codename WheatOS
//Made by Major Cooke

//	If one could imagine Wheatley winning GLaDOS' heart and helping her kill Chell,
//	then one could imagine him invading other realms such as the DooM world.

//==================================================================================
//==================================================================================

Actor WheatOS		//spidermastermind, spell immune
{
	var int user_shock;
	var int user_count;
	Monster	RipLevelMin 1
	+BOSS
	+BOSSDEATH
	+DONTMORPH
	+NOICEDEATH
	+FLOAT
	+NORADIUSDMG
	+NOBLOOD
	+NODAMAGE
	+NOGRAVITY
	+DONTTHRUST
	+DONTCORPSE
	PainChance "Massacre", 256
	PainChance "Telefrag", 256
	PainChance "DemonMorph", 256
	PainChance 64
	Radius 40
	Height 100
	Speed 8
	Scale 1
	Health 9000
	SeeSound "Wheatley/Sight"
	ActiveSound "Wheatley/Active"
	PainSound "Wheatley/Pain"
	DeathSound "Wheatley/PainShock"
	DamageFactor "Fire", 1.5
	DamageFactor "Ice", 0.75
	DamageFactor "Electric", 2.0
	DamageFactor "AEPoison", 0.5
	DamageFactor "AEBullet", 0.75
	DamageFactor "AEMagic", 0.75
 	DamageFactor "Plasma", 1.5
 	DamageFactor "PlasmaDisint", 1.5
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("AEPortal2FXAltSticker",0,0,128,0,0,0,0,SXF_NOCHECKPOSITION)
		//TNT1 A 0 A_SpawnItemEx("WheatOSShield",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
	Idle:
		N007 A 1 A_Look
		Loop
	See:
		N007 A 0 A_ChangeFlag("NOPAIN",0)
		N007 A 2 A_Chase
		Loop
	Missile:
		N007 A 0 A_Jump(256,"Grenade1","Grenade2")
	Grenade1:
		N007 A 8 A_FaceTarget
		N007 A 0 Bright A_PlaySound("q2weapon/grenadelaunfire",CHAN_AUTO,1.0,0)
		N007 B 0 A_SpawnProjectile("QuakeGrenadeBig",85)
		N007 BCAA 6
		Goto See
	Grenade2:
		N007 A 8 A_FaceTarget
		N007 A 0 Bright A_PlaySound("q2weapon/grenadelaunfire",CHAN_AUTO,1.0,0)
		N007 A 0 A_Jump(256,1,2,3)
		N007 BBBBB 0 A_SpawnProjectile("QuakeGrenade",85,0,random(-8,8))
		N007 BCAA 6
		Goto See
	Pain:
		N007 B 4 A_Jump(2,"ShieldTakeDown")
		N007 B 4 A_Pain
		Goto See
	Pain.DemonMorph:
	Pain.Telefrag:
	ShieldTakedown:
		N007 A 0 A_ChangeFlag("NOPAIN",1)
		N007 A 0 A_ChangeFlag("NODAMAGE",0)
		N007 A 0 A_PlaySound("Wheatley/PainShock",CHAN_VOICE,1,0)
		N007 DE 6 bright
		N007 F 4
	Shocking:
		N007 GHIH 1 A_SpawnItemEx("ShockSpawnerSystem",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		N007 H 0 A_SetUserVar("user_shock",user_shock+1)
		N007 I 0 A_JumpIf(user_shock>=30,1)
		Loop
		N007 J 8 A_SetUserVar("user_shock",0)
		N007 KL 6
		N007 MNO 5
		N007 OO 35
		N007 PQRS 9
		N007 S 0 A_ChangeFlag("NOPAIN",0)
		N007 S 0 A_ChangeFlag("NODAMAGE",1)
		Goto See
	Pain.Massacre:
		"####" "#" 1 A_ChangeFlag("NOPAIN",1)
		"####" "#" 1 A_Die
		Goto Death
	Death:
		N007 A 0 A_QuakeEx(2,2,0, 106,0,4096,"", QF_SCALEDOWN)
		N007 A 0 A_Scream
		N007 A 0 A_PlaySound("IconOfSin/Screams",CHAN_BODY,1,0,ATTN_NONE)
		N007 DDEEFFGGHHIIJJKKLLMMNN 4 A_SpawnItemEx("SpecialExploSpawner",random(30,95),0,random(40,128),0,0,0,random(0,359),SXF_NOCHECKPOSITION)
		N007 H 0 A_PlaySound("Wheatley/Explosion",CHAN_WEAPON,1,0,ATTN_NONE)
		N007 OOOOOOOO 4 A_SpawnItemEx("SpecialExploSpawner",random(30,95),0,random(40,128),0,0,0,random(0,359),SXF_NOCHECKPOSITION)
		N007 O 0 A_GiveInventory("WheatleyDied",1)
		//N007 O 0 A_SpawnItemEx("SpecialExploShockwave",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 a 0 A_PlaySound("reilsss/weapons/redeemerexplosion",7,1.0)
		TNT1 A 0 A_SpawnItemEx("RedeemerExplosionSpawner",0,0,16,0,0,0,0,SXF_NOCHECKPOSITION)
		N007 O 0 A_JumpIfInventory("AEoDPortalGun",1,2,AAPTR_PLAYER1)
		N007 O 0 A_SpawnItemEx("AEoDPortalGunItem",0,0,64,frandom(0,1),0,0,random(0,359),SXF_NOCHECKPOSITION)
		N007 O 0 A_Fall
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 AAAAAA 35 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_KillMaster("Massacre",KILS_FOILINVUL,None,"AEoDSpawner")
		Stop
	}
}

Actor WheatleyDied : Inventory
{	Inventory.MaxAmount 1	}

Actor ShockSpawnerSystem
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 1 A_PlaySound("disintegrate/extrasnd/extra",CHAN_BODY,0.4)
		TNT1 AA 0 A_SpawnItemEx("WheatleyShockParticle",random(0,60),0,random(40,120),frandom(-2,2),0,frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("WheatleyShockParticle",random(0,60),0,random(40,120),frandom(-2,2),0,frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION,128)
		Stop
	}
}

Actor WheatleyShockParticle
{
	+NOINTERACTION
	RenderStyle "Add"
	Alpha 0.8
	Scale 0.05
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"S1","S2","S3","S4","S5","S6","S7","S8")
	S1:
		X079 A 0
		Goto Playoff
	S2:
		X079 B 0
		Goto Playoff
	S3:
		X079 C 0
		Goto Playoff
	S4:
		X079 D 0
		Goto Playoff
	S5:
		X079 E 0
		Goto Playoff
	S6:
		X079 F 0
		Goto Playoff
	S7:
		X079 G 0
		Goto Playoff
	S8:
		X079 H 0
		Goto Playoff
	Playoff:
		"####" "#" 0 A_ScaleVelocity(0.96)
		"####" "#" 0 A_SetScale(ScaleX+0.01)
		"####" "#" 1 A_FadeOut(0.02)
		Loop
	}
}
		
Actor AEPortal2FXAltSticker
{
	Projectile
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
	Stickening:
		TNT1 A 1 A_Warp(AAPTR_TARGET,24,0,128,0,WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION)
		TNT1 AAAA 0 A_SpawnItemEx("Portal2ParticleAlt",0,0,0,random(-5,5),0,0,random(-90,90),SXF_NOCHECKPOSITION)
		TNT1 AAAAA 0 A_SpawnItemEx("Portal2ParticleAlt",0,0,0,random(-5,5),0,0,random(0,359),SXF_NOCHECKPOSITION,128)
		TNT1 A 0 A_JumpIfInventory("WheatleyDied",1,"End",AAPTR_TARGET)
		TNT1 A 0 A_Warp(AAPTR_TARGET,-24,0,128,0,WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION)
		TNT1 AA 0 A_SpawnItemEx("Portal2ParticleAlt",0,0,0,random(-5,5),0,0,random(0,359),SXF_NOCHECKPOSITION)
		TNT1 AAAAA 0 A_SpawnItemEx("Portal2ParticleAlt",0,0,0,random(-5,5),0,0,random(0,359),SXF_NOCHECKPOSITION,128)
		TNT1 A 0 A_JumpIfInventory("WheatleyDied",1,"End",AAPTR_TARGET)
		Loop
	End:
		TNT1 A 0
		Stop
	}
}





//==================================================================================
//==================================================================================


//==================================================================================
//==================================================================================

Actor CoreCheck1 : Inventory	//Space core
{ Inventory.MaxAmount 1 }

Actor CoreCheck2 : Inventory	//Rick core
{ Inventory.MaxAmount 1 }

Actor CoreCheck3 : Inventory	//Fact core
{ Inventory.MaxAmount 1 }

Actor SpaceCore
{
	var int user_xy[2];
	var int user_count[5];
	//[0] = Which frame was last chosen for spazz?
	//[1] = Cooldown for the sounds.
	//[2] = Duration of booster
	//[3] = Cooldown for booster
	//[4] = Initial flying countdown
	var int user_angle;
	var int user_z;
	Monster
	-SOLID
	-COUNTKILL
	+THRUACTORS
	+NONSHOOTABLE
	-SHOOTABLE
	+VULNERABLE
	+NODAMAGE
	+NOTRIGGER
	+NOTELESTOMP
	+ALWAYSTELEFRAG
	+DONTTHRUST
	+NODAMAGE
	+NOBLOOD
	+NEVERTARGET
	+NOPAIN
	+CANTSEEK
	PainChance "Normal", 0
	PainChance "Massacre", 256
	PainChance "Telefrag", 256
	BounceType "Hexen"
	BounceFactor 0.7
	WallBounceFactor 0.6 //Need to stop the clipping into walls...
	Radius 32
	Height 32
	Scale 0.3
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserArray("user_xy",0,x)
		TNT1 A 0 A_SetUserArray("user_xy",1,y)
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TNT1 A 0 A_JumpIfInventory("CoreCheck1",1,"End",AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("CoreCheck1",1,AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("RickImmune",1)
		TNT1 A 0 A_SetUserVar("user_angle",0)
		TNT1 A 0 A_SetUserVar("user_z",0)
	FrameSetup:
		TNT1 A 0 A_Jump(256,"S1","S2","S3","S4","S5")
	S1:
		TNT1 A 0 A_JumpIf(user_count[0]==1,"S2") //Force randomization.
		N008 G 0 A_SetUserArray("user_count",0,1)
		Goto Acting1
	S2:
		TNT1 A 0 A_JumpIf(user_count[0]==2,"S3")
		N008 H 0 A_SetUserArray("user_count",0,2)
		Goto Acting1
	S3:
		TNT1 A 0 A_JumpIf(user_count[0]==3,"S4")
		N008 I 0 A_SetUserArray("user_count",0,3)
		Goto Acting1
	S4:
		TNT1 A 0 A_JumpIf(user_count[0]==4,"S5")
		N008 J 0 A_SetUserArray("user_count",0,4)
		Goto Acting1
	S5:
		TNT1 A 0 A_JumpIf(user_count[0]==5,"S1")
		N008 K 0 A_SetUserArray("user_count",0,5)
		Goto Acting1
	Acting1:
		"####" "#" 0 A_JumpIf(user_count[3]>=1,2)
		"####" "#" 0 A_Jump(4,"Blastoff")
		"####" "#" 0 A_JumpIf(user_count[1]>=1,"Frame1")
		"####" "#" 0 A_PlaySound("SpaceCore/Active")
		"####" "#" 0 A_SetUserArray("user_count",1,random(90,300))
	Frame1:
		"####" "#" 0 A_Jump(256,1,2,3,4)
		"####" "####" 1 A_SetUserArray("user_count",1,user_count[1]-1)
		"####" "#" 0 A_SetUserArray("user_count",3,user_count[3]-random(1,3))
		Goto FrameSetup
	Blastoff:
		N008 A 0 A_SetUserArray("user_count",3,random(200,800))
		N008 A 0 A_SetUserArray("user_count",2,random(200,800))
		N008 A 0 A_PlaySound("Screamer/AltFire",CHAN_WEAPON,1,0)
		N008 A 0 A_PlaySound("SpaceCore/Spazz")
		N008 A 0 A_PlaySound("weapons/shockwaveactive",CHAN_VOICE,1,1)
		N008 A 0 A_ChangeFlag("MISSILE",1)
		N008 A 0 A_ChangeFlag("BOUNCEONFLOORS",1)
		N008 A 0 A_ChangeFlag("BOUNCEONWALLS",1)
		N008 A 0 A_ChangeFlag("BOUNCEONCEILINGS",1)
		N008 A 0 A_ChangeFlag("CANBOUNCEWATER",1)
		N008 A 0 A_GiveInventory("SpaceCoreFlying",1)
		//N008 A 0 A_SpawnItemEx("SpaceCoreRampController",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
	Flying:
		N008 A 0 A_SetAngle(angle+random(-4,4))
		N008 A 0 A_ChangeVelocity(random(1,3),0,frandom(-3,3),1)
		N008 A 1 A_SpawnItemEx("SpaceCoreFlames",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		N008 A 0 A_SetUserArray("user_count",2,user_count[2]-1)
		N008 A 0 A_JumpIf(user_count[2]<=0,"Grounder")
		Loop
	Grounder:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 A 0 A_PlaySound("Core/BoosterOff",CHAN_VOICE,1,0)
		TNT1 A 0 A_TakeInventory("SpaceCoreFlying",1)
		N008 A 0 A_ChangeFlag("MISSILE",0)
		TNT1 A 0 A_ChangeFlag("BOUNCEONFLOORS",0)
		TNT1 A 0 A_ChangeFlag("BOUNCEONWALLS",0)
		TNT1 A 0 A_ChangeFlag("BOUNCEONCEILINGS",0)
		TNT1 A 0 A_ChangeFlag("CANBOUNCEWATER",0)
		Goto Spawn
	Pain.Massacre:
	Pain.Telefrag:
		TNT1 A 1 A_ChangeFlag("NODAMAGE",0)
		TNT1 A 0 A_Die
	Death:
		TNT1 A 0 A_SpawnItemEx("PBExplosionSpawner",0,0,5,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAA 0 A_TossGib
		TNT1 A 0 A_PlaySound("weapons/rockexplo")
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 A 0 A_Jump(256,1,2,3,4,5)
		TNT1 AAAAA random(150,350)
		TNT1 A 0 A_SpawnItemEx("TeleportFog",user_xy[0],user_xy[1],floorz,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		TNT1 A 0 A_TakeInventory("CoreCheck1",1,0,AAPTR_PLAYER1)
		TNT1 A 1 A_SpawnItemEx("SpaceCore",user_xy[0],user_xy[1],floorz,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Stop
	End:
		TNT1 A 0
		Stop
	}
}

Actor SpaceCoreFlying : Inventory
{	Inventory.MaxAmount 1	}

//==============================================================================
//Effects
//==============================================================================

Actor SpaceCoreFlames
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAA 0 A_SpawnItemEx("SpaceCoreFire",random(0,20),0,random(-10,10),0,0,0,random(0,359),SXF_NOCHECKPOSITION)
		TNT1 AAAAAA 0 A_SpawnItemEx("SpaceCoreFire",random(0,20),0,random(-10,10),0,0,0,random(0,359),SXF_NOCHECKPOSITION,128)
		Stop
	}
}

Actor SpaceCoreFire
{
	Scale 0.75
	RenderStyle Add
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"Two","Three")
	One:
		X006 ABCDEFGHIJKLMNOP 1 bright A_ScaleVelocity(0.98)
		//X006 LMNOP 1 bright A_FadeOut(0.05)
		Stop
	Two:
		X007 ABCDEFGHIJ 1 bright A_ScaleVelocity(0.98)
		Stop
	Three:
		X033 ABCDEFGHIJKLMNOPQR 1 bright A_ScaleVelocity(0.98)
		Stop
	}
}


//==================================================================================
//==================================================================================


//==================================================================================
//==================================================================================
Actor FactCore
{
	var int user_delay;
	Radius 32
	Height 32
	Scale 0.3
	Monster
	+NONSHOOTABLE
	-SHOOTABLE
	+NODAMAGE
	-COUNTKILL
	+THRUACTORS
	+BOUNCEONWALLS
	+NOEXPLODEFLOOR
	+NOTELESTOMP
	+CANTSEEK
	Gravity 0.6
	States
	{
	Spawn:
		N010 A 0
		TNT1 A 0 A_JumpIfInventory("CoreCheck2",1,"End",AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("CoreCheck2",1,AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("RickImmune",1)
	Idle:
		N010 A 0 A_SetUserVar("user_delay",user_delay-1)
		N010 A 35 A_Jump(12,"Facts","Move")
		Loop
	Facts:
		N010 A 0 A_JumpIf(user_delay>=1,"Idle")
		N010 A 0 A_PlaySound("FactCore/Fact",CHAN_VOICE,1,0)
		N010 A 0 A_SetUserVar("user_delay",35*6)
		Goto Spawn
	Move:
		N010 A 0 A_ChangeFlag("MISSILE",1)
		N010 A 0 A_ChangeFlag("BOUNCEONWALLS",1)
		N010 A 0 A_ChangeFlag("BOUNCEONCEILINGS",1)
		N010 A 0 A_ChangeFlag("CANBOUNCEWATER",1)
		N010 A 0 A_ChangeFlag("SKYEXPLODE",1)
		N010 A 0 A_SetAngle(angle+random(-50,50))
		N010 A 2 A_ChangeVelocity(random(-12,12),0,random(4,12),1)
	FloorChecker:
		N010 A 1 A_CheckFloor("Reset")
		Loop
	Reset:
		N010 A 0 A_ChangeFlag("MISSILE",0)
		Goto Spawn
	End:
		TNT1 A 0
		Stop
	}
}


//==================================================================================
//==================================================================================

Actor RickCoreForced
{
	+NOINTERACTION
	+NOTIMEFREEZE
	Radius 32
	Height 32
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_TakeInventory("CoreCheck3",9999,0,AAPTR_PLAYER1)
		TNT1 A 0 A_SpawnItemEx("RickCore",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

//==================================================================================
//==================================================================================
Actor RickCore
{
	var int user_song;
	var int user_count;
	var int user_move;
	var int user_plr;
	var int user_x;
	var int user_y;
	Monster
	+FLOORCLIP
	-COUNTKILL
	+NONSHOOTABLE
	-SHOOTABLE
	+THRUACTORS
	+NEVERTARGET
	+NODAMAGE
	+NOTELESTOMP
	+LOOKALLAROUND
	-NODROPOFF
	+DROPOFF
	+VULNERABLE
	+BUDDHA
	+ALWAYSTELEFRAG
	+DONTCORPSE
	+CANTSEEK
	+NOBLOOD
	DamageFactor "RickRoll", 0.0
	PainChance "Normal", 0
	PainChance "Telefrag", 256
	Radius 32
	Height 32
	Scale 0.25
	States
	{
	Spawn:
		N009 A 0 NoDelay A_SpawnItemEx("RickCoreSighter",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		N009 A 0 A_JumpIfInventory("CoreCheck3",1,"End",AAPTR_PLAYER1)
		N009 A 0 A_GiveInventory("CoreCheck3",1,AAPTR_PLAYER1)
	Idle:
		N009 A 0 A_Gravity
		N009 A 0 A_TakeInventory("RickCoreDirection",9999,0)
	/*IdleCont:
		N009 A 0 A_JumpIf(user_plr==1,"Idle2")
		N009 A 0 A_JumpIf(user_count>=1,2)
		N009 A 0 A_Jump(20,"Speak")
		N009 A 5 A_SetUserVar("user_count",user_count-5)
		N009 A 0 A_LookEx(LOF_NOSOUNDCHECK,0,0,0,0,"Sight")
		Loop
	Sight:
		N009 A 0 A_SetUserVar("user_plr",1)
		Goto Idle2*/
	Idle2:
		N009 A 0 A_JumpIf(user_count>=1,2)
		N009 A 0 A_Jump(20,"Speak")
		N009 A 5 A_SetUserVar("user_count",user_count-5)
		Loop
	Speak:
		"####" "#" 0 A_SetUserVar("user_move",random(1,2))
		"####" "#" 0 A_Jump(16,"Sing2") //This ones long. Let's make it more rare.
		"####" "#" 0 A_Jump(128,"Sing1")
		"####" "#" 0 A_PlaySound("RickCore/Active",CHAN_VOICE,1,0)
		"####" "#" 0 A_SetUserVar("user_count",random(350,600))
		Goto Idle
	Sing1:
		"####" "#" 0 A_Jump(128,2)
		"####" "#" 0 A_GiveInventory("RickCoreDirection",1)
		N009 A 0 A_PlaySound("RickCore/Sing1",CHAN_VOICE,1,0)
		N009 BCDEFGHCDEFG 3	A_GiveInventory("RickCoreMover",1)//~1 second
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEF 3 A_GiveInventory("RickCoreMover",1)
		N009 E 0 A_StopSound(CHAN_VOICE)
		N009 F 0 A_Jump(128,"Idle","RickSelector","RickSelector")
		N009 F 0 A_Jump(256,"Disturbed","DeathStars","DreamTheatre")
		Goto Idle
	Sing2:
		"####" "#" 0 A_Jump(128,2)
		"####" "#" 0 A_GiveInventory("RickCoreDirection",1)
		N009 A 0 A_PlaySound("RickCore/Sing2",CHAN_VOICE,1,0)
		N009 BCDEFGHCDEFG 3	A_GiveInventory("RickCoreMover",1) //~1 second
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEF 3 A_GiveInventory("RickCoreMover",1)
		N009 E 0 A_StopSound(CHAN_VOICE)
		N009 F 0 A_Jump(128,"Idle","RickSelector","RickSelector")
		N009 F 0 A_Jump(256,"Disturbed","DeathStars","DreamTheatre")
		Goto Idle
	RickSelector:
		WTF1 A 0 A_NoGravity
		WTF1 A 0 A_Stop
		"####" "#" 0 A_Jump(256,"RickRoll1","RickRoll2","RickRoll3","RickRoll4","RickRoll5",
								"RickRoll6")
	Disturbed:
		N009 A 0 A_PlaySound("RickCore/Stupify",CHAN_WEAPON,1,0)
		N009 BCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDE 3 A_GiveInventory("RickCoreMover",1)
		N009 A 0 A_StopSound(CHAN_WEAPON)
		N009 A 0 A_PlaySound("Core/BoosterOff",CHAN_WEAPON,1,0)
		"####" "#" 0 A_Stop
		Goto Idle
	DeathStars:
		N009 A 0 A_PlaySound("RickCore/DeathStars",CHAN_WEAPON,1,0)
		N009 BCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDE 3 A_GiveInventory("RickCoreMover",1)
		N009 A 0 A_StopSound(CHAN_WEAPON)
		N009 A 0 A_PlaySound("Core/BoosterOff",CHAN_WEAPON,1,0)
		"####" "#" 0 A_Stop
		Goto Idle
	DreamTheatre:
		N009 A 0 A_PlaySound("RickCore/DreamTheatre",CHAN_WEAPON,1,0)
		N009 BCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHCDEFGHCDEFG 3 A_GiveInventory("RickCoreMover",1)
		N009 HCDEFGHCDEFGHCDEFGHCDEFGHC 3 A_GiveInventory("RickCoreMover",1)
		N009 A 0 A_StopSound(CHAN_WEAPON)
		N009 A 0 A_PlaySound("Core/BoosterOff",CHAN_WEAPON,1,0)
		"####" "#" 0 A_Stop
		Goto Idle
	RickRoll1: //We're no strangers to looooooove.
		"####" "#" 0 A_PlaySound("RickCore/RickRoll1",CHAN_WEAPON,1,0)
		"####" "#" 0 A_NoGravity
		"####" "###" 35 A_Stop
		"####" "#" 14
		"####" "#" 0 A_StopSound(CHAN_WEAPON)
		"####" "#" 0 A_PlaySound("Core/FuelEmpty1",CHAN_WEAPON,1,0)
		Goto Idle
	RickRoll2: 
	//Never gonna give you up, never gonna let you down.
	//Never gonna run around and desert you.
		"####" "#" 0 A_PlaySound("RickCore/RickRoll2",CHAN_WEAPON,1,0)
		"####" "#" 0 A_NoGravity
		"####" "########" 35 A_Stop
		"####" "#" 14
		"####" "#" 0 A_StopSound(CHAN_WEAPON)
		"####" "#" 0 A_PlaySound("Core/BoosterOff",CHAN_WEAPON,1,0)
		Goto Idle
	RickRoll3:
	//Never gonna make you cry, never gonna say goodbye,
	//Never gonna tell a lie, and hurt you.
		"####" "#" 0 A_PlaySound("RickCore/RickRoll3",CHAN_WEAPON,1,0)
		"####" "#" 0 A_NoGravity
		"####" "########" 35 A_Stop
		"####" "#" 21
		"####" "#" 2 A_SpawnItemEx("WHATTHEHELLISTHATCRAP",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION) //>:)
		"####" "#" 0 A_CheckSight(2) //Ignore the following jump if the player is in sight.
		"####" "#" 0 A_JumpIf(user_plr==0,2)
		"####" "#" 0 A_RadiusGive("RickTrollD",2048,RGF_MONSTERS,1)
		"####" "#" 0 A_StopSound(CHAN_WEAPON)
		"####" "#" 0 A_PlaySound("Core/FuelEmpty1",CHAN_WEAPON,1,0)
		Goto Idle
	RickRoll4:
	//Never gonna give, never gonna give. (Give you up) x2
		"####" "#" 0 A_PlaySound("RickCore/RickRoll4",CHAN_WEAPON,1,0)
		"####" "#" 0 A_NoGravity
		"####" "#######" 35 A_Stop
		"####" "#" 18
		"####" "#" 0 A_StopSound(CHAN_WEAPON)
		"####" "#" 0 A_PlaySound("Core/BoosterOff",CHAN_WEAPON,1,0)
		Goto Idle
	RickRoll5:
	//Never gonna give, never gonna give. (Give you up)
		"####" "#" 0 A_PlaySound("RickCore/RickRoll5",CHAN_WEAPON,1,0)
		"####" "#" 0 A_NoGravity
		"####" "####" 35 A_Stop
		"####" "#" 3
		"####" "#" 0 A_StopSound(CHAN_WEAPON)
		"####" "#" 0 A_PlaySound("Core/BoosterOff",CHAN_WEAPON,1,0)
		Goto Idle
	RickRoll6:
	//Aaaaahhh just wanna tell you how I'm feeling. Gassy~!
		"####" "#" 0 A_PlaySound("RickCore/RickRoll6",CHAN_WEAPON,1,0)
		"####" "#" 0 A_NoGravity
		"####" "#####" 35 A_Stop
		"####" "#" 7
		"####" "#" 0 A_StopSound(CHAN_WEAPON)
		"####" "#" 0 A_PlaySound("Core/FuelEmpty1",CHAN_WEAPON,1,0)
		Goto Idle
	End:
		TNT1 A 0
		Stop
	}
}

Actor RickCoreSighter
{
	+NOTONAUTOMAP
	+NOINTERACTION
	+LOOKALLAROUND
	+NOTELEPORT
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 5 A_Look
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,16,0,WARPF_NOCHECKPOSITION)
		Loop
	See:
		TNT1 A 0 A_GiveInventory("RickCorePlayerFound",1,AAPTR_MASTER)
		Stop
	}
}

Actor RickCorePlayerFound : CustomInventory
{
	//var int user_plr;
	//var int user_ggg2;
	+INVENTORY.ALWAYSPICKUP
	//+INVENTORY.AUTOACTIVATE
	States
	{
	Pickup:
		TNT1 A 0 A_SetUserVar(user_plr,1)
		//TNT1 A 0 A_SetUserVar(user_ggg2,1)
		Stop
	}
}

Actor RickCoreDirection : Inventory
{	Inventory.MaxAmount 1	}

Actor RickImmune : Inventory
{	Inventory.MaxAmount 1	}

Actor RickCoreMover : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	States
	{
	Use:
		TNT1 A 0 A_JumpIfInventory("RickCoreDirection",1,"MoveLeft")
	MoveRight:
		TNT1 A 0 A_ChangeVelocity(0,frandom(0.5,2.5),0,CVF_RELATIVE)
		TNT1 A 0 A_SetAngle(angle+random(2,10))
		Stop
	MoveLeft:
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.5,-2.5),0,CVF_RELATIVE)
		TNT1 A 0 A_SetAngle(angle+random(-2,-10))
		Stop
	}
}

Actor WHATTHEHELLISTHATCRAP
{
	+NOINTERACTION
	Scale 0.6
	States
	{
	Spawn:
		WTF1 B 2
		Stop
	}
}

Actor RickTrollD : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_CheckFlag("FRIENDLY","Nope")
		TNT1 A 0 A_DamageSelf(20000,"RickRoll",DMSS_FOILINVUL)
		Stop
	Nope:
		TNT1 A 0
		Stop
	}
}