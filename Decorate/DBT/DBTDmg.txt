
Actor DBTNoDamageOff : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_ChangeFlag("NODAMAGE",0)
		Stop
	}
}

Actor DBTNoDamageOn : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_ChangeFlag("NODAMAGE",1)
		Stop
	}
}


//************************************************
actor DBTVulnerableThing
{
	const int DamageMultiplier = 1;
	const int MaxTics = 70;
	var int user_tics;
	var int user_damage;
	var int user_hp;
	var int user_t;
    Monster
    -COUNTKILL
    +NOGRAVITY
    +DONTFALL
	+NOICEDEATH
	+DONTMORPH
    +DONTTHRUST
	+QUICKTORETALIATE
	+GHOST
	+NEVERTARGET
	+NOTELESTOMP
	+NOTELEPORT
	+NOTRIGGER
	Species "DeathBringer"
	const int HP = 1000; 
	const int MaxDamage = 1000; //Keep it from bringing DBT to 0 on one hit.
    Health 1000
    Radius 30
    Height 56
    PainChance 255
	PainChance "DBTSetup", 0
	PainChance "DBTDamage", 0
    DamageFactor "Normal", 1
	DamageFactor "DBTSetup", 0
	DamageFactor "DBTDamage", 0.0
	RenderStyle None
    States
    {
    Spawn:
		TNT1 A 0 NoDelay 
		{
			//if (tid <= 0 || tid > 70)
			//{	A_SetUserVar(user_t,70);	}
			//else
			//{	A_SetUserVar(user_t,tid);	}
			A_ResetHealth;
			A_ChangeFlag(CORPSE,0);
			A_ClearTarget;
			A_CheckFlag("FRIENDLY","EndNoChange",AAPTR_MASTER);
			A_ChangeFlag("NOBLOCKMAP",0);
			A_ChangeFlag("NOPAIN",0);
			A_SetShootable;
		}
	Waiting:
		TNT1 A 0 A_Stop
		TNT1 A 1 A_SetUserVar(user_tics,user_tics+1)
		TNT1 A 0 A_JumpIf(user_tics >= MaxTics, "Waiting")
		TNT1 A 0 A_JumpIf(health < HP,1)
		Stop
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		Goto Pain+2
	Pain:
		TNT1 A 0 A_UnsetShootable
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 1 A_ChangeFlag("NOBLOCKMAP",1)
		//Make sure it's only a player damaging him.
		TNT1 A 0 A_JumpIf(CheckClass("PlayerPawn",AAPTR_TARGET,1),1)
		Goto Spawn
		TNT1 A 0 A_JumpIf(health >= HP,"EndNoChange")
		TNT1 A 0 A_SetUserVar(user_damage,HP-Health)
		TNT1 A 0 A_GiveInventory("DBTNoDamageOff",1,AAPTR_MASTER)
		TNT1 A 0 A_DamageMaster(user_damage*DamageMultiplier,"DBTVulnerable",DMSS_NOFACTOR,"FDeathBringerThanatos")
		TNT1 A 0 A_GiveInventory("DBTNoDamageOn",1,AAPTR_MASTER)
		TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TARGET,AAPTR_NULL),3)
		TNT1 A 0 A_JumpIfHealthLower(1,2,AAPTR_TARGET)
		TNT1 A 0 A_TransferPointer(AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_TARGET,AAPTR_TARGET,PTROP_NOSAFEGUARDS)
		TNT1 A 0 A_JumpIf(DamageMultiplier > 1,1)
		Stop
		TNT1 A 0 A_QuakeEx(1,2,2,35,0,2048,"",QF_RELATIVE|QF_SCALEDOWN)
		Stop
    Death:
		TNT1 A 0 A_JumpIf(CheckClass("PlayerPawn",AAPTR_TARGET,1),1)
		Goto End
		TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TARGET,AAPTR_NULL),3)
		TNT1 A 0 A_JumpIfHealthLower(1,2,AAPTR_TARGET)
		TNT1 A 0 A_TransferPointer(AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_TARGET,AAPTR_TARGET,PTROP_NOSAFEGUARDS)
		TNT1 A 0 A_GiveInventory("DBTNoDamageOff",1,AAPTR_MASTER)
		TNT1 A 0 A_DamageMaster(1000*DamageMultiplier,"DBTVulnerable",DMSS_NOFACTOR,"FDeathBringerThanatos")
		TNT1 A 0 A_GiveInventory("DBTNoDamageOn",1,AAPTR_MASTER)
		TNT1 A 0 A_JumpIf(DamageMultiplier > 1,1)
		Stop
		TNT1 A 35 A_QuakeEx(1,2,2,35,0,2048,"",QF_RELATIVE|QF_SCALEDOWN)
		Stop
	Death.Massacre:
	EndNoChange:
		TNT1 A 0
		Stop
	End:
		TNT1 A 0 A_JumpIfHealthLower(1,"EndNoChange",AAPTR_TARGET)
		TNT1 A 0 A_TransferPointer(AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_TARGET,AAPTR_TARGET,PTROP_NOSAFEGUARDS)
		Stop
    }
}

//=============================================================
//=============================================================

//	Double Damage taken! Half the duration though.

//=============================================================
//=============================================================

actor DBTVulnerableDouble : DBTVulnerableThing
{
	const int MaxTics = 35;
	const int DamageMultiplier = 2;
}

//A_QuakeEx(1,3,2,35,0,32767,"",QF_SCALEDOWN|QF_RELATIVE)

//=============================================================
//=============================================================

//	For when DBT has "Steel" active

//=============================================================
//=============================================================

actor DBTVulnerableThingSteel : DBTVulnerableThing
{
    DamageFactor "Normal", 0.2
	DamageFactor "TempestLightning", 0.21
	DamageFactor "Telefrag", 0.1 //Needing a direct hit now from demon morph, not a glancing blow!
	Health 1000
}

/*==============================================================================
 Health Check

 Disabling the NODAMAGE flag is extremely ill advised, so instead, use 
 A_SetHealth and a loop. I don't trust the room for error in which DBT could
 be unintentionally damaged with a NOFACTOR damaging attack and I don't want
 to risk it. The NODAMAGE flag stays at all times and the following can safely
 kill him off without needing to remove the flag.
==============================================================================*/

Actor DBTDamageCounter : Inventory
{	Inventory.MaxAmount 1000 }

Actor DBTDamageCheck : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_JumpIfInventory("DBTDamageCounter",health,"Kill") //Skip it all if it matches.
	LoopCheck:
		//Most efficiently done in increments of 10. Less time spent. instead of
		//one at a time.
		TNT1 A 0 A_JumpIfInventory("DBTDamageCounter",1000,"Dmg1000") 
		TNT1 A 0 A_JumpIfInventory("DBTDamageCounter",100,"Dmg100")
		TNT1 A 0 A_JumpIfInventory("DBTDamageCounter",10,"Dmg10")
		TNT1 A 0 A_JumpIfInventory("DBTDamageCounter",5,"Dmg5")
		TNT1 A 0 A_JumpIfInventory("DBTDamageCounter",1,"Dmg1")
		Stop
	Dmg1000:
		TNT1 A 0 A_JumpIf(health <= 1000,"Kill")
		TNT1 A 0 A_TakeInventory("DBTDamageCounter",1000,0)
		TNT1 A 0 A_SetHealth(health-1000)
		Goto LoopCheck
	Dmg100:
		TNT1 A 0 A_JumpIf(health <= 100,"Kill")
		TNT1 A 0 A_TakeInventory("DBTDamageCounter",100,0)
		TNT1 A 0 A_SetHealth(health-100)
		Goto LoopCheck
	Dmg10:
		TNT1 A 0 A_JumpIf(health <= 10,"Kill")
		TNT1 A 0 A_TakeInventory("DBTDamageCounter",10,0)
		TNT1 A 0 A_SetHealth(health-10)
		Goto LoopCheck
	Dmg5:
		TNT1 A 0 A_JumpIf(health <= 5,"Kill")
		TNT1 A 0 A_TakeInventory("DBTDamageCounter",5)
		TNT1 A 0 A_SetHealth(health-5)
		Goto LoopCheck
	Dmg1:
		TNT1 A 0 A_JumpIf(health <= 1,"Kill")
		TNT1 A 0 A_TakeInventory("DBTDamageCounter",1)
		TNT1 A 0 A_SetHealth(health-1)
		Goto LoopCheck
	KillCheck:
		TNT1 A 0 A_JumpIfInventory("DBTDamageCounter",health,"Kill")
		Goto LoopCheck
	Kill:
		TNT1 A 0 A_Die("Normal")
		Stop
	}
}




/*actor DBTVulnerableThingSteel
{
    Monster
    -COUNTKILL
//    -Solid		//Removed to have it DBT stuck when attacking
    +NOGRAVITY
    +DONTFALL
    Mass 0xFFFFFFFFFFF
    Health 3000
    Radius 30
    Height 56
    PainChance 255
    RenderStyle Add
    Scale 3.9
	+GHOST
    DamageFactor "Railgun", 0.6
    DamageFactor "BFGSplash", 0.5
    DamageFactor "Normal", 1.0
	DamageFactor "DarkSyphon", 0.0
	DamageFactor "DBTDamage", 0.0
    DeathSound "halflife/monsters/nihilant/pain"
	BloodColor "88 88 88"
    States
    {
    Spawn:
		TNT1 A 0
		PLAY A 70
		Stop
    Pain:
		TNT1 A 0 A_UnSetShootable
		TNT1 A 0 A_JumpIfHealthLower(1000,"Damage6")		//If you deal 2000 damage, it hurts 6
		TNT1 A 0 A_JumpIfHealthLower(2000,"Damage3")		//If you deal 1000 damage, it hurts 3
		TNT1 A 0 A_JumpIfHealthLower(2500,"Damage2")		//If you deal 500 damage, it hurts 2
		TNT1 A 0 A_JumpIfHealthLower(2925,"Damage1")		//If you deal 75 damage, it hurts 1
		Goto "NoDamage"
	NoDamage:
		TNT1 A 0 A_Print("Dealt 0")						//If you deal less than 75, no damage
		TNT1 A 0 A_Die
		Goto Death2
    Damage1:
		TNT1 A 0 A_Print("Dealt 1")
		TNT1 A 0 A_DamageMaster(1,"DBTVulnerable")		//If you deal >75&&<501, it hurts 2
		TNT1 A 0 A_Die
		Goto Death2
	Damage2:
		TNT1 A 0 A_Print("Dealt 2")
		TNT1 A 0 A_DamageMaster(2,"DBTVulnerable")		//If you deal >500&&<1001, it hurts 2
		TNT1 A 0 A_Die
		Goto Death2
	Damage3:
		TNT1 A 0 A_Print("Dealt 3")
		TNT1 A 0 A_DamageMaster(3,"DBTVulnerable")		//If you deal >1001&&<2001, it hurts 3
		TNT1 A 0 A_Die
		Goto Death2
    Damage6:
		TNT1 A 0 A_Print("Dealt 6")
		TNT1 A 0 A_DamageMaster(6,"DBTVulnerable")
		TNT1 A 0 A_Die
		Goto Death2
    Death:
		TNT1 A 0 A_Print("Dealt 10")
		BOS2 A 50 A_DamageMaster(10,"DBTVulnerable")	//if you deal 1500 damage, it hurts 10
		Stop
    Death2:
		BOSS A 50
		Stop
    }
}*/

Actor DBTVoices
{
	Monster
	+NEVERTARGET
	+THRUACTORS
	+NOGRAVITY
	-SHOOTABLE
	+NONSHOOTABLE
	+NODAMAGE
	+NOTIMEFREEZE
	-COUNTKILL
	+NOTELESTOMP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 AAA 35
		TNT1 A 0 A_PlaySound("mon/dbt2/idle",5,1,0,ATTN_NONE)
	Acting:
		TNT1 AAAAAAAAAAAAAAA 35 A_JumpIfHealthLower(1,"End",AAPTR_MASTER) //A_JumpIfInventory("IDied",1,"End",AAPTR_MASTER)
		TNT1 A 0 A_PlaySound("mon/dbt2/idle",5,1,0,ATTN_NONE)
		Loop
	End:
		TNT1 A 0 A_StopSound(5)
		Stop
	}
}