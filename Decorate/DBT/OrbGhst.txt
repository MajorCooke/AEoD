
// This is the code that was used by LilWhiteMouse's HammerHound
// in the "Bosses Resource Wad". As said "Kudos to Nanami for this trick"

actor OrbitGhostSpawner
{
    Radius 6
    Height 8
    Speed 7
    PROJECTILE
    +NOCLIP
    Scale 0.4
    SeeSound "greatwraith/active"
    +NOTIMEFREEZE
    States
    {
    Spawn:
	PARS AAA 3 Bright A_SpawnItem("DBTSGTrail",0)
	PARS A 0 A_CustomMissile("OrbitGhost", 0, 0, 80)
	Stop
    }
}


actor OrbitGhost
{
    Radius 20
    Height 30
	Health 30
    Speed 7
    PROJECTILE
    Scale 0.4
    +NOCLIP
    +THRUGHOST
	+Shootable
    //+NOTIMEFREEZE
    States
    {
    Spawn:
		PARS AAA 2 Bright A_SpawnItem("DBTSGTrail",0)
		PARS A 0 A_Jump(10, "Death")
		PARS A 0 A_CustomMissile("OrbitGhost", 0, 0, 70)		//80
		Stop
	Death:
		PARS A 0 A_CustomMissile("OrbitGhostShooter", 0, 0, 0)
		PARS Z 999
		Stop
    }
}


ACTOR OrbitGhostShooter
{
    Radius 1
    Height 1
    Speed 0
    +ISMONSTER
    +NOGRAVITY
    +NONSHOOTABLE
    +NOTELEPORT
    +NOCLIP
    +NOBLOCKMAP
    +LOOKALLAROUND
    +THRUGHOST
    RENDERSTYLE NONE
    +NOTIMEFREEZE
    States
    {
    Spawn:
	HHRB AA 1 A_Look
	Loop
    See:
	HHRB A 1 A_CustomMissile("DBTSummonedGhost", 0, 0, 0)
	HHRB A 400
	Stop
    }
}

//---------------------------Ghost Seeker
actor DBTSummonedGhost
{
    Radius 13
    Height 8
    Speed 8
    Damage 35
    PROJECTILE
    Scale 0.4
    +THRUGHOST
    +SEEKERMISSILE
    SeeSound "greatwraith/active"
    DeathSound "greatwraith/death"
    +NOTIMEFREEZE
    States
    {
    Spawn:
        PARS A 3 Bright A_SeekerMissile(2,90)
        PARS A 0 Bright A_SpawnItem("DBTSGTrail",0)
        Loop
    Death:
	TNT1 A 1 A_CustomMissile(DBTSGExplo,0,0,0)
	Stop
    }
}

actor DBTSGExplo
{
    Radius 13
    Height 8
    Speed 0
    +NOCLIP
    Damage 0
    PROJECTILE
    Scale 1.1
    +NOTIMEFREEZE
    States
    {
    Spawn:
	TNT1 A 0 A_Explode(160,256)
        PARS BCDEF 2 Bright
	PARS G 1 Bright A_FadeOut
        Wait
    }
}

Actor DBTSGTrail
{
    Projectile
    +NOCLIP
    Damage 0
    Speed 0
    RenderStyle Add
    Alpha 0.5
    Scale 0.4
    +NOTIMEFREEZE
    States
    {
    Spawn:
	PARS A 16 Bright
	PARS A 2 Bright A_FadeOut
	Wait
    }
}

//---------------------------MiniGhost Seeker
actor MiniGhostSeeker
{
    Radius 8
    Height 8
    Speed 16
    Damage 15
    PROJECTILE
    Scale 0.2
    +THRUGHOST
    +SEEKERMISSILE
    SeeSound ""
    DeathSound "Lich/death"
    +NOTIMEFREEZE
    States
    {
    Spawn:
        TSSH A 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH B 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH C 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH D 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH E 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH D 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH C 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH B 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        TSSH A 3 Bright A_SeekerMissile(2,90)
        TSSH A 0 Bright A_SpawnItem("DBTSGTrailSmall",0)
        Loop
    Death:
	TNT1 A 1 A_CustomMissile(DBTSGExploSmall,0,0,0)
	Stop
    }
}

actor DBTSGExploSmall
{
    Radius 13
    Height 8
    Speed 0
    +NOCLIP
    Damage 0
    PROJECTILE
    Scale .55
    +NOTIMEFREEZE
    States
    {
    Spawn:
        PARS BCDEF 2 Bright
	PARS G 1 Bright A_FadeOut
        Wait
    }
}

Actor DBTSGTrailSmall
{
    Projectile
    +NOCLIP
    Damage 0
    Speed 0
    RenderStyle Add
    Alpha 0.5
    Scale 0.2
    +NOTIMEFREEZE
    States
    {
    Spawn:
	TSSH A 16 Bright
	TSSH A 2 Bright A_FadeOut
	Wait
    }
}
