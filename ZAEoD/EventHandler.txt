Class AEoDHandler : EventHandler
{
	bool DemonMorph;
	
	//Array<uint8> SectorD;
		
	override void NetworkProcess(ConsoleEvent e)
	{		
		if (e.Name == 'EndDemonMorph')
		{
			int num = e.Player;
			let plr = players[num].mo;
			
			if (plr)
			{
				let dm = DemonPlayerMorph(plr);
				if (dm)
				{
					dm.EndMorph();
				}
			}
			return;
		}
		
		if (e.Name == 'DemonMorphOn')
		{
			/*
			if (!DemonMorph)
			{
				SectorD.Clear();
				SectorD.Reserve(level.Sectors.Size());
			}
			
			for (int i = 0; i < level.Sectors.Size(); i++)
			{
				Sector s = level.sectors[i];
				SectorD[i] = s.Colormap.Desaturation;
				s.SetColor(s.ColorMap.LightColor, 255);
			}
			*/
			DemonMorph = true;
			int num = e.Player;
			let plr = players[num].mo;
			
			
			ThinkerIterator it = ThinkerIterator.Create("Actor");
			Actor mo;
			while (mo = Actor(it.Next()))
			{
				if (mo.bISMONSTER && mo.health > 0)
				{
					if (mo.CountInv("DemonMorphTransToken") 
						|| mo.Species == 'Director'
						|| mo.Species == 'AEoDSpawner')	continue;
					mo.A_SetInventory("DemonMorphTransToken",1);
					DemonMorphTranslation dm = new("DemonMorphTranslation");
					dm.who = mo;
					dm.plr = plr;
				}
			}
		}
		else if (e.Name == 'DemonMorphOff')
		{
			DemonMorph = false;
			
			ThinkerIterator it = ThinkerIterator.Create("DemonMorphTranslation");
			DemonMorphTranslation mo;
			while (mo = DemonMorphTranslation(it.Next(true)))
			{
				mo.DestroyMe = true;
				continue;
			}
			/*
			for (int i = 0; i < level.Sectors.Size(); i++)
			{
				Sector s = level.sectors[i];
				s.SetColor(s.ColorMap.LightColor, SectorD[i]);
			}
			*/
			
		}
	}
	
	override void WorldUnloaded(WorldEvent e)
	{
		DemonMorph = false;
	}
	
	override void WorldThingSpawned(WorldEvent e)
	{
		if (DemonMorph && e.Thing)
		{
			Actor mo = e.Thing;
			if (mo && mo.bISMONSTER && mo.health > 0)
			{
				DemonMorphTranslation dm = new("DemonMorphTranslation");
				dm.who = mo;
			}
		}
	}
	
	override void WorldThingRevived(WorldEvent e)
	{
		if (DemonMorph && e.Thing)
		{
			Actor mo = e.Thing;
			if (mo && mo.bISMONSTER)
			{
				DemonMorphTranslation dm = new("DemonMorphTranslation");
				dm.who = mo;
				dm.resurrecting = true;
			}
		}
	}
}
/*
Class DemonMorphTranslator : Inventory
{
	DemonMorphTranslation dm;
	
	override bool TryPickup (in out Actor toucher)
	{
		if (toucher && toucher.CountInv(self.GetClass()))
			return false;
		if (Super.TryPickup(toucher))
		{
			if (invoker.Owner)
			{
				invoker.dm = new("DemonMorphTranslation");
				invoker.dm.who = invoker.Owner;
				return true;
			}
		}
		return false;
	}
	
	override void OwnerDied()
	{
		if (invoker.dm && )
		{
			invoker.Owner.
		}
	}
}
*/

Class DemonMorphTransToken : Inventory {}

Class DemonMorphTranslation : Thinker
{
	uint OldTranslation;
	uint CurrentTranslation;
	uint FriendlyTranslation;
	uint EnemyTranslation;
	Actor who;
	Actor plr;
	bool wasFriendly;
	bool resurrecting;
	bool DestroyMe;
	
	
	override void PostBeginPlay()
	{
		if (who)
		{
			OldTranslation = who.Translation;
			who.A_SetTranslation("DemonMorphAlly");
			FriendlyTranslation = who.Translation;
			who.A_SetTranslation("DemonMorphTarget");
			EnemyTranslation = who.Translation;
			
			if (who.bFRIENDLY)	{ who.A_SetTranslation("DemonMorphAlly"); wasFriendly = true; }
			else				who.A_SetTranslation("DemonMorphTarget");
			
			who.bBRIGHT = true;
			CurrentTranslation = who.Translation;
		}
		Super.PostBeginPlay();
	}
	
	//==========================================================================
	//
	// The tick function.
	//
	//==========================================================================
	override void Tick()
	{
		// Destroy the thinker if we no actor.
		if (!who || DestroyMe) return Destroy();
		
		// Destroy the thinker if actor's dead and not resurrecting, 
		if (who.health < 0)
		{
			if (resurrecting)	return Super.Tick();
			else				return Destroy();
		}
		else resurrecting = false;
		
		// No point doing anything if invisible.
		if (who.alpha <= 0 || who.GetRenderStyle() <= 0 || who.bINVISIBLE)
			return Super.Tick();
		
		
		// Change the translation based on friendly or not.
		// Make sure not to record it by changing the base and current
		// at the same time.
		if (!wasFriendly && who.bFRIENDLY)
		{
			who.Translation = CurrentTranslation = FriendlyTranslation;
			wasFriendly = true;
		}
		else if (wasFriendly && !who.bFRIENDLY)
		{
			who.Translation = CurrentTranslation = EnemyTranslation;
			wasFriendly = false;
		}
		
		
		// Should their translation change at any point, record it.
		if (who.Translation != CurrentTranslation)
		{
			OldTranslation = who.Translation;
		}
		
		// Then set it again, and done.
		who.Translation = CurrentTranslation;
		who.bBRIGHT = true;
		Super.Tick();
	}
	//==========================================================================
	//
	// Destroy the thinker after resetting the translation. This only applies
	// to monsters that don't burst and disappear into tiny fragments.
	//
	//==========================================================================
	override void OnDestroy()
	{
		if (who)
		{
			who.A_SetInventory("DemonMorphTransToken",0);
			who.Translation = OldTranslation;
			who.bBRIGHT = false;
		}
		Super.OnDestroy();
	}
}