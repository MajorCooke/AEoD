Class AEoDFX : Actor
{
	bool RandomRoll;
	bool RandomScaleFlip;
	double RollAdd;
	double RollAddMin;
	double RollAddMax;
	
	property RollAdd:			RollAdd;
	property RollAddMin:		RollAddMin;
	property RollAddMax:		RollAddMax;
	property RandomRoll: 		RandomRoll;
	property RandomScaleFlip: 	RandomScaleFlip;
	
	Default
	{
		AEoDFX.RollAddMin 0.0;
		AEoDFX.RollAddMax 0.0;
		AEoDFX.RollAdd 0.0;
		AEoDFX.RandomRoll false;
		AEoDFX.RandomScaleFlip false;
		+ROLLSPRITE
		+NOTELESTOMP
	}
	override void BeginPlay()
	{
		ChangeStatNum(StatNum.EFFECTS());
		Super.BeginPlay();
	}
	
	override void PostBeginPlay()
	{
		if (bINVISIBLE) return Super.PostBeginPlay();
		Super.PostBeginPlay();
		if (RandomScaleFlip) 	A_SetScale(Scale.X*randompick(-1,1),Scale.Y*randompick(-1,1));
		if (RandomRoll)			roll = frandom(0.0, 359.9);
		if (bROLLSPRITE)
		{
			if (RollAddMin != 0.0 || RollAddMax != 0.0)
				RollAdd = frandom(RollAddMin, RollAddMax);
		}
	}
	
	override void Tick()
	{
		if (bINVISIBLE)	return Super.Tick();
		Super.Tick();
		if (RollAdd != 0.0)	A_SetRoll(roll + RollAdd, SPF_INTERPOLATE);
	}
}

Class InvisiPuff : AEoDFX
{
	Default
	{
		+NOINTERACTION
		+NOBLOCKMAP
		+BLOODLESSIMPACT
		+NODECAL
		+PAINLESS
	}
	States
	{
	Spawn:
		TNT1 A 1;
		Stop;
	}
}

Class BulletTracerFX : AEoDFX 
{
	const Sparsity = 125.0;
	const Distance = 3.25;
	const Multiplier = 1;
	const SpawnSpeed = 300;
	const TravelTic = Sparsity * 30.0;
	enum BulletTracerFX
	{
		ModelOnly = 		1,
		ParticleOnly =		2,
		ParticleTrail =		3,
		TrailOnly =			4,
	};
	int user_inv;
	int user_count;
	int user_rng;
	int user_render;
	double user_dista;
	double user_distb;
	
	override void PostBeginPlay()
	{
		if (target) pitch = target.pitch;
		Super.PostBeginPlay();
	}
	
	Default 
	{
		+NOINTERACTION
		+NOTONAUTOMAP
		+NOTIMEFREEZE
		RenderStyle "None";
		Speed 1;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{	
			if (A_CheckSight("Null"))
			{	return ResolveState("Null");	}
			
			A_FaceMovementDirection();
			//pitch *= -1;

			if (!IsPointerEqual(TracerPtr,NullPtr))
			{	user_distb = Clamp(GetDistance(true,TracerPtr),0.0,3000.0);	}
			else
			{	user_distb = 0;	}
			
			return ResolveState("B1");
		}
	B1:
		TNT1 A 1;
	Break:
		TNT1 A 1 
		{
			if (user_count > 14 || A_CheckSight("Null") || (pos.z < floorz) || (pos.z > ceilingz) ) 
			{	return ResolveState("Null");	} // Destroy it.
			user_dista = 0;
			return ResolveState(null); //Play out the tic.
		}
	Looping:	// Spawns the trail.
		TNT1 A 0
		{
			// If running GZDoom, spawn the neat trail.
			if (GetCvar("vid_renderer"))
			{
				if (user_dista <= 0.0)
				{	user_dista = 64.0;	}
				A_SpawnItemEx("BulletTrailBeam",cos(pitch)*user_dista,0,-sin(pitch)*user_dista,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
				user_dista += Sparsity;
			}
			else //Otherwise, do the ol' fashion particles.
			{
				A_SpawnParticle("FFE99E",SPF_FULLBRIGHT|SPF_RELATIVE,100,random(3,6),0,
					cos(pitch)*user_dista,0,-sin(pitch)*user_dista,
					frandom(1.0,2.0),0,0,
					-0.005,0,0.005,
					frandom(0.5,1.0),frandom(0.06,0.12));
				
				user_dista += frandom(3.0,6.0);
			}
			
			if (user_distb > 0.0)
			{
				if (user_dista > user_distb)
				{	return ResolveState("Null");	}
				return ResolveState("Looping");
			}
			else if (user_dista >= TravelTic)
			{
				user_count++;
				A_Warp(DefPtr,cos(pitch)*TravelTic,0,-sin(pitch)*TravelTic,0,WARPF_NOCHECKPOSITION);
				return ResolveState("Break");
			}
			return ResolveState("Looping");
		}
		Loop;
	End:
		TNT1 A 0;
		Stop;
	}
}

Class BulletTracerFXInverted : BulletTracerFX
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{	
			if (A_CheckSight("Null"))
			{	return ResolveState("Null");	}
			
			A_FaceMovementDirection();
			pitch *= -1;

			if (!IsPointerEqual(TracerPtr,NullPtr))
			{	user_distb = Clamp(GetDistance(true,TracerPtr),0.0,3000.0);	}
			else
			{	user_distb = 0;	}
			
			return ResolveState("B1");
		}
	}
}

Class TrailBeam : AEoDFX 
{
	const ZOffset = 16;
	const NoSpawn = 256;
	const PFlags = WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION|WARPF_COPYVELOCITY|WARPF_ABSOLUTEOFFSET;
	double user_time;
	double user_scale;
	int user_neg;
	Default 
	{
		+NOINTERACTION
		+FLATSPRITE
		+BRIGHT
		+ROLLCENTER
		YScale 0.25; //Don't touch this! Otherwise the trail will become broken up!
		XScale 0.03; //This modifies the width of the beam. It is safe to play around with.
		Alpha 0;
		RenderStyle "Add";
	}
	// DO NOT add a translation. If you need to change the colors, make a sprite
	// entry in TEXTURES lump and use BLEND. NEVER TRANSLATE!
	States
	{
	Spawn:
		X163 B 0 NoDelay
		{
			// Sets how long to split the scaling-in effect.
			user_time = 3.0;
			
			// Set this to the actor's same name. The TID will prevent it spawning infinitely (tid * 256).
			A_SpawnItemEx("TrailBeam",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_TRANSFERSCALE|SXF_TRANSFERPOINTERS,tid * 256,1);
			return ResolveState("Process");
		}
	Process:
		"####" "#" 1
		{
			//A_Warp(DefPtr,0,0,ZOffset,0,WARPF_NOCHECKPOSITION);
			user_scale = Scale.X;
			user_neg = ((user_scale < 0) ? 1 : 0);
			// If it's a secondary trail (vertical)
			if (tid > 0)	
			{	
				Thing_ChangeTID(0,0);
				roll = pitch + 90;
				pitch = 90;
				angle += 90;
			}
			A_FadeIn(1.0,FTF_CLAMP);
		}
		"####" "#" 1 
		{
			// Keep 'shrinking'. NEVER fade out!
			
			if (user_neg)
			{
				A_SetScale(Scale.X - (user_scale / Max(1.0, user_time)), Scale.Y);
				if (Scale.X >= 0)	{ return ResolveState("Null"); }
			}
			else
			{			
				A_SetScale(Scale.X - (user_scale / Max(1.0, user_time)), Scale.Y);
				if (Scale.X <= 0)	{ return ResolveState("Null"); }
			}
			return ResolveState(null);
		}
		Wait;
	}
}

Class MovingTrailBeam : AEoDFX 
{
	const NoSpawn = 256;
	double  user_dist;
	int user_t;
	int user_zoffset;
	double user_angle;
	double user_pitch;
	double user_time;
	double user_scale;
	int user_pflags;
	int user_neg;
	Vector3 next1, next2;
	int uid;
	Default 
	{
		+NOINTERACTION
		+FLATSPRITE
		+BRIGHT
		+ROLLCENTER
		YScale 0.25; //Don't touch this! Otherwise the trail will become broken up!
		XScale 0.03; //This modifies the width of the beam. It is safe to play around with.
		Alpha 0;
		RenderStyle "Add";
	}
	// DO NOT add a translation. If you need to change the colors, make a sprite
	// entry in TEXTURES lump and use BLEND. NEVER TRANSLATE!
	
	States
	{
	Spawn:
		X202 B 0 NoDelay
		{
			// Sets how long to split the scaling-in effect.
			user_time = 3.0;
			user_zoffset = 16;
			
			// Set this to the actor's same name. The TID will prevent it spawning infinitely (tid * 256).
			A_SpawnItemEx("MovingTrailBeam",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_TRANSFERSCALE|SXF_TRANSFERPOINTERS,tid * 256,1);
		}
		Goto Process;
	Process:
		"####" "#" 0
		{
			if (!user_pflags)	{ user_pflags = WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET;	}
			A_Warp(DefPtr,0,0,user_zoffset,0,WARPF_NOCHECKPOSITION);
			// If it's a secondary trail (vertical)
			if (tid > 0)	
			{	
				ChangeTID(0);
				roll = pitch + 90;
				pitch = 90;
				angle += 90;
			}
		
			// Save our current information so we know how to properly interpolate whenever
			// the owner moves.
			user_neg = (Scale.X < 0) ? 1 : 0;
			user_scale = Scale.X;
			user_angle = angle;
			user_pitch = pitch;
			user_dist = GetDistance(false, TargetPtr);
			next1 = pos;
			
			// Move to the shooter so we can get just how far out we are.
			A_Warp(TargetPtr,0,0,0,0,WARPF_NOCHECKPOSITION);
			next2 = next1 - pos;
			
			// Now move back without interpolating. Otherwise it'll look weird.
			A_Warp(TargetPtr,next2.x,next2.y,next2.z,0,WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET);
			angle = user_angle;
			pitch = user_pitch;
		}
		"####" "#" 1 
		{
			A_FadeIn(1.0,FTF_CLAMP);
			A_Warp(TargetPtr, next2.x + vel.x, next2.y + vel.y, next2.z + vel.z, 0, user_pflags);
			angle = user_angle;
			pitch = user_pitch;
		}
		"####" "#" 1 
		{
			// Keep 'shrinking'. NEVER fade out!
			if (user_neg)
			{
				A_SetScale(Scale.X - (user_scale / Max(1.0, user_time)), Scale.Y);
				if (Scale.X >= 0)	{ return ResolveState("Null"); }
			}
			else
			{
				A_SetScale(Scale.X - (user_scale / Max(1.0, user_time)), Scale.Y);
				if (Scale.X <= 0)	{ return ResolveState("Null"); }
			}
			A_Warp(TargetPtr, next2.x + vel.x, next2.y + vel.y, next2.z + vel.z ,0, user_pflags);
			angle = user_angle;
			pitch = user_pitch;
			return ResolveState(null);
		}
		Wait;
	}
}

/*
Class MovingTrailBeam : AEoDFX 
{
	const NoSpawn = 256;
	double user_dist;
	int user_t;
	int user_zoffset;
	double user_angle;
	double user_pitch;
	Vector3 pa, pb;
	double user_time;
	double user_scale;
	int user_pflags;
	int user_neg;
	int uid;
	Default 
	{
		+NOINTERACTION
		+FLATSPRITE
		+BRIGHT
		+ROLLCENTER
		YScale 0.25; //Don't touch this! Otherwise the trail will become broken up!
		XScale 0.03; //This modifies the width of the beam. It is safe to play around with.
		Alpha 0;
		RenderStyle "Add";
	}
	// DO NOT add a translation. If you need to change the colors, make a sprite
	// entry in TEXTURES lump and use BLEND. NEVER TRANSLATE!
	
	override void BeginPlay()
	{
		uid = tid;
		ChangeTID(0);
		Super.BeginPlay();
	}
	
	States
	{
	Spawn:
		X202 B 0 NoDelay
		{
			// Sets how long to split the scaling-in effect.
			user_time = 3.0;
			user_zoffset = 16;
			// Set this to the actor's same name. The TID will prevent it spawning infinitely (tid * 256).
			A_SpawnItemEx("MovingTrailBeam",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_TRANSFERSCALE|SXF_TRANSFERPOINTERS,uid * 256,1);
			return ResolveState("Process");
		}
	Process:
		"####" "#" 0
		{
			if (!user_pflags)	{ user_pflags = WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET;	}
			A_Warp(DefPtr,0,0,user_zoffset,0,WARPF_NOCHECKPOSITION);
			// If it's a secondary trail (vertical)
			if (uid > 0)	
			{
				roll = pitch + 90;
				pitch = 90;
				angle -= 90;
			}
		
			// Save our current information so we know how to properly interpolate whenever
			// the owner moves.
			user_neg = (Scale.X < 0) ? 1 : 0;
			user_scale = Scale.X;
			user_angle = angle;
			user_pitch = pitch;
			user_dist = GetDistance(false, TargetPtr);
			pa = pos;
			
			// Move to the shooter so we can get just how far out we are.
			A_Warp(TargetPtr,0,0,0,0,WARPF_NOCHECKPOSITION);
			pb = pa - pos;
			
			// Now move back without interpolating. Otherwise it'll look weird.
			A_Warp(TargetPtr, pb.x, pb.y, pb.z,0,WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET);
			angle = user_angle;
			pitch = user_pitch;
		}
		"####" "#" 1 
		{
			A_FadeIn(1.0,FTF_CLAMP);
			A_Warp(TargetPtr, pb.x + vel.x, pb.y + vel.y, pb.z + vel.z, 0, user_pflags);
			angle = user_angle;
			pitch = user_pitch;
		}
		"####" "#" 1 
		{
			// Keep 'shrinking'. NEVER fade out!
			if (user_neg)
			{
				A_SetScale(Scale.X - (user_scale / Max(1.0, user_time)), Scale.Y);
				if (Scale.X >= 0)	{ return Destroy(); }
			}
			else
			{
				A_SetScale(Scale.X - (user_scale / Max(1.0, user_time)), Scale.Y);
				if (Scale.X <= 0)	{ return Destroy(); }
			}
			A_Warp(TargetPtr, pb.x + vel.x, pb.y + vel.y, pb.z + vel.z, flags: user_pflags);
			angle = user_angle;
			pitch = user_pitch;
		}
		Wait;
	}
}
*/

Class BulletTrailBeam : TrailBeam
{
	Default 
	{
		XScale 0.05;
		YScale 1; //Don't touch.
	}
	States
	{
	Spawn:
		X163 B 0 NoDelay
		{
			// user_time = time to shrink to nothing
			user_time = 4.0;
			A_SpawnItemEx("BulletTrailBeam",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_TRANSFERSCALE|SXF_TRANSFERPOINTERS,tid * 256,1);
			return ResolveState("Process");
		}
	}
}

Class BulletTrailBeamInverted : BulletTrailBeam
{
	Default 
	{
		XScale 0.05;
		YScale 1; //Don't touch.
	}
	States
	{
	Spawn:
		X163 B 0 NoDelay
		{
			// user_time = time to shrink to nothing
			pitch *= -1;
			user_time = 4.0;
			A_SpawnItemEx("BulletTrailBeamInverted",0,0,0,0,0,0,180,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_TRANSFERSCALE|SXF_TRANSFERPOINTERS,tid * 256,1);
			return ResolveState("Process");
		}
	}
}