Actor SkillCount : Inventory
{
	Inventory.MaxAmount 5
	Inventory.InterHubAmount 0
}

Actor NightmareToken : Inventory
{
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 1
}

Actor HordeModeTest : Inventory
{	Inventory.MaxAmount 1	}

Actor ZombieHealthDivider : Inventory
{	Inventory.MaxAmount 6	
	Inventory.InterHubAmount 6	}

ACTOR L4DDirectorSpawner : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("TheDirectorL4D",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_NOPOINTERS|SXF_SETTARGET)
		Stop
	}
}

Actor DirectorIsLeaving : Inventory
{	Inventory.MaxAmount 1	}

Actor SmallCDUp : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	var int user_nightmare;
	States
	{
	Use:
		TNT1 A 0 A_SetUserVar(user_nightmare,user_nightmare)
		TNT1 A 0 A_JumpIf(user_nightmare > 0,"NM")
		TNT1 A 0 A_SetUserArray(user_cooldown,1,random(75,200))
		Stop
	NM:
		TNT1 A 0 A_SetUserArray(user_cooldown,1,random(100,350))
		Stop
	}
}
Actor SmallSpawnTokenTwo : Inventory
{
	Inventory.MaxAmount 50
	Inventory.InterHubAmount 50
}

Actor TheDirectorL4D
{
	TeleFogSourceType None
	TeleFogDestType None
	var int user_index;
	var int user_nightmare;
	var int user_mul;
	var int user_bias[2];
	//Biasity (#/256) to jump to the medium and large spawning categories.
	//random(1,256) <= user_bias[#]
	//[0] = Medium spawn chance
	//[1] = Large spawn chance
	var int user_cc[3];
	//Max number of children allowed active at any time.
	//Determined by script.
	//[0] = Small spawners
	//[1] = Medium spawners
	//[2] = Large spawners
	var int user_cd[9];
	//Will be used by the script to determine current intensities.
	//[0] = Small cooldown min
	//[1] = Small cooldown max
	//[2] = Medium cooldown min
	//[3] = Medium cooldown max
	//[4] = Large cooldown min
	//[5] = Large cooldown max
	//[6] = Horde cooldown min
	//[7] = Horde cooldown max
	//[8] = Random amount to add to cooldowns from 0 to [8]
	var int user_cooldown[5];
	//[0] = Normal cooldown
	//[1] = Exclusive small child countdown
	//[2] = Bool [0] on cooldown
	//[3] = Horde Cooldown
	var int user_delay;
	var int user_horde[3];
	//[0] = Is it available?
	//[1] = Timer
	//[2] = Cooldown
	var int user_volume;
	Monster
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+NORADIUSDMG
	+THRUACTORS
	+NEVERTARGET
	+NOTARGETSWITCH
	+CANTSEEK
	-FRIENDLY
	-COUNTKILL
	+NOCLIP
	+FLOORHUGGER
	+LOOKALLAROUND
	+NEVERFAST
	+NODAMAGE
	+NONSHOOTABLE
	-SHOOTABLE
	-NODROPOFF
	//+DROPOFF
	+NOTRIGGER
	-TELESTOMP
	+NOTELESTOMP
	+DONTMORPH
	+NOICEDEATH
	+DONTSPLASH
	+DONTCORPSE
	Radius 10
	Height 20
	Speed 20
	+DONTTHRUST
	Health 1000000
	DamageFactor "Normal", 0
	Species "Director"
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_ChangeFlag("FRIENDLY",0)
		TNT1 A 1 Thing_ChangeTID(0,0)
		//Default values
		TNT1 A 0 A_SetUserVar(user_mul,CallACS("DirectorIntensity",2))
		TNT1 A 0 A_SetUserArray(user_bias,0,6)	//#/256 chance to spawn medium
		TNT1 A 0 A_SetUserArray(user_bias,1,2) 	//^ large
		TNT1 A 0 A_SetUserArray(user_cd,0,2500) 	//Small cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,1,5000) 	//Small cooldown max
		TNT1 A 0 A_SetUserArray(user_cd,2,4000)  //Medium cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,3,10000) //Medium cooldown max
		TNT1 A 0 A_SetUserArray(user_cd,4,10000) //Large cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,5,20000) //Large cooldown max
		TNT1 A 0 A_SetUserArray(user_cd,6,22200) //Horde cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,7,46500) //Horde cooldown max
		TNT1 A 0 A_SetUserArray(user_cd,8,random(50,175))
		TNT1 A 0 A_SetUserArray(user_cc,0,6) //small
		TNT1 A 0 A_SetUserArray(user_cc,1,4) //medium
		TNT1 A 0 A_SetUserArray(user_cc,2,2) //large
		TNT1 A 0 A_RearrangePointers(AAPTR_PLAYER1,AAPTR_NULL,AAPTR_NULL,3)
		TNT1 A 0 A_SpawnItemEx("L4DPopulater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SetUserVar(user_volume,10)
		TNT1 A 0 A_JumpIf(user_mul < 666,2)
		TNT1 A 0 A_SetUserVar(user_volume,1)
		TNT1 A 0 A_JumpIf(user_mul > 0,"Idle")
		TNT1 A 0 A_PrintBold("Multiplier was 0. This should not happen!\nReport this to the AEoD team immediately.",7.0)
		TNT1 A 0 A_SetUserVar(user_mul,3) //Prevent division by 0.
		Goto Idle
	Idle:
		TNT1 A 1 A_Look
		Loop
	See:
		TNT1 AAA 350 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_PLAYER1) //The original director has 60 seconds. This one is 30.
		TNT1 A 0 A_JumpIf(user_mul > 4,"EnableNightmare")
		TNT1 A 0 A_TakeInventory("NightmareToken",1,0,AAPTR_PLAYER1)
	SeeCont:
		TNT1 A 0 A_JumpIfInventory("PFirstTimeToken2",1,"FirstTime",AAPTR_PLAYER1)
		Goto Chasing
	//Nightmare mode effectively causes regular zombies to spawn roughly twice as fast.
	EnableNightmare:
		TNT1 A 0 A_SetUserVar("user_nightmare",1)
		TNT1 A 0 A_TakeInventory("NightmareToken",100,0,AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("NightmareToken",1,AAPTR_PLAYER1)
		/*
		TNT1 A 0 A_SetUserArray(user_cd,0,18) //Small cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,1,50) //Small cooldown max
		TNT1 A 0 A_SetUserArray(user_cd,2,150) //Medium cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,3,337) //Medium cooldown max
		TNT1 A 0 A_SetUserArray(user_cd,4,225) //Large cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,5,450) //Large cooldown max
		TNT1 A 0 A_SetUserArray(user_cd,6,273) //Horde cooldown min
		TNT1 A 0 A_SetUserArray(user_cd,7,788) //Horde cooldown max
		*/
		Goto SeeCont
	FirstTime:
		TNT1 A 0 A_TakeInventory("PFirstTimeToken2",1,0,AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIf(user_mul >= 666,"Chasing")
		TNT1 A 0 A_GiveInventory("SmallSpawnTokenTwo",(50-(user_mul*3)),AAPTR_PLAYER1)
		
	Chasing:
		TNT1 A 0 A_SetUserVar(user_index,0)
	CLoop:
		TNT1 A 0 A_JumpIf(user_cooldown[user_index] <= 0,2)
		TNT1 A 0 A_SetUserArray("user_cooldown",user_index,user_cooldown[user_index]-(user_mul*random(7,10)))
		TNT1 A 0 A_SetUserVar(user_index,user_index+1)
		TNT1 A 0 A_JumpIf(user_index > 3,"ChaseCont")
		Goto CLoop
	ChaseCont:
		TNT1 A 0 A_SetUserVar(user_index,0)
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("HordeModeTest",1,"HordeModeForce",AAPTR_TARGET)
		TNT1 A 2 A_Chase
		Goto CLoop
	HordeModeForce:
		TNT1 A 0 A_TakeInventory("HordeModeTest",1,0,AAPTR_TARGET)
		TNT1 A 0 A_SetUserArray("user_horde",2,0) //Wipe it clean for testing purposes.
		TNT1 A 0 A_SetUserArray("user_cooldown",0,0)
		TNT1 A 0 A_SetUserArray("user_cooldown",1,0)
		TNT1 A 0 A_SetUserArray("user_cooldown",2,0)
		TNT1 A 0 A_SetUserArray("user_cooldown",3,0)
		Goto HordeMode
	HordeMode:
		TNT1 A 0 A_JumpIfInventory("DirSmallChildCount",24,"Chasing") //Don't go overwhelming on 666 intensities.
		TNT1 A 0 A_JumpIf(user_cooldown[3] >= 1,"Chasing")
		TNT1 A 0 A_PlaySound("Director/Horde",5,(user_volume*0.1),0,ATTN_NONE)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_GiveInventory("HordeToken",1)
		TNT1 A 0 A_GiveInventory("DirSmallChildCount",6)
		TNT1 AAAAAA 0 A_SpawnItemEx("L4DSmallMobRunner",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,user_mul)
		TNT1 A 0 A_SetUserArray("user_cooldown",3,random(user_cd[6],user_cd[7]))
		TNT1 AAAAA 35
		TNT1 A 0 A_TakeInventory("HordeToken",1)
		Goto ChanceCD
	Missile:
		TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION)
		TNT1 A 0 A_JumpIfInventory("SmallSpawnTokenTwo",1,"SmallMobShoot",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIf(user_mul < 666,"Missile2")
		TNT1 A 0 A_Jump(104,"MediumMobshoot")
		TNT1 A 0 A_Jump(64,"LargeMobshoot","HordeMode")
		Goto SmallMobShoot
	Missile2:
		TNT1 A 0 A_JumpIf(random(1,2560) <= (user_bias[1]+(user_mul*20)),"LargeMobDecide")
		TNT1 A 0 A_JumpIf(random(1,2560) <= (user_bias[0]+(user_mul*60)),"MediumMobshoot")
		Goto SmallMobShoot
	LargeMobDecide:
		TNT1 A 0 A_Jump(192,"HordeMode")
	LargeMobshoot:
		TNT1 A 0 A_JumpIfInventory("DirLargeChildCount",user_cc[2]+user_mul,"Chasing")
		TNT1 A 0 A_JumpIf(user_cooldown[2] > 0,"Chasing")
		TNT1 A 0 Thing_ChangeTID(0,32765)
		TNT1 A 1 A_SpawnItemEx("L4DLargeMobRunner",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		TNT1 A 0 A_JumpIf(tid != 0, "FailedSpawn")
		TNT1 A 0 A_GiveInventory("DirLargeChildCount",1)
		TNT1 A 0 A_SetUserArray(user_cooldown,2,random(user_cd[4],user_cd[5]))
		Goto ChanceCD
	MediumMobshoot:
		TNT1 A 0 A_JumpIfInventory("DirMediumChildCount",user_cc[1]+user_mul,"Chasing")
		TNT1 A 0 A_JumpIf(user_cooldown[1] > 0,"Chasing")
		TNT1 A 0 Thing_ChangeTID(0,32765)
		TNT1 A 1 A_SpawnItemEx("L4DMediumMobRunner",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		TNT1 A 0 A_JumpIf(tid != 0, "FailedSpawn")
		TNT1 A 0 A_GiveInventory("DirMediumChildCount",1)
		TNT1 A 0 A_SetUserArray(user_cooldown,1,random(user_cd[2],user_cd[3]))
		Goto ChanceCD
	SmallMobShoot:
		TNT1 A 0 A_JumpIfInventory("DirSmallChildCount",user_cc[0]+user_mul,"Chasing")
		TNT1 A 0 A_JumpIf(user_cooldown[0] > 0,"Chasing")
		TNT1 A 0 A_TakeInventory("SmallSpawnTokenTwo",1,0,AAPTR_PLAYER1)
		TNT1 A 0 Thing_ChangeTID(0,32765)
		TNT1 A 1 A_SpawnItemEx("L4DSmallMobRunner",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		TNT1 A 0 A_JumpIf(tid != 0, "FailedSpawn")
		TNT1 A 0 A_GiveInventory("DirSmallChildCount",1)
		TNT1 A 0 A_SetUserArray(user_cooldown,0,random(user_cd[0],user_cd[1]))
		Goto ChanceCD
	FailedSpawn:
		TNT1 A 0 Thing_ChangeTID(0,0)
		Goto Chasing
	ChanceCD:
		TNT1 A 0 A_Jump(240,"Chasing")
		TNT1 A 0 A_Jump(256,1,1,2,2,3,3,4)
		TNT1 AAAAAA 350
		Goto Chasing
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		Goto Chasing
	Death:
		TNT1 A 35 A_RemoveChildren(1,RMVF_EVERYTHING,None,"Director")
		//TNT1 A 35 A_GiveInventory("DirectorIsLeaving",1)
		TNT1 A 0 A_TakeInventory("RemoveDirector",32767,0,AAPTR_TARGET)
		Stop
	}
}

Actor L4DPopulater
{
	var int user_wander;
	var int user_populate;
	var int user_cooldown;
	TeleFogSourceType None
	TeleFogDestType None
	Monster
	-TELESTOMP
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+NORADIUSDMG
	+THRUACTORS
	+NEVERTARGET
	+NOTARGETSWITCH
	+CANTSEEK
	-FRIENDLY
	-COUNTKILL
	+LOOKALLAROUND
	+NEVERFAST
	+NODAMAGE
	+DONTSPLASH
	+NONSHOOTABLE
	-SHOOTABLE
	-NODROPOFF
	+DROPOFF
	+NOTRIGGER
	+NOBLOCKMAP
	+NOTELESTOMP
	+NOTONAUTOMAP
	+DONTTHRUST
	+DONTSPLASH
	+DONTCORPSE
	Radius 16
	Height 64
	Speed 20
	RenderStyle None
	Species "Director"
	States
	{
	Spawn:
		TNT1 A 0
	Idle:
		TNT1 A 1 A_Look
		Loop
	See:
	Cooldown:
		TNT1 A 1 A_SetTics(35*random(1,10))
	Wandering:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Removal",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("DirectorIsLeaving",1,"Removal",AAPTR_MASTER)
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SetUserVar("user_wander",user_wander+1)
		TNT1 A 0 A_JumpIf(user_wander>=900,"Populate")
		Loop
	Populate:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Removal",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		TNT1 A 0 A_SetUserVar("user_wander",0)
		TNT1 A 0 A_JumpIfCloser(250,"NoPop")
		TNT1 A 0 A_JumpIfTargetInLOS("NoPop",360)
		TNT1 A 0 A_CheckSight(1)
		Goto NoPop
		TNT1 A 1 A_SpawnItemEx("L4DCommonSpawner",random(-60,60),random(-60,60),0,0,0,0,random(0,359),SXF_TRANSFERPOINTERS,210)
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,random(15,150),0,0,random(0,359))
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10)
		TNT1 AAAAAAAAAA 1 A_Wander
		TNT1 A 0 A_SetUserVar("user_populate",user_populate+1)
		TNT1 A 0 A_JumpIf(user_populate>=random(15,45),1)
		Loop
		TNT1 A 0 A_SetUserVar("user_populate",0)
	MoveBack:
		TNT1 A 0 A_Warp(AAPTR_PLAYER1,0,0,0,0,WARPF_NOCHECKPOSITION)
		Goto Cooldown
	NoPop:
		TNT1 A 0 A_Jump(256,1,2,3,4,5)
		TNT1 AAAAAAA 1 A_Wander
		Goto Populate
	Frozen:
		TNT1 A 0 A_JumpIfInventory("DirectorIsLeaving",1,"Removal",AAPTR_MASTER)
		TNT1 A 35 A_JumpIfInventory("RemoveDirector",1,"Removal",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_PLAYER1)
		Goto Wandering
	Removal:
	Death:
		TNT1 A 0
		Stop
	}
}


Actor L4DLargeMobRunner
{
	var int user_check[2];
	var int user_t;
	Monster
	+THRUACTORS
	+FRIGHTENED
	+NOFEAR
	+DONTTHRUST
	-SOLID
	+NEVERTARGET
	-COUNTKILL
	+CANTSEEK
	+FLOORHUGGER
	+NORADIUSDMG
	+NOBLOOD
	-TELESTOMP
	+LOOKALLAROUND
	+NOTARGETSWITCH
	+NONSHOOTABLE
	-SHOOTABLE
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+CANTLEAVEFLOORPIC
	+NOTRIGGER
	+NOTELESTOMP
	+DONTSPLASH
	+DONTCORPSE
	+NOBLOCKMAP
	+LAXTELEFRAGDMG
	+ALWAYSTELEFRAG
	+NOPAIN
	RenderStyle None
	//MaxDropOffHeight 128
	//MaxStepHeight 128
	+DONTTHRUST
	Speed 14
	Radius 60
	Height 60
	Health 1000000
	DamageFactor "Normal", 0.0
	DamageFactor "DirectorKiller", 32767
	Species "Director"
	States
	{
	Spawn:
		TNT1 A 0 //NoDelay A_JumpIfInventory("HordeToken",1,"Idle",AAPTR_PLAYER1)
	Idle:
		TNT1 A 0 A_ChangeFlag("FRIENDLY",0)
		//TNT1 A 0 //A_PrintBold("Large")
		TNT1 AAAAA 1 A_Look
		Loop
	See:
	Wanderbout:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 0 Thing_ChangeTID(0,0)
		TNT1 A 1 A_Chase
		TNT1 AAAAAAAAA 1 A_Wander
		
	//After a bit of time, allow them to leave the floor they are bound to.
		TNT1 A 0 A_JumpIf(user_t >= 35,4)
		TNT1 A 0 A_SetUserVar(user_t,user_t+1)
		TNT1 A 0 A_JumpIf(user_t < 35,2)
		TNT1 A 0 A_ChangeFlag("CANTLEAVEFLOORPIC",0)
		
	//So here's the deal about A_CheckBlock. For situations like this, we only
	//care about being stuck on lines. So merely turn off NOBLOCKMAP for now
	//and enable it again afterwards.
		//TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_NULL,PTROP_NOSAFEGUARDS)
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",0)
		TNT1 A 0 A_CheckBlock("Stuck")
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",1)
	Mobmaking:
		//TNT1 A 0 A_JumpIfInventory("HordeToken",1,3,AAPTR_MASTER)
		TNT1 A 0 A_JumpIfCloser(250,"Wanderbout")
		TNT1 A 0 A_CheckSight(1)
		Goto Wanderbout
		//TNT1 A 0 A_JumpIfInventory("HordeToken",1,1,AAPTR_MASTER)
		//Goto MobChance
	MobChance:
		TNT1 A 0 Thing_ChangeTID(0,32668)
		TNT1 A 0 A_Jump(128,2,3)
		TNT1 A 0 A_SpawnItemEx("L4DTank",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto Vanish
		TNT1 A 0 A_SpawnItemEx("L4DWitchSitting",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto Vanish
		TNT1 A 0 A_SpawnItemEx("L4DWitchWander",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto Vanish
	Vanish:
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 A 0 A_TakeFromSiblings("AEoDToken",100)
		TNT1 A 0 A_GiveToSiblings("AEoDToken",1)
		//TNT1 AAAAAAAA 0 A_Wander
		//TNT1 A 0 A_JumpIfInventory("HordeToken",1,"VanishHorde",AAPTR_MASTER)
		TNT1 A 0 A_GiveInventory("CooldownDisable1",1,AAPTR_MASTER)
		TNT1 A 0 A_GiveInventory("LargeCooldown",5,AAPTR_MASTER)
		Goto VanishC
	VanishHorde:
		TNT1 A 1
		Goto End
	VanishC:
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",1,2)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck")
		TNT1 A 0 A_DropItem("L4DPackageSpawnerCheck",1,128)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck",1,64)
		TNT1 A 0 A_SpawnItemEx("Megasphere",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,190)
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,160)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerDirector",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,64)
		TNT1 A 0
		Goto End
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		Goto See
	Stuck:
	Death:
	End:
		TNT1 A 0 A_TakeInventory("DirLargeChildCount",1,0,AAPTR_MASTER)
		Stop
	}
}

Actor L4DMediumMobRunner
{
	TeleFogSourceType None
	TeleFogDestType None
	var int user_t;
	var int user_dlimit;
	var int user_limit;
	Monster
	+THRUACTORS
	+FRIGHTENED
	+NOFEAR
	+DONTTHRUST
	-SOLID
	+NEVERTARGET
	-COUNTKILL
	+CANTSEEK
	+FLOORHUGGER
	+NORADIUSDMG
	+NOBLOOD
	-TELESTOMP
	+LOOKALLAROUND
	+NOTARGETSWITCH
	+NONSHOOTABLE
	-SHOOTABLE
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+CANTLEAVEFLOORPIC
	+NOTRIGGER
	+NOTELESTOMP
	+DONTSPLASH
	+DONTCORPSE
	+NOBLOCKMAP
	+LAXTELEFRAGDMG
	+ALWAYSTELEFRAG
	+NOPAIN
	RenderStyle None
	//MaxDropOffHeight 128
	//MaxStepHeight 128
	+DONTTHRUST
	Speed 14
	Health 1000000
	DamageFactor "Normal", 0.0
	DamageFactor "DirectorKiller", 32767
	Species "Director"
	Radius 40
	Height 60
	States
	{
	Spawn:
		TNT1 A 0 
		TNT1 A 0 A_SetUserVar("user_limit",random(1,2))
	Idle:
		TNT1 A 0 A_ChangeFlag("FRIENDLY",0)
		//TNT1 A 0 //A_PrintBold("Medium")
		TNT1 AAAAA 1 A_Look
		Loop
	See:
	Wanderbout:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 1 A_Chase
		TNT1 AAAAAAAAA 1 A_Wander
		TNT1 A 0 A_JumpIf(user_t >= 35,4)
		TNT1 A 0 A_SetUserVar(user_t,user_t+1)
		TNT1 A 0 A_JumpIf(user_t < 35,2)
		TNT1 A 0 A_ChangeFlag("CANTLEAVEFLOORPIC",0)
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",0)
		TNT1 A 0 A_CheckBlock("Stuck")
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",1)
	Mobmaking:
		TNT1 A 0 A_JumpIfTargetInLOS("Wanderbout",360)
		TNT1 A 0 A_JumpIfCloser(250,"Wanderbout")
		TNT1 A 0 A_CheckSight(1)
		Goto Wanderbout
	MobChance:
		TNT1 A 0 Thing_ChangeTID(0,32669)
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,"Wanderbout")
		TNT1 A 0 A_SpawnItemEx("L4DJockey",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DSmoker",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DSpitter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DHunter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
		TNT1 A 0 A_SpawnItemEx("L4DCharger",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		Goto CountUp
	CountUp:
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 A 0 A_TakeFromSiblings("AEoDToken",100) //Don't need overflow because someone's using sv_unlimited_pickup...
		TNT1 A 0 A_GiveToSiblings("AEoDToken",1)
		TNT1 A 0 A_SetUserVar("user_dlimit",user_dlimit+1)
		TNT1 A 0 A_JumpIf(user_dlimit>=user_limit,"VanishC")
		Goto Wanderbout
	Vanish:
		TNT1 A 0 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,0),"VanishC")
	VanishHorde:
		TNT1 A 1
		Goto End
	VanishC:
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",1,1)
		TNT1 A 0 A_DropItem("Megasphere",1,32)
		TNT1 A 0 A_DropItem("SoulSphere",1,64)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerDirector",0,128)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck")
		TNT1 A 0 A_DropItem("L4DPackageSpawnerCheck",0,64)
		Goto End
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		Goto See
	Stuck:
		TNT1 A 1 A_JumpIf(user_dlimit > 0, "VanishC")
		Goto End
	Death:
	End:
		TNT1 A 0 A_TakeInventory("DirMediumChildCount",1,0,AAPTR_MASTER)
		Stop
	}
}

Actor L4DSmallMobRunner
{
	var int user_dlimit;
	var int user_max;
	var int user_check[2];
	var int user_nightmare;
	var int user_t;
	var int user_horde;
	var int user_tid;
	var int user_ambush;
	TeleFogSourceType None
	TeleFogDestType None
	Monster
	+THRUACTORS
	+FRIGHTENED
	+NOFEAR
	+DONTTHRUST
	-SOLID
	+NEVERTARGET
	-COUNTKILL
	+CANTSEEK
	+FLOORHUGGER
	+NORADIUSDMG
	+NOBLOOD
	-TELESTOMP
	+LOOKALLAROUND
	+NOTARGETSWITCH
	+NONSHOOTABLE
	-SHOOTABLE
	-CANUSEWALLS
	-CANPUSHWALLS
	-ACTIVATEMCROSS
	+CANTLEAVEFLOORPIC
	+NOTRIGGER
	+NOTELESTOMP
	+DONTSPLASH
	+DONTCORPSE
	+NOBLOCKMAP
	+LAXTELEFRAGDMG
	+ALWAYSTELEFRAG
	+NOPAIN
	RenderStyle None
	//MaxDropOffHeight 128
	//MaxStepHeight 128
	+DONTTHRUST
	Speed 14
	Health 1000000
	DamageFactor "Normal", 0.0
	DamageFactor "DirectorKiller", 32767
	Species "Director"
	Radius 20
	Height 64
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_tid",tid)
		TNT1 A 0 Thing_ChangeTID(0,0)
		TNT1 A 0 A_JumpIfInventory("HordeToken",1,1,AAPTR_MASTER)
		Goto Sp2
		TNT1 A 0 A_SetUserVar(user_horde,1)
		Goto Sp2
	Sp2:
		TNT1 A 0 A_SetUserVar("user_max",random(7,15))
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,1,AAPTR_PLAYER1)
		Goto Idle
		TNT1 A 0 A_SetUserVar("user_max",random(10,25))
		Goto Idle
	Idle:
		TNT1 AAAAA 1 A_Look
		Loop
	See:
	Wanderbout:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 0 A_JumpIf(user_horde == 1, "MobHorde")
		TNT1 AAAA 1 A_Chase("","")
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,4,AAPTR_PLAYER1)
		TNT1 AAAAA 1 A_Wander
		TNT1 A 0 A_JumpIf(user_t >= 35,4)
		TNT1 A 0 A_SetUserVar(user_t,user_t+1)
		TNT1 A 0 A_JumpIf(user_t < 35,2)
		TNT1 A 0 A_ChangeFlag("CANTLEAVEFLOORPIC",0)
	//Remember, we don't care about if we can get stuck in actors. We only want
	//to make sure lines aren't blocking us.
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",0)
		TNT1 A 0 A_CheckBlock("Stuck")
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",1)
	Mobmaking:
		TNT1 A 0 A_JumpIfTargetInLOS("Wanderbout",360)
		TNT1 A 0 A_JumpIfCloser(250,"Wanderbout")
		TNT1 A 0 A_CheckSight(1)
		Goto Wanderbout
	MobChance:
		TNT1 A 0 Thing_ChangeTID(0,32670)
		TNT1 A 0 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,1),"MobChanceNA")
		TNT1 A 0 A_SpawnItemEx("L4DCommonSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		//This is the only means to check if the spawn was successful or not.
		//If the TID is cleared, then continue onwards. If not, it failed and force
		//abortion so it can try again without penalty.
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 AA 0 A_SpawnItemEx("L4DCommonSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,225)
		Goto CountUp
	MobChanceNA:
		TNT1 A 0 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLEARCALLERTID)
		TNT1 A 0 A_JumpIf(tid!=0,"Wanderbout")
		TNT1 AA 0 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,225)
		Goto CountUp
	MobHorde:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,"MobHordeNightmare",AAPTR_PLAYER1)
		TNT1 A 1 A_Chase("","")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",0)
		TNT1 A 0 A_CheckBlock("Stuck")
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",1)
		TNT1 A 0 A_JumpIfTargetInLOS("MobHorde",360)
		TNT1 A 0 A_JumpIfCloser(250,"MobHorde")
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,128)
		TNT1 AAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,192)
		TNT1 A 0 A_TakeFromSiblings("AEoDToken",100)
		TNT1 A 0 A_GiveToSiblings("AEoDToken",1)
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",0,1)
		TNT1 A 0 A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DGeneralWeaponSpawner",0,224)
		Goto MHCont
		TNT1 A 0 A_DropItem("L4DJokeWeaponSpawner",0,224)
	MHCont:
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,128)
		TNT1 A 0 A_DropItem("SoulSphere",0,26)
		TNT1 AAAAAAA 0 A_DropItem("Medikit",0,224)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerDirector",1,128)
		Goto End
	MobHordeNightmare:
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"Death",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		TNT1 A 1 A_Chase("","")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",0)
		TNT1 A 0 A_CheckBlock("Stuck")
		TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",1)
		TNT1 A 0 A_JumpIfTargetInLOS("MobHordeNightmare",360)
		TNT1 A 0 A_JumpIfCloser(250,"MobHordeNightmare")
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,128)
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("L4DCommonSpawnerNA",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,192)
		TNT1 A 0 A_TakeFromSiblings("AEoDToken",100)
		TNT1 A 0 A_GiveToSiblings("AEoDToken",1)
		TNT1 A 0 A_JumpIf(user_tid == 666,"SpecialDrops")
		TNT1 A 0 A_DropItem("AEoD6SpellSpawner",1,1)
		TNT1 A 0 A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DGeneralWeaponSpawner",0,234)
		Goto MHNCont
		TNT1 A 0 A_DropItem("L4DJokeWeaponSpawner",0,234)
	MHNCont:
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,192)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,192)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,192)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,192)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerDirector",0,128)
		Goto End
	CountUp:
		TNT1 A 0 A_TakeFromSiblings("AEoDToken",100)
		TNT1 A 0 A_GiveToSiblings("AEoDToken",1)
		TNT1 A 0 A_SetUserVar("user_dlimit",user_dlimit+1)
		TNT1 A 0 A_JumpIf(user_dlimit>=user_max,"Vanish")
		Goto Wanderbout
	Vanish:
		TNT1 A 0 A_JumpIfInventory("NightmareToken",1,"VanishNightmare",AAPTR_PLAYER1)
		TNT1 A 1 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,0),1)
		//TNT1 A 0 //A_JumpIfInventory("DirectorMasterToken",1,1,AAPTR_MASTER)
		Goto Death
		TNT1 A 0 A_DropItem("L4DPackageSpawnerCheck")
		TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,230)
		//TNT1 AAAA 0 A_SpawnItemEx("Medikit",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,32)
		TNT1 AAA 0 A_SpawnItemEx("PowerupsSpawnerDirector",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,190)
		Goto End
	VanishNightmare:
		TNT1 A 0 A_Jump(32,2)
		TNT1 A 1 A_JumpIf(CheckClass("TheDirectorL4D",AAPTR_MASTER,0),1)
		Goto Death
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMCheck")
		TNT1 A 0 A_DropItem("SoulSphere",1,66)
		TNT1 A 0 A_DropItem("Megasphere",1,11)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerDirector",1,160)
		TNT1 A 0
		Goto End
	Frozen:
		TNT1 A 35 A_Stop
		TNT1 A 0 A_JumpIfInventory("RemoveDirector",1,"End",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("FreezeDirector",1,"Frozen",AAPTR_TARGET)
		Goto See
	SpecialDrops:
		TNT1 A 0 A_DropItem("BeastlyHealthDropper",0,256)
		TNT1 AAA 0 A_DropItem("PowerupsSpawnerDirector",0,128)
		TNT1 A 0 A_DropItem("RandomWeaponPowerAmmoGive",0,192)
		TNT1 A 0 A_DropItem("RandomWeaponPowerAmmoGive",0,128)
		TNT1 A 0 A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DGeneralWeaponSpawner",0,234)
		Goto End
		TNT1 A 0 A_DropItem("L4DJokeWeaponSpawner",0,234)
	Death:
	Stuck:
	End:
		TNT1 A 0 A_TakeInventory("DirSmallChildCount",1,0,AAPTR_MASTER)
		Stop
	}
}	


//There's a reason for this, because they can be picked up at any time unlike life seeds and such.
Actor BeastlyHealthDropper : RandomSpawner
{
	+NOTELESTOMP
	DropItem "FMegaSphere" 255 50
	DropItem "PentagramOfProtection" 255 25	//This shit gets insane after a few minutes.
	DropItem "MegaSphere" 255 15
}

Actor L4DPackageSpawnerMMCheck
{
	+NOINTERACTION
	+NOTONAUTOMAP
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMM",1)
		Stop
		TNT1 A 0 A_DropItem("L4DPackageSpawnerMMJ",1)
		Stop
	}
}

Actor L4DPackageSpawnerCheck
{
	+NOINTERACTION
	+NOTONAUTOMAP
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIfInventory("JokeToken",1,2,AAPTR_PLAYER1)
		TNT1 A 0 A_DropItem("L4DPackageSpawner",1)
		Stop
		TNT1 A 0 A_DropItem("L4DPackageSpawnerJ",1)
		Stop
	}
}

Actor L4DAmmoPack
{
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 
		TNT1 A 0 A_DropItem("ShellBoxReplacer",0,128)
		TNT1 A 0 A_DropItem("ClipBoxReplacer",0,128)
		TNT1 A 0 A_DropItem("RocketBoxReplacer",0,94)
		TNT1 A 0 A_DropItem("CellPackReplacer",0,94)
	HP:
		TNT1 AAAA 0 A_DropItem("Medikit",0,32)
		Stop
	}
}

Actor L4DAmmoPackMM
{
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,128)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,94)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,94)
	HP:
		TNT1 AAAA 0 A_DropItem("Medikit",0,32)
		Stop
	}
}

Actor L4DHealthPack
{
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(12,"MS")
		TNT1 A 0 A_Jump(24,"SS")
		TNT1 A 0 A_Jump(32,"Armor1","Armor2")
		TNT1 A 0 A_Jump(64,"HP2")
	HP1:		
		TNT1 AAAA 0 A_DropItem("Medikit",1,192)
		Goto AmmoPile
	HP2:
		TNT1 AAAAAAAAAA 0 A_DropItem("HealthBonus",1,192)
		Goto AmmoPile
	Armor1:
		TNT1 A 0 A_DropItem("L4DArmorSpawner",1)
		TNT1 A 0 A_Jump(32,"SS")
		Goto AmmoPile
	Armor2:
		TNT1 AAAAAAAAAA 0 A_DropItem("ArmorShardSpawner",1,192)
		TNT1 A 0 A_Jump(32,"SS")
		Goto AmmoPile
	SS:
		TNT1 A 0 A_DropItem("SoulSphere",1,256)
		Goto AmmoPile
	MS:
		TNT1 A 0 A_DropItem("MegaSphere",1,256)
		Goto AmmoPile
	AmmoPile:
		TNT1 A 0 A_DropItem("ShellBoxReplacer",0,48)
		TNT1 A 0 A_DropItem("ClipBoxReplacer",0,48)
		TNT1 A 0 A_DropItem("RocketBoxReplacer",0,24)
		TNT1 A 0 A_DropItem("CellPackReplacer",0,24)
		Stop
	}
}

Actor L4DHealthPackMM
{
	var int user_d;
	+NOINTERACTION
	+NOTELESTOMP
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(12,"MS")
		TNT1 A 0 A_Jump(24,"SS")
		TNT1 A 0 A_Jump(32,"Armor1","Armor2")
		TNT1 A 0 A_Jump(64,"HP2")
	HP1:		
		TNT1 AAAAAAAA 0 A_DropItem("Medikit",1,128)
		Goto AmmoPile
	HP2:
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_DropItem("HealthBonus",1,128)
		Goto AmmoPile
	Armor1:
		TNT1 A 0 A_DropItem("L4DArmorSpawner",1)
		TNT1 A 0 A_Jump(48,"SS")
		Goto AmmoPile
	Armor2:
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_DropItem("ArmorShardSpawner",1,128)
		TNT1 A 0 A_Jump(48,"SS")
		Goto AmmoPile
	SS:
		TNT1 A 0 A_DropItem("SoulSphere",1,256)
		Goto AmmoPile
	MS:
		TNT1 A 0 A_DropItem("MegaSphere",1,256)
		Goto AmmoPile
	AmmoPile:
		TNT1 AA 0 A_DropItem("ShellBoxReplacer",0,24)
		TNT1 AA 0 A_DropItem("ClipBoxReplacer",0,24)
		TNT1 AA 0 A_DropItem("RocketBoxReplacer",0,12)
		TNT1 AA 0 A_DropItem("CellPackReplacer",0,12)
		Stop
	}
}

Actor L4DArmorSpawner : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "VestArmor" 255 10
	DropItem "SmallArmor" 255 8
	DropItem "MediumArmor" 255 4
	DropItem "HeavyArmor" 255 1
}

Actor L4DPackageSpawner : RandomSpawner
{
	//Each one of them have an extreme biasity towards their name.
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "L4DAmmoPack" 255 28
	DropItem "L4DHealthPack" 255 14
	DropItem "L4DGeneralWeaponSpawner" 255 12
}

Actor L4DPackageSpawnerMM : RandomSpawner
{
	//Each one of them have an extreme biasity towards their name.
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "L4DAmmoPackMM" 255 16
	DropItem "L4DHealthPackMM" 255 12
	DropItem "L4DGeneralWeaponSpawner" 255 14
}

Actor L4DPackageSpawnerJ : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "L4DAmmoPack" 255 28
	DropItem "L4DHealthPack" 255 14
	DropItem "L4DJokeWeaponSpawner" 255 12
}

Actor L4DPackageSpawnerMMJ : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "L4DAmmoPackMM" 255 16
	DropItem "L4DHealthPackMM" 255 12
	DropItem "L4DJokeWeaponSpawner" 255 12
}

Actor HordeToken : Inventory
{	Inventory.MaxAmount 6	}

Actor L4DGeneralWeaponSpawner : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "Slot1Weps" 255 16
	DropItem "Slot2Weps" 255 32
	DropItem "Slot3Weps" 255 32
	DropItem "Slot4Weps" 255 32
	DropItem "Slot5Weps" 255 24
	DropItem "Slot6Weps" 255 16
	DropItem "Slot7Weps" 255 12
	DropItem "Slot8Weps" 255  8
	DropItem "Slot9Weps" 255  4
	DropItem "Slot0Weps" 255  2
}

Actor L4DJokeWeaponSpawner : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "Slot1Weps"  255 16
	DropItem "Slot2WepsJ" 255 32
	DropItem "Slot3Weps"  255 32
	DropItem "Slot4WepsJ" 255 32
	DropItem "Slot5Weps"  255 24
	DropItem "Slot6WepsJ" 255 16
	DropItem "Slot7Weps"  255 12
	DropItem "Slot8WepsJ" 255  8
	DropItem "Slot9WepsJ" 255  4
	DropItem "Slot0Weps"  255  2
}

Actor Slot1Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDProxDetonatorItem" 	255 10
	DropItem "AEoDTNTItem" 				255 10
	DropItem "AEoDHandgrenadeItem" 		255  5
	DropItem "AEoDStickyMineItem" 		255  5
	DropItem "AEoDRemoteDetonatorItem" 	255  5
	DropItem "AEoDGlyphItem" 			255  2
	DropItem "AEoDBoneStaffItem" 		255  2 //Added as of 6.03PR3. Why? Demon Orb. That is all.
	DropItem "AEoDJackBombItem" 		255  1
}

Actor Slot2Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDKatanaItem" 			255 10
	DropItem "AEoDMedievalAxeItem" 		255  8
	DropItem "AEoDGauntletsItem" 		255  8
	DropItem "AEoDVorpalBladeItem" 		255  8
	DropItem "AEoDScytheItem" 			255  8
	DropItem "AEoDPainkillerItem" 		255  4
	DropItem "AEoDChainsawItem" 		255  1
	DropItem "AEoDStroggArmSawItem" 	255  1
}

Actor Slot2WepsJ : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDKatanaItem" 		255 10
	DropItem "AEoDMedievalAxeItem" 	255  8
	DropItem "AEoDGauntletsItem" 	255  8
	DropItem "AEoDVorpalBladeItem" 	255  8
	DropItem "AEoDSpyKnifeItem" 	255  8
	DropItem "AEoDScytheItem" 		255  8
	DropItem "AEoDPainkillerItem" 	255  4
	DropItem "AEoDChainsawItem" 	255  1
	DropItem "AEoDStroggArmSawItem" 255  1
}

Actor Slot3Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDBerettaItem" 			255 40
	DropItem "AEoDBlasterItem" 			255 40
	DropItem "AEoDGargoyleWandItem" 	255 40
	DropItem "AEoDSPASShotgunItem" 		255 40
	DropItem "AEoDMac10Item" 			255 40
	DropItem "AEoDFlaregunItem" 		255 40
	DropItem "AEoDMP5Item" 				255 20
	DropItem "AEoDGoldenColtsItem" 		255 20
	DropItem "AEoDCrossbowItem" 		255 20
	DropItem "AEoDTekbowItem" 			255 20
	DropItem "AEODDesertEagleItem"		255 20
	DropItem "AEoDAssaultShotgunItem" 	255 10
	DropItem "AEoDShellShockerItem" 	255  5
}

Actor Slot4Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDBloodShotgunItem"  255 70
	DropItem "AEoDPKShotgunItem" 	 255 70
	DropItem "AEoDBioRifleItem" 	 255 70
	DropItem "AEoDCMRifleItem" 		 255 70
	DropItem "AEoDC14Item" 			 255 70
	DropItem "AEoDCrystalGunItem" 	 255 40
	DropItem "AEoDVoodooDollItem" 	 255 40
	DropItem "AEoDBoltGunItem" 		 255 40
	DropItem "AEoDCarbineItem" 		 255 40
	DropItem "AEoDSerpentStaffItem"	 255 40
	DropItem "AEoDRiotgunItem" 		 255 30
	DropItem "AEoDDisruptorItem" 	 255 20
	DropItem "AEoDCombatShotgunItem" 255 20
	DropItem "AEoDDragonClawItem"	 255 20
	DropItem "AEoDShockRifleItem"	 255 20
	DropItem "AEoDHeadHunterItem"	 255  5
	DropItem "AEoDDemonOrbItem"		 255  3
	DropItem "AEoDAWPItem"			 255  1
}

Actor Slot4WepsJ : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDBloodShotgunItem"  255 70
	DropItem "AEoDPKShotgunItem"	 255 70
	DropItem "AEoDBioRifleItem" 	 255 70
	DropItem "AEoDCMRifleItem" 		 255 70
	DropItem "AEoDC14Item" 			 255 70
	DropItem "AEoDCrystalGunItem" 	 255 40
	DropItem "AEoDVoodooDollItem" 	 255 40
	DropItem "AEoDBoltGunItem" 		 255 40
	DropItem "AEoDCarbineItem" 		 255 40
	DropItem "AEoDSerpentStaffItem"  255 40
	DropItem "AEoDRiotgunItem" 		 255 30
	DropItem "AEoDBisonItem" 		 255 20
	DropItem "AEoDDisruptorItem" 	 255 20
	DropItem "AEoDCombatShotgunItem" 255 20
	DropItem "AEoDDragonClawItem" 	 255 20
	DropItem "AEoDShockRifleItem" 	 255 20
	DropItem "AEoDHeadHunterItem" 	 255  5
	DropItem "AEoDDemonOrbItem" 	 255  3
	DropItem "AEoDAWPItem" 			 255  1
}

Actor Slot5Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDPlasmaRifleItem" 		255 100
	DropItem "AEoDFreezethrowerItem"	255 100
	DropItem "AEoDHyperBlasterItem" 	255 100
	DropItem "AEoDMinigunItem" 			255 100
	DropItem "AEoDIceStormRodItem" 		255  80
	DropItem "AEoDDemonStaffItem" 		255  80
	DropItem "AEoDGaussGunItem" 		255  80
	DropItem "AEoDHornetGunItem"		255  80
	DropItem "AEoDSkullRodItem" 		255  80
	DropItem "AEoDLasergunItem" 		255  50
	DropItem "AEoDFusionChaingunItem" 	255  50
	DropItem "AEoDFlamethrowerItem" 	255  40
	DropItem "AEoDPeaceMakerItem" 		255  40
	DropItem "AEoDNailGunItem" 			255  40
	DropItem "AEoDNailgunMK2Item" 		255  40
	DropItem "AEoDWyvernHeartItem" 		255   5
}

Actor Slot6Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDGrenadeLauncherItem" 		255 150
	DropItem "AEoDRocketLauncherItem" 		255 150
	DropItem "AEoDMissileLauncherItem" 		255 150
	DropItem "AEoDPMFLayerItem" 			255 150
	DropItem "AEoDMeteoriteRodItem" 		255 150
	DropItem "AEoDFireStormSpellItem" 		255  80
	DropItem "AEoDLightningRodItem" 		255  80
	DropItem "AEoDScreamerItem" 			255  80
	DropItem "AEoDRazorItem" 				255  50
	DropItem "AEoDScarabStaffItem" 			255  20
	DropItem "AEoDHowitzerItem" 			255   5
	DropItem "AEoDEnlightenmentStaffItem" 	255   5
	DropItem "AEoDWarAxeItem" 				255   2
}

Actor Slot6WepsJ : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDGrenadeLauncherItem" 		255 150
	DropItem "AEoDRocketLauncherItem" 		255 150
	DropItem "AEoDMissileLauncherItem" 		255 150
	DropItem "AEoDPMFLayerItem" 			255 150
	DropItem "AEoDMeteoriteRodItem" 		255 150
	DropItem "AEoDCowManglerItem" 			255  80
	DropItem "AEoDFireStormSpellItem" 		255  80
	DropItem "AEoDLightningRodItem" 		255  80
	DropItem "AEoDScreamerItem" 			255  80
	DropItem "AEoDRazorItem" 				255  50
	DropItem "AEoDScarabStaffItem" 			255  20
	DropItem "AEoDHowitzerItem" 			255   5
	DropItem "AEoDEnlightenmentStaffItem" 	255   5
	DropItem "AEoDWarAxeItem" 				255   2
}

Actor Slot7Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDPulsegunItem" 			255 100
	DropItem "AEoDEidechsenesserItem" 		255 100
	DropItem "AEoDThunderBoltItem" 			255 100
	DropItem "AEoDPrismaCannonItem" 		255 100
	DropItem "AEoDElectroDriverItem" 		255 100
	DropItem "AEoDTeslaCannonItem" 			255 100
	DropItem "AEoDIceWandItem" 				255  50
	DropItem "AEoDIncineratorItem" 			255  50
	DropItem "AEoDDevastatorItem" 			255  50
	DropItem "AEoDFragLauncherItem" 		255  50
	DropItem "AEoDRipperGunItem" 			255  50
	DropItem "AEoDLifeLeechItem" 			255  30
	DropItem "AEoDConcussionLauncherItem" 	255  50
	DropItem "AEoDPlutoniumCannonItem" 		255  50
	DropItem "AEoDGuardianHeadItem" 		255  30
	DropItem "AEoDRaptorItem" 				255  20
	DropItem "AEoDHailStormItem" 			255  20
	DropItem "AEoDVulcanCannonItem" 		255  20
	DropItem "AEoDPerforatorItem" 			255   5
	DropItem "AEoDArbalestItem" 			255   1
}

Actor Slot8Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDMindBlowerItem" 		 255 150
	DropItem "AEoDStakeGunItem" 		 255 150
	DropItem "AEoDPulseCannonItem" 		 255 150
	DropItem "AEoDFlakCannonItem" 		 255 100
	DropItem "AEoDRailGunItem" 			 255 100
	DropItem "AEoDBoneShotgunItem" 		 255  50
	DropItem "AEoDPKSMGItem" 			 255  50
	DropItem "AEoDIonCannonItem"		 255  50
	DropItem "AEoDHEGrenadeLauncherItem" 255  50
	DropItem "AEoDPhalanxItem" 			 255  50
	DropItem "AEoDBoneDusterItem" 		 255  50
	DropItem "AEoDSniperRailgunItem" 	 255  50
	DropItem "AEoDLichStaffItem" 		 255  50
	DropItem "AEoDMorserLauncherItem" 	 255  50
	DropItem "AEoDFlailGunItem" 		 255  50
	DropItem "AEoDDsparilStaffItem" 	 255  30
	DropItem "AEoDDeathRayItem" 		 255  30
	DropItem "AEoDNapalmLauncherItem" 	 255  30
	DropItem "AEoDIncendiaryItem" 		 255  30
	DropItem "AEoDTomeOfSetItem" 		 255  20
	DropItem "AEoDRavenStaffItem" 		 255  10
	DropItem "AEoDShredderStaffItem" 	 255  10
}

Actor Slot8WepsJ : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDMindBlowerItem" 			255 150
	DropItem "AEoDStakeGunItem" 			255 150
	DropItem "AEoDPulseCannonItem" 			255 150
	DropItem "AEoDFlakCannonItem" 			255 100
	DropItem "AEoDRailGunItem" 				255 100
	DropItem "AEoDGutFlingerItem" 			255  80
	DropItem "AEoDBoneShotgunItem" 			255  50
	DropItem "AEoDPKSMGItem" 				255  50
	DropItem "AEoDIonCannonItem" 			255  50
	DropItem "AEoDHEGrenadeLauncherItem" 	255  50
	DropItem "AEoDPhalanxItem" 				255  50
	DropItem "AEoDBoneDusterItem" 			255  50
	DropItem "AEoDSniperRailgunItem" 		255  50
	DropItem "AEoDLichStaffItem" 			255  50
	DropItem "AEoDMorserLauncherItem" 		255  50
	DropItem "AEoDFlailGunItem" 			255  50
	DropItem "AEoDDsparilStaffItem" 		255  30
	DropItem "AEoDDeathRayItem" 			255  30
	DropItem "AEoDNapalmLauncherItem" 		255  30
	DropItem "AEoDIncendiaryItem" 			255  30
	DropItem "AEoDTomeOfSetItem" 			255  20
	DropItem "AEoDRavenStaffItem" 			255  10
	DropItem "AEoDShredderStaffItem" 		255  10
}

Actor Slot9Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDSoulReaverItem" 		255 100
	DropItem "AEoDRailGunPrototypeItem" 255 100
	DropItem "AEoDHERocketLauncherItem" 255 100
	DropItem "AEoDPrototypeItem" 		255  50
	DropItem "AEoDNecrossbowItem" 		255  50
	DropItem "AEoDSoulleechItem" 		255  50
	DropItem "AEoDProteusDynamoItem" 	255  25
	DropItem "AEoDPhotonCannonItem" 	255  25
	DropItem "AEoDHellCoreItem" 		255  12
	DropItem "AEoDHellRootItem" 		255  12
	DropItem "AEoDBouncerItem" 			255  12
	DropItem "AEoDWraithVergeItem" 		255   6
	DropItem "AEoDDrakanItem" 			255   6
	DropItem "AEoDBloodScourgeItem" 	255   6
	DropItem "AEoDCrawlerItem" 			255   6
	DropItem "AEoDGluonGunItem" 		255   6
	DropItem "AEoDTempestStaffItem" 	255   3
}

Actor Slot9WepsJ : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDSoulReaverItem" 		255 100
	DropItem "AEoDRailGunPrototypeItem" 255 100
	DropItem "AEoDHERocketLauncherItem" 255 100
	DropItem "AEoDPopcornGunItem" 		255  50
	DropItem "AEoDPrototypeItem" 		255  50
	DropItem "AEoDNecrossbowItem" 		255  50
	DropItem "AEoDSoulleechItem" 		255  50
	DropItem "AEoDProteusDynamoItem" 	255  25
	DropItem "AEoDPhotonCannonItem" 	255  25
	DropItem "AEoDHellCoreItem" 		255  12
	DropItem "AEoDHellRootItem" 		255  12
	DropItem "AEoDBouncerItem" 			255  12
	DropItem "AEoDWraithVergeItem" 		255   6
	DropItem "AEoDDrakanItem" 			255   6
	DropItem "AEoDBloodScourgeItem" 	255   6
	DropItem "AEoDCrawlerItem" 			255   6
	DropItem "AEoDGluonGunItem" 		255   6
	DropItem "AEoDTempestStaffItem" 	255   3
}

Actor Slot0Weps : RandomSpawner
{
	+NOTELESTOMP
	+NOTONAUTOMAP
	DropItem "AEoDPenetratorItem" 		255 32
	DropItem "AEoDBioForceGunItem" 		255 32
	DropItem "AEoDBFG9000Item" 			255 16
	DropItem "AEoDRedeemerItem" 		255 16
	DropItem "AEoDSoulReaperItem" 		255  8
	DropItem "AEoDDarkMatterGunItem" 	255  8
	DropItem "AEoDFusionCannonItem" 	255  8
	DropItem "AEoDQSGItem" 				255  8
	DropItem "AEoDApocalypseItem" 		255  2
	DropItem "AEoDNovaItem" 			255  2
	DropItem "AEoDVolcanoCannonItem" 	255  2
	DropItem "AEoDDarkFlameStaffItem" 	255  2
	DropItem "AEoDInfernoItem" 			255  2
	DropItem "AEoDDeathsHandItem" 		255  1
	DropItem "AEoDHandOfGodItem"		255  1
}