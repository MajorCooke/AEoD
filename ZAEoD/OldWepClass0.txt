Class HarmlessPuff : Actor
{
	Default
	{
		+PAINLESS
		+BLOODLESSIMPACT
		+PUFFONACTORS
		+HITTRACER
		+CANNOTPUSH
		Damage 0;
	}
	States
	{
	Spawn:
		TNT1 A 2;
		Stop;
	}
}

Class SoulReaperBeam : Actor
{
	const minDist = 96;
	const TurnAmount = 2;
	int seekflags;
	
	Actor closest;
	double closestDist;
	bool skip;
	int count;
	
	Default
	{
		Projectile;
		+SEEKERMISSILE
		+DONTREFLECT
		+FOILINVUL
		+HITTRACER
		+PAINLESS
		+SCREENSEEKER
		+DONTSPLASH
		+NOTIMEFREEZE
		+NODAMAGETHRUST +CANNOTPUSH
		+EXTREMEDEATH
		Speed 4;
		Radius 4;
		Height 2;
		Damage 0;
		DamageType "SoulReaperPull";
		//MaxTargetRange 4
		RenderStyle "None";
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		if (target)	pitch = target.pitch;
	}
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay 
		{
			skip = false;
			int maxTime = 30;
			closestDist = 500;
			closest = null;
			
			// Find soul fragments first.
			let it = ActorIterator.Create(SoulFragmentID, "DMSoulFragment");
			Actor mo;
			while (mo = it.Next())
			{
				double dist = Distance3D(mo);
				
				if (dist > closestDist)
					continue;
				
				if (!CheckSight(mo, SF_IGNOREVISIBILITY|SF_IGNOREWATERBOUNDARY))
					continue;
					
				closest = mo;
				closestDist = dist;
			}
			
			if (!closest)
			{
				if (target) 
				{
					tracer = target.AimTarget();
					closest = tracer;
				}
			}
			if (closest)
			{
				bTHRUACTORS = true;
				while (Distance3D(closest) > minDist)
				{
					if (!closest || maxTime < 1)		
					{
						skip = true;
						return Destroy();
					}
					A_SpawnItemEx("SoulReaperBeamTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
					A_Face(closest,TurnAmount,TurnAmount,0,0,FAF_MIDDLE);
					A_Warp(DefPtr, cos(pitch) * speed, 0, -sin(pitch) * speed, 0, WARPF_NOCHECKPOSITION);
					maxTime--;
				}
				
				let ms = closest.Spawn("DMSoulMissile", closest.pos);
				if (ms)
				{
					ms.target = target;
					ms.tracer = tracer;
					ms.master = master;
					ms.translation = closest.translation;
					if (closest.GetClass() == "DMSoulFragment")
						closest.Destroy();
				}
				skip = true;
			}
			else
			{
				
				while (maxTime > 0)
				{
					A_SpawnItemEx("SoulReaperBeamTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
					A_SeekerMissile(1,1,SMF_PRECISE|SMF_LOOK,256,4);
					if (tracer)
						A_FaceTracer(10,10,0,0,FAF_MIDDLE);
					
					
					if (!A_Warp(AAPTR_DEFAULT,cos(pitch) * speed, 0, -sin(pitch) * speed,0,0,"Null") || 
						(tracer && Distance3D(tracer) < minDist))
					{
						if (abs(vel.X) + abs(vel.Y) != 0)
							A_FaceMovementDirection();
						A_Stop();
						if (!tracer)
						{
							A_CheckLOF("Null",
								CLOFF_JUMPENEMY|CLOFF_JUMPNONHOSTILE|CLOFF_JUMPOBJECT|CLOFF_SKIPFRIEND|
								CLOFF_SETTRACER|CLOFF_NOAIM|CLOFF_FROMBASE|CLOFF_ALLOWNULL|CLOFF_MUSTBESHOOTABLE,
								speed * 2,0,angle,0,height / 2,0,AAPTR_NULL,0);
						}
						bNOINTERACTION = true;
						skip = (!tracer);
						return Destroy();
					}
					maxTime--;
				}
			}
		}
		Stop;
	Death:
		TNT1 A 1
		{
			bNOINTERACTION = true;
			if (!tracer && target)
			{
				tracer = target.AimTarget();
				if (!tracer) skip = true;
				/*
				Actor puff = LineAttack(angle, speed * 2, pitch, 0, 'Normal', "HarmlessPuff", LAF_NOINTERACT|LAF_NORANDOMPUFFZ|LAF_NOIMPACTDECAL|LAF_OVERRIDEZ,null,height / 2.0);
				if (puff)	
				{
					tracer = puff.tracer;
					puff.Destroy();
				}
				*/
			}
			else skip = true;
			
			if (skip) return Destroy();
			
			if (tracer && tracer.bSHOOTABLE && tracer.health > 0)
			{
				int dmg = random(1,2);
				Vector3 SoulPos = tracer.pos;
					SoulPos.z = pos.z;
					
				int ret = tracer.DamageMobj(self, target, dmg, "SoulReaperPull", DMG_FOILINVUL | DMG_THRUSTLESS);
				if (!tracer || tracer.health < 1)
				{
					let ms = tracer.Spawn("DMSoulMissile", SoulPos);
					if (ms)
					{
						ms.target = target;
						ms.master = master;
						ms.tracer = tracer;
					}
				}
			}
		}
		Stop;
	}
}